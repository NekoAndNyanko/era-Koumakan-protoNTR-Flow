@CUSTOM_CHARAMAKE(ARG)
;NOは適当に設定
NO:ARG = 0

;性別(1=女性 2= 男性 3=ふたなり)
TALENT:ARG:2 = 1
SIF ARG == 0
	TALENT:ARG:2 = 2
;气力、体力
MAXBASE:ARG:0 = 2000
BASE:ARG:0 = MAXBASE:ARG:0
MAXBASE:ARG:1 = 2000
BASE:ARG:1 = MAXBASE:ARG:1

CALL NAME_CUSTOM(ARG)

CALL CUSTOM_TERMINAL(ARG)

@CUSTOM_TERMINAL(ARG)
;排他処理
IF HAS_VAGINA(ARG)
	ABL:ARG:断背之气 = 0
	ABL:ARG:搞基中毒 = 0
	EXP:ARG:搞基经验 = 0
	SIF !(HAS_PENIS(ARG))
		EXP:ARG:射精经验 = 0
ELSE
	ABL:ARG:Ｖ感觉 = 0
	ABL:ARG:百合之气 = 0
	ABL:ARG:百合中毒 = 0
	EXP:ARG:百合经验 = 0
	EXP:ARG:喷乳经验 = 0
	EXP:ARG:Ｖ经验 = 0
	EXP:ARG:乳交经验 = 0
	EXP:ARG:Ｖ性交经验 = 0
	EXP:ARG:Ｖ扩张经验 = 0
	TALENT:ARG:Ｖ感度 = 0
	TALENT:ARG:处女 = 0
	TALENT:ARG:泌乳体质 = 0
	TALENT:ARG:胸部尺寸 = 0
ENDIF
IF HAS_PENIS(ARG)
	MAXBASE:ARG:精力 = 1000
	BASE:ARG:精力 = 1000
ELSE
	TALENT:ARG:童贞 = 0
	MAXBASE:ARG:精力 = 0
	BASE:ARG:精力 = 0
ENDIF
IF TALENT:ARG:处女 == 1
	ABL:ARG:Ｖ感觉 = 0
	EXP:ARG:Ｖ经验 = 0
	EXP:ARG:Ｖ性交经验 = 0
	EXP:ARG:Ｖ扩张经验 = 0
ELSEIF HAS_VAGINA(ARG) && EXP:ARG:Ｖ经验 == 0
	EXP:ARG:Ｖ经验 = 1
ENDIF

DRAWLINE
PRINTSL NAME:ARG
PRINT 体力:
BAR BASE:ARG:0,MAXBASE:ARG:0,16
PRINTFORM ({BASE:ARG:0,4}/{MAXBASE:ARG:0,4}) 
PRINT 气力:
BAR BASE:ARG:1,MAXBASE:ARG:1,16
PRINTFORM ({BASE:ARG:1,4}/{MAXBASE:ARG:1,4})
PRINTL 
CALL PRINT_STATE_ABL(ARG)
PRINTL 
CALL PRINT_STATE_TALENT(ARG)
PRINTL 
CALL PRINT_STATE_EXP(ARG)
PRINTL
DRAWLINE
;最終決定
IF LOCAL:100 == 1000
	DRAWLINE
	PRINTL これで本当によろしいですか？
	;名前の設定
ELSEIF LOCAL:100 == 1001
	CALL NAME_CUSTOM(ARG)
	LOCAL:100 = 0
	RESTART
	;ベース設定
ELSEIF LOCAL:100 == 1002
	CALL BASE_CUSTOM(ARG)
	;素質設定
ELSEIF LOCAL:100 == 1003
	CALL TALENT_CUSTOM(ARG,LOCAL:101)
	;能力設定
ELSEIF LOCAL:100 == 1004
	CALL ABL_CUSTOM(ARG)
	;経験設定
ELSEIF LOCAL:100 == 1005
	CALL EXP_CUSTOM(ARG)
ELSEIF LOCAL:100 == 1006
	CALL COPY_CUSTOM(ARG)
ENDIF

DRAWLINE
PRINTFORML [1000] - \@(LOCAL:100 == 1000) ? 是的 # 決定\@  [1001] - 名前の変更  [1002] - 体力、气力の設定
PRINTL [1003] - 素質の設定  [1004] - 能力の設定  [1005] - 経験の設定  [1006] - 素質コピー
INPUT
IF RESULT == 1000 && LOCAL:100 == 1000
	;	SIF NO:MASTER == 0
	RETURN 0
ELSEIF RESULT >= 1000
	LOCAL:100 = RESULT
	RESTART
ENDIF
;ベース設定
IF LOCAL:100 == 1002
	IF RESULT == 0 && MAXBASE:ARG:0 > 1000
		MAXBASE:ARG:0 -= 100
	ELSEIF RESULT == 1 && MAXBASE:ARG:0 < 3000
		MAXBASE:ARG:0 += 100
	ELSEIF RESULT == 2 && MAXBASE:ARG:1 > 1000
		MAXBASE:ARG:1 -= 100
	ELSEIF RESULT == 3 && MAXBASE:ARG:1 < 3000
		MAXBASE:ARG:1 += 100
	ENDIF
	BASE:ARG:0 = MAXBASE:ARG:0
	BASE:ARG:1 = MAXBASE:ARG:1
	RESTART
	;素質設定
ELSEIF LOCAL:100 == 1003
	;ページ送り
	SIF RESULT == 998 && LOCAL:101
		LOCAL:101 --
	SIF RESULT == 999
		LOCAL:101 ++
	SIF RESULT < 0 || RESULT > 500 || TALENTNAME:RESULT == ""
		RESTART
	;陥落素質ほか
	;性別
	IF RESULT == 2
		IF TALENT:ARG:RESULT == 3
			TALENT:ARG:RESULT = 1
		ELSE
			TALENT:ARG:RESULT ++
		ENDIF
		;性別嗜好
	ELSEIF RESULT == 81
		IF TALENT:ARG:RESULT == 3
			TALENT:ARG:RESULT = -1
		ELSE
			TALENT:ARG:RESULT ++
		ENDIF
		;オンオフ型の素質
	ELSEIF TALENTNAME:RESULT == GET_TALENTNAME(RESULT,1) || RESULT == 1
		TALENT:ARG:RESULT = !TALENT:ARG:RESULT
		;量的素質
	ELSE
		;性別
		IF LOCAL == 2
			IF TALENT:ARG:RESULT == 3
				TALENT:ARG:RESULT = 1
			ELSE
				TALENT:ARG:RESULT ++
			ENDIF
			;端まで行くと-1を掛ける
		ELSEIF STRLENS(GET_TALENTNAME(RESULT,TALENT:ARG:RESULT + 1)) || TALENT:ARG:RESULT == -1
			TALENT:ARG:RESULT ++
		ELSE
			TALENT:ARG:RESULT = -1 * TALENT:ARG:RESULT
		ENDIF
	ENDIF
	;能力設定
ELSEIF LOCAL:100 == 1004
	LOCAL = RESULT / 2
	SIF LOCAL >= 100
		RESTART
	SIF !STRLENS(ABLNAME:LOCAL)
		RESTART
	;同性愛関係
	IF !(HAS_VAGINA(ARG)) && (LOCAL == 17 || LOCAL == 32)
		RESTART
	ELSEIF HAS_VAGINA(ARG) && (LOCAL == 18 || LOCAL == 33)
		RESTART
	ENDIF
	;中毒関係
	SIF LOCAL >= 30
		RESTART
	;奇数(+)
	IF RESULT % 2
		IF ABL:ARG:LOCAL >= 2
			RESTART
		ELSE
			ABL:ARG:LOCAL ++
		ENDIF
		;偶数(-)
	ELSE
		IF ABL:ARG:LOCAL <= 0
			RESTART
		ELSE
			ABL:ARG:LOCAL --
		ENDIF
	ENDIF
ELSEIF LOCAL:100 == 1005
	LOCAL = RESULT
	SIF LOCAL >= 100
		RESTART
	SIF !STRLENS(EXPNAME:LOCAL)
		RESTART
	;同性愛関係
	IF !(HAS_VAGINA(ARG)) && LOCAL == 51
		RESTART
	ELSEIF HAS_VAGINA(ARG) && LOCAL == 52
		RESTART
	ENDIF
	PRINTFORM %EXPNAME:LOCAL%を入力してください      
	PRINTBUTTON "[キャンセル]",-1
	INPUT
	SIF RESULT < 0
		RESTART
	EXP:ARG:LOCAL = MIN(RESULT,50)
ELSEIF LOCAL:100 == 1006

ENDIF
RESTART

;名前の設定
@NAME_CUSTOM(ARG)
PRINTL キャラのNAMEを入力してください
IF STRLENS(NAME:ARG)
	PRINTFORM 現在のNAME：%NAME:ARG%      
	PRINTBUTTON "[変更しない]",""
	PRINTL
ENDIF
INPUTS
IF ARG == 0 && RESULTS == ""
	NAME:ARG = %NAME:ARG%
ELSE
	NAME:ARG = %RESULTS%
ENDIF

PRINTL キャラのCALLNAMEを入力してください
IF STRLENS(CALLNAME:ARG)
	PRINTFORM 現在のCALLNAME：%CALLNAME:ARG%      
	PRINTBUTTON "[変更しない]",""
	PRINTL
ENDIF
INPUTS
IF ARG == 0 && RESULTS == ""
	CALLNAME:ARG = %CALLNAME:ARG%
ELSE
	CALLNAME:ARG = %RESULTS%
ENDIF

;ベースの設定
@BASE_CUSTOM(ARG)
PRINT 体力:
BAR BASE:ARG:0,MAXBASE:ARG:0,16
PRINTFORM ({BASE:ARG:0,4}/{MAXBASE:ARG:0,4})
IF BASE:ARG:0 != 1000
	PRINTBUTTON "[－]", 0
ELSE
	SETCOLOR 色設定_設定_無効化
	PRINT [－]
	RESETCOLOR
ENDIF
IF BASE:ARG:0 != 3000
	PRINTBUTTON "[＋]", 1
ELSE
	SETCOLOR 色設定_設定_無効化
	PRINT [＋]
	RESETCOLOR
ENDIF
PRINTL
PRINT 气力:
BAR BASE:ARG:1,MAXBASE:ARG:1,16
PRINTFORM ({BASE:ARG:1,4}/{MAXBASE:ARG:1,4})
IF BASE:ARG:1 != 1000
	PRINTBUTTON "[－]", 2
ELSE
	SETCOLOR 色設定_設定_無効化
	PRINT [－]
	RESETCOLOR
ENDIF
IF BASE:ARG:1 != 3000
	PRINTBUTTON "[＋]", 3
ELSE
	SETCOLOR 色設定_設定_無効化
	PRINT [＋]
	RESETCOLOR
ENDIF
PRINTL
;素質の設定
@TALENT_CUSTOM(ARG,ARG:1)
VARSET LOCAL
FOR LOCAL,ARG:1 * 40,40 + ARG:1 * 40
	IF LOCAL % 10 == 0
		PRINTL
		SIF LOCAL:1 % 2
			PRINTL
		LOCAL:1 = 0
	ENDIF
	;陥落素質ほか
	;TALENT.CSVに登録されていない
	SIF TALENTNAME:LOCAL == ""
		CONTINUE
	;オンオフ型の素質(TALENTNAMEと素質表示が一致)
	IF TALENTNAME:LOCAL == GET_TALENTNAME(LOCAL,1)
		SIF !TALENT:ARG:LOCAL
			SETCOLOR 色設定_テキスト_無効
		LOCALS = [{LOCAL,3}] - %TALENTNAME:LOCAL%
		;童贞
	ELSEIF LOCAL == 1
		SIF TALENT:ARG:LOCAL
			SETCOLOR 色設定_テキスト_無効
		LOCALS = [{LOCAL,3}] - %TALENTNAME:LOCAL%
		;量的素質
	ELSE
		SIF !TALENT:ARG:LOCAL
			SETCOLOR 色設定_テキスト_無効
		LOCALS =  [{LOCAL,3}] - %TALENTNAME:LOCAL%
		IF STRLENS(GET_TALENTNAME(LOCAL,TALENT:ARG:LOCAL))
			LOCALS:1 =  …【%GET_TALENTNAME(LOCAL,TALENT:ARG:LOCAL)%】
		ELSE
			LOCALS:1 = …
		ENDIF
		LOCALS += LOCALS:1
	ENDIF
	PRINTFORM %LOCALS,35,LEFT%
	LOCAL:1 ++
	SIF LOCAL:1 % 2 == 0
		PRINTL
	RESETCOLOR
NEXT
PRINTL

IF ARG:1 != 0
	PRINTBUTTON "[998] - 前のページ                 ",998
ELSE
	SETCOLOR 色設定_テキスト_無効
	PRINTFORM %"[---] - 前のページ",35,LEFT%
	RESETCOLOR
ENDIF
IF ARG:1 != 3
	PRINTBUTTON "[999] - 次のページ",999
ELSE
	SETCOLOR 色設定_テキスト_無効
	PRINTFORM %"[---] - 次のページ",35,LEFT%
	RESETCOLOR
ENDIF
PRINTL

@ABL_CUSTOM(ARG)
LOCAL:1 = 0
FOR LOCAL,0,50
	SIF !STRLENS(ABLNAME:LOCAL)
		CONTINUE
	;同性愛関係
	IF !(HAS_VAGINA(ARG)) && (LOCAL == 17 || LOCAL == 32)
		CONTINUE
	ELSEIF HAS_VAGINA(ARG) && (LOCAL == 18 || LOCAL == 33)
		CONTINUE
	ENDIF
	;中毒も無し
	SIF LOCAL >= 30
		CONTINUE
	PRINTFORM %ABLNAME:LOCAL,12%:{ABL:ARG:LOCAL,5,LEFT} 
	IF ABL:ARG:LOCAL != 0
		PRINTBUTTON "[－]", LOCAL * 2
	ELSE
		SETCOLOR 色設定_テキスト_無効
		PRINT [－]
		RESETCOLOR
	ENDIF
	IF ABL:ARG:LOCAL != 2
		PRINTBUTTON "[＋]", LOCAL * 2 + 1
	ELSE
		SETCOLOR 色設定_テキスト_無効
		PRINT [＋]
		RESETCOLOR
	ENDIF
	LOCAL:1 ++
	SIF LOCAL:1 % 2 == 0
		PRINTL 
NEXT
PRINTL
@EXP_CUSTOM(ARG)
VARSET LOCAL
FOR LOCAL,0,100
	SIF !STRLENS(EXPNAME:LOCAL)
		CONTINUE
	;同性愛関係
	IF !(HAS_VAGINA(ARG)) && LOCAL == 51
		CONTINUE
	ELSEIF HAS_VAGINA(ARG) && LOCAL == 52
		CONTINUE
	ENDIF
	PRINTFORM [{LOCAL,2}] - %EXPNAME:LOCAL,16%:{EXP:ARG:LOCAL,5,LEFT} 
	LOCAL:1 += 1
	SIF LOCAL:1 % 3 == 0
		PRINTL 
NEXT
PRINTL

@CLOTHES_CUSTOM(ARG)
;あなた以外なら変更しない
SIF NO:ARG
	RETURN 0
IF HAS_VAGINA(ARG)
	CFLAG:ARG:服装パターン = 2
ELSE
	CFLAG:ARG:服装パターン = 1
ENDIF
PRINTFORM 現在の%CALLNAME:MASTER%は
IF CFLAG:ARG:服装パターン == 2
	PRINT 女装
ELSEIF CFLAG:ARG:服装パターン == 1
	PRINT 男装
ENDIF
PRINTL です
PRINTL 要变更吗？
PRINTL [0] - 就这样 [1] - 穿上男装 [2] - 穿上女装
INPUT
SIF RESULT == 1 || RESULT == 2
	CFLAG:ARG:服装パターン = RESULT

IF CFLAG:ARG:服装パターン == 2
	PRINTFORML \@(CFLAG:ARG:服装パターン == 1) ? 執事 # メイド\@服にしますか？
	PRINTFORML [0] - 就这样 [1] - \@(CFLAG:ARG:服装パターン == 1) ? 執事 # メイド\@になる
	INPUT
	SIF RESULT == 1
		CFLAG:ARG:服装パターン += RESULT * 2
ENDIF
RETURN 0

;全員处女モード
@VIRGIN_CUSTOM
#DIM LOOP_CHR
PRINTL 全员处女吗？
PRINTL [0] 全部都是处女了
PRINTL [1] 默认
$INPUT_LOOP
INPUT
IF RESULT == 0
	PRINTW 全员处女，于是改变了素质和能力。
	FOR LOOP_CHR,1,CHARANUM
		IF IS_FEMALE(LOOP_CHR)
			TALENT:LOOP_CHR:处女 = 1
			EXP:LOOP_CHR:Ｖ经验 = 0
			EXP:LOOP_CHR:Ｖ性交经验 = 0
			EXP:LOOP_CHR:Ａ性交经验 = 0
			ABL:LOOP_CHR:Ｖ感觉 = 0
			ABL:LOOP_CHR:Ａ感觉 = 0
		ENDIF
	NEXT
ELSEIF RESULT == 1
	PRINTW 以默认设定开始。
ELSE
	GOTO INPUT_LOOP
ENDIF
RETURN 0

@GEME_START_FIRSTKISS_FIX
#DIM LOOP_CHR
FOR LOOP_CHR,1,CHARANUM
	IF TALENT:LOOP_CHR:处女 || TALENT:LOOP_CHR:童贞
		CFLAG:LOOP_CHR:初始无接吻经验 = 1
	ENDIF
NEXT
	