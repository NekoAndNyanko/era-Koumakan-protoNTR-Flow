@LIFE_LIST(ARG)
;ARG = 0 ステータス表示
;ARG = 1 調教対象選択
;ARG = 2 助手選択
;ARG = 3 キャラ売却
#DIM LOOP_CHR
#DIM LOOP_I
#DIM LOOP_J
#DIM 精子量
DRAWLINE
IF !ARG
	PRINTFORML 　[0]%NAME:MASTER%
	DRAWLINE
ENDIF
FOR LOOP_CHR,1, CHARANUM
	IF ARG == 2
		SIF !CFLAG:LOOP_CHR:和主角已做爱
			CONTINUE
		IF COUNT == ASSI
			PRINT ☆
		ELSE
			PRINT 　
		ENDIF
	ENDIF
	IF ARG == 3
		SIF !TALENT:LOOP_CHR:恋人
			CONTINUE
	ENDIF
	PRINTFORM [{LOOP_CHR,2}]
	PRINTFORM %CALLNAME:LOOP_CHR,16,LEFT%

	SELECTCASE FLAG:能力表示一覧用
			;基礎表示選択
		CASE 2
			PRINTFORM \@(FLAG:能力表示詳細) ? 气力#体力\@
			LOCAL = MAX(BASE:LOOP_CHR:(FLAG:能力表示詳細), 0)
			BAR LOCAL, MAXBASE:LOOP_CHR:(FLAG:能力表示詳細),10
			PRINTFORM ({LOCAL,4}/{MAXBASE:LOOP_CHR:(FLAG:能力表示詳細),4})

			SIF ARG == 0
				TRYCALL 簡易追加情報, LOOP_CHR

			;能力表示選択or性技表示選択
		CASE 3, 4
			IF FLAG:能力表示一覧用 == 4 && !(HAS_VAGINA(LOOP_CHR)) && (FLAG:能力表示詳細 == 52 || FLAG:能力表示詳細 == 54)
				PRINTFORM %ABLNAME:(FLAG:能力表示詳細),12%:* 
			ELSE
				PRINTFORM %ABLNAME:(FLAG:能力表示詳細),12%:{ABL:LOOP_CHR:(FLAG:能力表示詳細),5,LEFT} 
			ENDIF
			;刻印表示選択
		CASE 5
			PRINTFORM %GET_MARK_LEVEL_OWNERNAME(LOOP_CHR,FLAG:能力表示詳細),12%
			;経験表示選択
		CASE 6
			IF (!(HAS_VAGINA(LOOP_CHR)) && FLAG:能力表示詳細 == 51) || (HAS_VAGINA(LOOP_CHR) && FLAG:能力表示詳細 == 52)
				PRINTFORM %EXPNAME:(FLAG:能力表示詳細),16%:* 
			ELSE
				PRINTFORM %EXPNAME:(FLAG:能力表示詳細),16%:{EXP:LOOP_CHR:(FLAG:能力表示詳細),5,LEFT} 
			ENDIF
			;宝珠表示選択
		CASE 7
			LOCALS = {JUEL:LOOP_CHR:(FLAG:能力表示詳細)}
			SIF JUEL:LOOP_CHR:(FLAG:能力表示詳細) > 1000000
				LOCALS = {JUEL:LOOP_CHR:(FLAG:能力表示詳細) / 1000}k
			SIF JUEL:LOOP_CHR:(FLAG:能力表示詳細) > 1000000000
				LOCALS = {JUEL:LOOP_CHR:(FLAG:能力表示詳細) / 1000000}m
			PRINTFORM %PALAMNAME:(FLAG:能力表示詳細),6%の珠:%LOCALS,7,LEFT%
			;その他表示選択
		CASE 8
			SELECTCASE FLAG:能力表示詳細
					;既成事実
				CASE 1
					VARSET LOCALS
					SIF TALENT:LOOP_CHR:恋人
						LOCALS += "已告白　"
					SIF CFLAG:LOOP_CHR:和主角已接吻
						LOCALS += "已接吻　"
					SIF CFLAG:LOOP_CHR:和主角已做爱
						LOCALS += "有过性行为"
					PRINTFORM %LOCALS%
					;好感度
				CASE 2
					VARSET LOCALS
					;恋慕/NTR状態
					SIF TALENT:LOOP_CHR:NTR
						LOCALS += "NTR "
					SIF TALENT:LOOP_CHR:恋慕
						LOCALS += "恋慕 "
					SIF TALENT:LOOP_CHR:人妻
						LOCALS += "人妻 "
					PRINTFORM 好感度:{CFLAG:LOOP_CHR:(FLAG:能力表示詳細),5,LEFT}  
					IF !FLAG:真实模式
						PRINTFORM 屈服度:{CFLAG:LOOP_CHR:屈服度,5,LEFT} %LOCALS%
					ELSE

					ENDIF
					;浮気公認
				CASE 3
					PRINTFORM 浮気公認:{TALENT:LOOP_CHR:浮気公認}
					;子宮内精液量
				CASE 4
					精子量 = 0
					FOR LOOP_I, 0, 301
						REPEAT 9
							IF TA:LOOP_CHR:LOOP_I:COUNT > 0
								精子量 += TA:LOOP_CHR:LOOP_I:COUNT
							ENDIF
						REND
					NEXT
					IF TALENT:LOOP_CHR:妊娠
						LOCALS:1 = 孕
					ELSEIF CFLAG:LOOP_CHR:卵子生存日数 == 1
						LOCALS:1 = 排
					ELSEIF TALENT:LOOP_CHR:危险日
						LOCALS:1 = 危
					ELSE
						LOCALS:1 = 　
					ENDIF
					PRINTFORM 子宮内精液量:{精子量,5}/{CFLAG:LOOP_CHR:子宮内体積,5}
					PRINTFORM  %LOCALS:1%
					;弱味
				CASE 10
					VARSET LOCALS
					SIF GETBIT(CFLAG:LOOP_CHR:(FLAG:能力表示詳細),0) 
						LOCALS += "被没收了　"
					SIF GETBIT(CFLAG:LOOP_CHR:(FLAG:能力表示詳細),1) 
						LOCALS += "被看到了情事　"
					PRINTFORM %LOCALS%
			ENDSELECT
	ENDSELECT

	IF ARG == 2
		IF LOOP_CHR == ASSI
			PRINT 　　　　　
		ELSEIF ISASSI:LOOP_CHR == 1
			PRINT [元助手] 
		ELSE
			PRINT [未経験] 
		ENDIF
	ENDIF
	PRINTL
NEXT
PRINTL 

;ステータス表示
@SHOW_CHARADATA
CALL LIFE_LIST
PRINTLC  [2000]基礎表示切替
IF !FLAG:真实模式
	PRINTLC  [3000]能力表示切替
	PRINTLC  [4000]性技表示切替
	PRINTLC  [5000]刻印表示切替
	PRINTLC  [6000]経験表示切替
	PRINTLC  [7000]宝珠表示切替
	PRINTLC  [8000]その他表示切替
ENDIF
PRINTFORML  [1000]返回

$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 2000 || RESULT == 3000 || RESULT == 4000 || RESULT == 5000 || RESULT == 6000 || RESULT == 7000 || RESULT == 8000
	CALL SHOW_CHARADATA2, RESULT
	RESTART
ELSEIF RESULT < 0 || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ENDIF
CALL PRINT_STATE(RESULT, 0, 1)
RESTART

@SHOW_CHARADATA2, ARG
VARSET LOCAL, 0
LOCAL:999 = ARG/1000
DRAWLINE
DRAWLINE

SELECTCASE LOCAL:999
		;基礎表示選択
	CASE 2
		PRINTLC  [   0] 体力
		PRINTLC  [   1] 气力
		LOCAL:0 = 1
		LOCAL:1 = 1
		LOCAL:998 = 1
		;能力表示選択(性技は別）
	CASE 3
		FOR LOCAL:998,0,40
			SIF !STRLENS(ABLNAME:(LOCAL:998))
				CONTINUE
			PRINTFORMLC  [{LOCAL:998,4}] %ABLNAME:(LOCAL:998),12%
			LOCAL:(LOCAL:998) = 1
		NEXT
		;性技表示選択
	CASE 4
		FOR LOCAL:998,50,60
			SIF !STRLENS(ABLNAME:(LOCAL:998))
				CONTINUE
			PRINTFORMLC  [{LOCAL:998,4}] %ABLNAME:(LOCAL:998),12%
			LOCAL:(LOCAL:998) = 1
		NEXT
		;刻印表示選択
	CASE 5
		FOR LOCAL:998,0,5
			SIF !STRLENS(MARKNAME:(LOCAL:998))
				CONTINUE
			PRINTFORMLC  [{LOCAL:998,4}] %MARKNAME:(LOCAL:998),12%
			LOCAL:(LOCAL:998) = 1
		NEXT
		;経験表示選択
	CASE 6
		FOR LOCAL:998,0,100
			SIF !STRLENS(EXPNAME:(LOCAL:998))
				CONTINUE
			PRINTFORMLC  [{LOCAL:998,4}] %EXPNAME:(LOCAL:998),16%
			LOCAL:(LOCAL:998) = 1
		NEXT
		;宝珠表示選択
	CASE 7
		FOR LOCAL:998,0,22
			SIF !STRLENS(PALAMNAME:(LOCAL:998))
				CONTINUE
			PRINTFORMLC  [{LOCAL:998,4}] %PALAMNAME:(LOCAL:998),6%の珠
			LOCAL:(LOCAL:998) = 1
		NEXT
		PRINTLC  [ 100] 否定の珠
		LOCAL:100 = 1
		LOCAL:998 = 100
		;その他表示選択
	CASE 8
		PRINTLC  [   1]既成事実
		PRINTLC  [   2]好感度
		PRINTLC  [   3]浮気公認
		PRINTLC  [   4]子宮内精液量
		PRINTLC  [  10]弱味
		LOCAL:1 = 1
		LOCAL:2 = 1
		LOCAL:3 = 1
		LOCAL:4 = 1
		LOCAL:10 = 1
		LOCAL:998 = 10
ENDSELECT
PRINTL  [1000] 返回
$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT < 0 || RESULT > LOCAL:998 || LOCAL:RESULT <= 0
	GOTO INPUT_LOOP
ENDIF
FLAG:能力表示一覧用 = LOCAL:999
FLAG:能力表示詳細 = RESULT

