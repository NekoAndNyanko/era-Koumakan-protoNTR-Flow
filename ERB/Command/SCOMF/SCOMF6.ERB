;-------------------------------------------------
;挿入Ｇスポ責め
;セックス系派生コマンド
;-------------------------------------------------
@SCOM6

TRYCALL COM417_is_Protected(PLAYER,TARGET,淫纹_太股)
SIF RESULT
	RETURN 0

;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;TCVAR:2 射精箇所フラグ（-1=避孕套 1=膣内 2=菊花 3=手淫 4=口淫 5=乳交 6=素股 7=足交 8=体表 9=舔肛侍奉 10=道具 11=触手）

LOCALS = {TARGET}/
CSTR:PLAYER:射精判定用 += LOCALS
TCVAR:PLAYER:2 = 1
;強度
TCVAR:PLAYER:4 = 1800

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、气力
DOWNBASE:体力 += 60
DOWNBASE:气力 += 120

SOURCE:快Ｖ = 500
SOURCE:情爱 = 200
SOURCE:苦痛 = 500
SOURCE:露出 = 100
SOURCE:反感 = 500

CALL 締り具合変動, TARGET, 3, 0

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑郁ソース追加
SIF TALENT:自己爱 < 0 || TALENT:抵抗
	SOURCE:抑郁 += 500

;贞操観念持ちで恋慕、亲爱持ち以外には常に反発のソースを追加する（セックス系のみ）
SIF TALENT:贞操 > 0 && !TALENT:恋慕
	SOURCE:反感 += 1000
;Vへの苦痛は先に処理しておく
CALL PAIN_CHECK_V(TARGET)

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;膣内に破瓜の血の汚れを付加
SIF TALENT:处女
	STAIN:膣内 |= 汚れ_破瓜の血
;奴隷の膣内⇔調教者のＰの汚れが移动
STAIN:膣内 |= STAIN:PLAYER:Ｐ
STAIN:PLAYER:Ｐ |= STAIN:膣内

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:Ｖ性交经验 ++
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 4
	EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 4
ENDIF

;爱情经验
SIF ASSIPLAY == 0
	EXP:爱情经验 += TALENT:恋慕
RETURN 1
