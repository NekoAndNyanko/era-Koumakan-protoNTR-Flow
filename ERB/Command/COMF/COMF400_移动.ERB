;-------------------------------------------------
;移动
;-------------------------------------------------
@COM400
#DIMS ROOMNAME
#DIM 当前选中地图
#DIM 住人
#DIM LOOP_CHR
#DIM LOOP_POS
#DIM 地图编号
#DIMS 地图名称
#DIM TargetRoom

CALL TURN_RESET
;遠距離移动中の処理
IF TFLAG:遠距離移动

	TargetRoom = TFLAG:遠距離移动

	IF TargetRoom == CFLAG:MASTER:当前位置
		NEXTCOM = -1
		TFLAG:遠距離移动 = 0
		CALLF RESET_ROUTE(MASTER, 1)
		RETURN 0
	ENDIF

	;キャラの位置更新(NEXTCOM設定中は@SHOW_STATUSが走らないのでこっちで処理)
	CALL CHARA_MOVEMENT
	TFLAG:時間経過 = DATETIME()
	;キャラをCFLAG:310の昇順に並び替え
	VARSET TARGET
	CALL SORT_CFLAG(310)
	地图编号 = 0
	CALL NTR_CHK_GOWITH(10)
	FOR LOOP_CHR,1,CHARANUM
		IF CFLAG:MASTER:当前位置 != CFLAG:(RESULT:LOOP_CHR):当前位置
			CONTINUE
		ENDIF
		地图编号 ++
		TARGET:地图编号 = RESULT:LOOP_CHR
	NEXT

	住人 = 0
	;誰かいないかチェック
	FOR LOOP_CHR,1,CHARANUM
		IF TARGET:LOOP_CHR <= 0
			CONTINUE
		ENDIF
		IF CFLAG:(TARGET:LOOP_CHR):同行
			CONTINUE
		ENDIF
		住人 = TARGET:LOOP_CHR
	NEXT
	IF 住人
		PRINTFORM 在%GETPLACENAME(CFLAG:MASTER:当前位置)%遇到了%CALLNAME:住人%
		;STRLENSU CALLNAME:住人
		;PRINTFORMW 対象:%CALLNAME:住人% パンツ:%CSTR:MASTER:収集品% %SUBSTRINGU(CSTR:MASTER:収集品, 0, RESULT)%
		IF STRLENS(CSTR:MASTER:収集品) && 収集品死守戦発生(住人) && !CFLAG:住人:睡眠
			CALL CHK_COLLECTION_ITEM(住人)
			IF RESULT == 1
				RETURN 0
			ENDIF
		ELSE
			PRINTL ……怎么办呢？
			PRINTL [0]继续移动
			PRINTL [1]停下来
			INPUT

			IF RESULT == 0
				;無視する場合
				CALL IGNORE_WHEN_MOVE(住人)
			ELSE
				;実際は0以外全部立ち止まる扱い
				CALL STOP_WHEN_MOVE(住人)
				RETURN 0
			ENDIF
		ENDIF
	ENDIF
	;遠距離移动
	IF CAN_MOVE(CFLAG:MASTER:当前位置, TargetRoom)
		;地下室
		IF TargetRoom == 位置_红魔馆_地下室 && ABL:MASTER:战斗技能 <= 0
			PRINTW 从门里感到不祥之意……
			PRINTW 为了能平安返回，一定程度的实力是必要的吧。
			;遠距離移动フラグ消去
			NEXTCOM = -1
			CALLF RESET_ROUTE(MASTER, 1)
			RETURN 0
		ELSEIF TargetRoom != 位置_红魔馆_玩家房间 && isLocked(TargetRoom) && !canUnLock(MASTER, TargetRoom)
			;あなた以外の私室 and 合鍵無し
			PRINTW 门从里面锁着……
			IF GETBIT(FLAG:雑多設定,2)
				;なかにいるキャラから頼まれ事を請けているか
				FOR LOOP_CHR, 1, 8
					IF CFLAG:LOOP_CHR:当前位置 != TargetRoom
						CONTINUE
					ENDIF
					IF CFLAG:LOOP_CHR:従者イベント１ >= 10
						TRYCALL 従者E_受け取り不可, LOOP_CHR, "鍵閉め"
					ENDIF
				NEXT

			ENDIF
			;遠距離移动フラグ消去
			NEXTCOM = -1
			CALLF RESET_ROUTE(MASTER, 1)
			RETURN 0
			;それ以外の場所
		ELSE
			;あなたの当前位置に移动先を代入
			CFLAG:MASTER:当前位置 = TargetRoom
			;施錠されているが解錠できる
			IF isLocked(TargetRoom)
				IF tryUnLock(MASTER, TargetRoom)
					IF HAS_DUPLICATE_KEY(TargetRoom)
						PRINTFORML %"门从里面锁着……"+CALLNAME:MASTER+"打开锁进去了"%
					ELSE
						PRINTFORML %"门从里面锁着……"+CALLNAME:MASTER+"用钥匙进去了"%
					ENDIF
				ENDIF
			ENDIF
			PRINTFORML 
			FOR LOOP_CHR, 1, CHARANUM
				CALL CHAR_ON_PLACE(LOOP_CHR)
			NEXT
			;遠距離移动フラグ消去
			NEXTCOM = -1
		ENDIF
		FOR LOOP_CHR,1,CHARANUM
			IF CFLAG:LOOP_CHR:同行
				CFLAG:LOOP_CHR:当前位置 = CFLAG:MASTER:当前位置
			ENDIF
		NEXT
	ELSE
		;当前位置を更新し、次も移动の扱いにする
		CFLAG:MASTER:当前位置 = 移动路线:MASTER:0
		CALL SORT_ROUTE(MASTER)
		NEXTCOM = 400
	ENDIF
	IF FLAG:訪問者同行フラグ
		FLAG:访问者当前位置 = CFLAG:MASTER:当前位置
	ENDIF
	CALL INHABITANT_FRIENDSHIP_END_MASTER_MOVE
	TIME += 3
	RETURN 0
ENDIF

[SKIPSTART]
;プレイヤーの位置から表示するマップを変更
当前选中地图 = MAP_DISTINCTION(CFLAG:MASTER:当前位置)
[SKIPEND]

;根据玩家所在区域显示地图
当前选中地图 = 地图位置(CFLAG:MASTER:当前位置)
CALL mapPrint(当前选中地图,LOOP_CHR,LOOP_POS,TargetRoom,地图编号,地图名称)

CALL INHABITANT_FRIENDSHIP_END_MASTER_MOVE
TIME += 3
PREVCOM = 400
STR:0 = 移动

RETURN 0

;住人による収集品チェック
;戻り値1:収集品没収
@CHK_COLLECTION_ITEM(住人)
#DIM 住人

PRINTL 
CALL 収集品死守戦開始, 住人
IF RESULT == 1
	PRINTFORMW %CALLNAME:MASTER%被%CALLNAME:住人%盯上并搜身了……
	PRINTFORMW 
	PRINTFORMW 
	PRINTFORMW 
	PRINTFORMW 没收了收藏品
	IF GETBIT (CFLAG:住人:MASTERの弱味,0)
		PRINTFORMW 被%CALLNAME:住人%抓住了弱点
	ENDIF
	CALL KOJO_EVENT(11, 住人, 0)
	SETBIT CFLAG:住人:MASTERの弱味 ,0
	CSTR:MASTER:収集品 =
	NEXTCOM = -1
	CALLF RESET_ROUTE(MASTER, 1)
	RETURN 1
ENDIF
RETURN 0

;移动時住人を無視した場合の処理
;戻り値 常に0
@IGNORE_WHEN_MOVE(住人)
#DIM 住人

;什麽也不做でいいかな…無視されて怒らせる？
IF CFLAG:住人:当前位置 == FLAG:访问者当前位置 && FLAG:訪問者の气氛 != 0
	PRINTFORMW %CALLNAME:住人%好像和谁躲起来做了什么
ELSEIF CFLAG:住人:当前位置 == FLAG:访问者当前位置
	PRINTFORMW %CALLNAME:住人%好像在和谁说话
ELSE
	CALL KOJO_EVENT(10, 住人, 0)
	IF GETBIT(FLAG:雑多設定,2)
		CALL 従者E_発生, 住人
	ENDIF
ENDIF
RETURN 0

;移动時住人を無視せず立ち止まった場合の処理
;戻り値 常に0
@STOP_WHEN_MOVE(住人)
#DIM 住人

IF CFLAG:住人:当前位置 == FLAG:访问者当前位置 && FLAG:訪問者の气氛 != 0
	IF IS_AIR_MASTER(住人) < FLAG:訪問者の气氛
		PRINTFORMW %CALLNAME:住人%好像和谁躲起来做了什么、但因为%CALLNAME:MASTER%来了停了下来
		CALL NTR_RESET_VISITOR_ACTION(住人)
	ENDIF
ELSEIF CFLAG:住人:当前位置 == FLAG:访问者当前位置
	PRINTFORMW %CALLNAME:住人%好像在和谁说话
	CALL NTR_RESET_VISITOR_ACTION(住人)
ELSE
	CALL KOJO_EVENT(10, 住人, 1)
ENDIF
NEXTCOM = -1
CALLF RESET_ROUTE(MASTER, 1)
RETURN 0

;場所移动時、住人毎の処理
;戻り値 常に0
@CHAR_ON_PLACE(住人)
#DIM 住人

;todo 和访问者偷情时玩家到场
;ターゲットと訪問者が一緒 and 訪問者の气氛が会话以外(キス以上) and あなたとターゲットが一緒
IF CFLAG:住人:当前位置 == FLAG:访问者当前位置 && FLAG:訪問者の气氛 != 0 && CFLAG:MASTER:当前位置 == CFLAG:住人:当前位置
	;性交以外
	IF 1 <= FLAG:访问者当前行为 && FLAG:访问者当前行为 <= 6
		PRINTFORML 【%GETPLACENAME(FLAG:21)%】%CALLNAME:住人%和%NTR_NAME(0)%好像在做什么、但因为%CALLNAME:MASTER%来了停了下来
		PRINTFORML 感觉%CALLNAME:住人%的脸有些红
		PRINTFORMW 
		CALL NTR_RESET_VISITOR_ACTION(住人)
		;性交時 and (1/3 or 空気度が4以上 or 性交時の覗きバレ)
	ELSEIF 7 <= FLAG:访问者当前行为 && FLAG:访问者当前行为 <= 13 && (RAND:3 == 0 || IS_AIR_MASTER(住人) >= 4 || (CFLAG:住人:覗き発覚回数 > 0 && CFLAG:住人:覗き発覚時行為 >= 7))
		TRYCALLFORM NTR_INFIDELITY(住人)
		;性交時
	ELSEIF 7 <= FLAG:访问者当前行为 && FLAG:访问者当前行为 <= 13
		PRINTFORML 【%GETPLACENAME(FLAG:21)%】%CALLNAME:住人%和%NTR_NAME(0)%好像在做些什么、但是注意到接近的%CALLNAME:MASTER%、慌忙中断了行动
		PRINTFORML %CALLNAME:住人%的脸有些红，衣服凌乱
		PRINTFORMW 
		CALL NTR_RESET_VISITOR_ACTION(住人)
	ENDIF
	;ターゲットと訪問者が一緒 and あなたとターゲットが一緒
ELSEIF CFLAG:住人:当前位置 == FLAG:访问者当前位置 && CFLAG:MASTER:当前位置 == CFLAG:住人:当前位置
	PRINTFORMW %CALLNAME:住人%好像在和谁说话
	CALL NTR_RESET_VISITOR_ACTION(住人)
	;あなたとターゲットが一緒 and 睡眠中
ELSEIF CFLAG:MASTER:当前位置 == CFLAG:住人:当前位置 && CFLAG:住人:睡眠
	PRINTFORMW %CALLNAME:住人%在睡觉
	CALL KOJO_EVENT(7,住人,2)
ENDIF
RETURN 0

;次の移动場所を設定
@SORT_ROUTE(住人)
#DIM 住人
#DIM LOOP_I

;SETCOLOR 200, 200, 0
;REPEAT 移动路线MAX
;	PRINTFORM {移动路线:住人:COUNT} → 
;REND
;PRINTFORML ルート設定変更前 (%CALLNAME:住人%)

;ARRAYSHIFTで行うと同室に複数キャラが居る時にこの処理に来ると何故か動作しない事があるので別の方法で
; ARRAYSHIFT 移动路线:住人, -1, 0, 0

FOR LOOP_I, 1, 移动路线MAX
	移动路线:住人:(LOOP_I-1) = 移动路线:住人:LOOP_I
NEXT

;RESETCOLOR
;REPEAT 移动路线MAX
;	PRINTFORM  {移动路线:住人:COUNT} →
;REND
;PRINTFORML ルート設定変更後 (%CALLNAME:住人%)

;目的地到達なら移动目的を消す
IF CFLAG:住人:移动目的 && 移动路线:住人:0 == 0
	CFLAG:住人:移动目的 = 0
ENDIF
RETURN

;合鍵所有チェック
@HAS_DUPLICATE_KEY(場所)
#FUNCTION
#DIM 場所
#DIM LOOP_CHR
FOR LOOP_CHR, 1, CHARANUM
	IF CFLAG:LOOP_CHR:開始位置 == 場所
		IF FLAG:Flow版本
			IF TALENT:LOOP_CHR:恋人
				RETURNF 1
			ENDIF
		ELSE
			IF TALENT:LOOP_CHR:恋慕 || FLAG:全员NTR主人公女体化
				RETURNF 1
			ENDIF
		ENDIF
	ENDIF
NEXT
RETURNF 0


;収集品死守戦発生条件
@収集品死守戦発生(住人)
#FUNCTION
#DIM 住人		;住人 対象キャラ番号
#DIM 察知確率
#DIM 基礎値		;基礎値 主人公の収集経験(MAX100)
;怪しまれる確率設定
基礎値 = MIN(CFLAG:MASTER:収集品死守戦フラグ, 100)

察知確率 = 0
;収集品が対象の物なら察知確率上昇
STRLENSU CALLNAME:住人
IF SUBSTRINGU(CSTR:MASTER:収集品, 0, RESULT) == CALLNAME:住人
	;但し好感度が高いと上昇値が減少
	IF CFLAG:住人:好感度 <= 50
		察知確率 += 20
	ELSEIF CFLAG:住人:好感度 <= 100
		察知確率 += 15
	ELSEIF CFLAG:住人:好感度 <= 200
		察知確率 += 10
	ELSEIF CFLAG:住人:好感度 <= 300
		察知確率 += 5
	ENDIF
	;そうでなければ対象が恋慕を取得していると上昇
ELSE
	;チルノ、大妖精、ルーミアは自分に関係がない場合は怪しまない
	IF (住人 == 人物_琪露诺 || 住人 == 人物_大妖精 || 住人 == 人物_露米娅)
		RETURNF 0
	ENDIF

	IF TALENT:住人:恋慕 && !TALENT:住人:亲爱
		;加えて好感度が高いとさらに上昇値が増加
		IF CFLAG:住人:好感度 >= 3000
			察知確率 += 25
		ELSEIF CFLAG:住人:好感度 >= 2000
			察知確率 += 20
		ELSEIF CFLAG:住人:好感度 >= 1000
			察知確率 += 15
		ELSE
			察知確率 += 10
		ENDIF
	ENDIF
ENDIF

;出会ったキャラ別の察知能力
SELECTCASE 住人
	CASE 人物_美铃
		察知確率 += 60
	CASE 人物_小恶魔
		察知確率 += 45
	CASE 人物_帕秋莉
		察知確率 += 40
	CASE 人物_咲夜
		察知確率 += 70
	CASE 人物_蕾米莉亚
		察知確率 += 35
	CASE 人物_芙兰朵露
		察知確率 += 20
	CASE 人物_艾丽娅
		察知確率 += 30
	CASE 人物_琪露诺
		察知確率 += 5
	CASE 人物_大妖精
		察知確率 += 10
	CASE 人物_魔理沙
		察知確率 += 30
	CASE 人物_灵梦
		察知確率 += 80
	CASE 人物_露米娅
		察知確率 += 10
	CASE 人物_爱丽丝
		察知確率 += 50
ENDSELECT

;確率設定
IF RAND:基礎値 >= 察知確率
	RETURNF 0
ENDIF
RETURNF 1


;収集品死守戦開始
;住人 キャラ名
;移动中 怪しまれた時の状態(1=移动中, 0=移动していない)
@収集品死守戦開始(住人, 移动中)
#DIM 住人
#DIM 移动中
#DIM 結果	; 1:敗北 0:勝利
#DIM 確率
IF !移动中
	;ちょいと適当すぎるか？
	PRINTFORMW 突然被%CALLNAME:住人%问到有没有隐藏什么
ELSE
	PRINTFORMW %CALLNAME:住人%叫住了%CALLNAME:MASTER%、问到有没有隐藏什么
ENDIF
PRINTFORMW 这样下去就会被发现持有的收藏品！
PRINTFORML 怎么办？
PRINTL
IF 移动中
	PRINTL [0] 逃跑
ENDIF
PRINTL [1] 转移话题
;PRINTL [2] 
$LOOP_死守戦開始
INPUT
IF RESULT == 0 && 移动中
	PRINTFORMW %CALLNAME:MASTER%一瞬间转移了对方的注意力，全力从那个场合跑了出去
	結果 = 0
	確率 = 0
	;対象の戦闘技能とMASTERの戦闘技能の差で判定
	;差が+4以上なら逃走失敗、-4以下なら逃走確定
	SELECTCASE (ABL:住人:战斗技能 - ABL:MASTER:战斗技能)
		CASE IS > 3
			結果 = 1
		CASE 3
			確率 = 90
		CASE 2
			確率 = 70
		CASE 1
			確率 = 60
		CASE 0
			確率 = 50
		CASE -1
			確率 = 40
		CASE -2
			確率 = 20
		CASE -3
			確率 = 10
	ENDSELECT
	;差が+4以上又は-4以下でなければ確率
	IF 確率 && RAND:100 < 確率
		結果 = 1
	ENDIF
	IF 結果
		PRINTFORMW 但是，马上被%CALLNAME:住人%赶上了……
		RETURN 1
	ELSE
		PRINTFORMW 好歹从%CALLNAME:住人%那里逃出来了…
		;好感度5%削減
		TIMES CFLAG:住人:好感度, 0.95
		RETURN 0
	ENDIF
ELSEIF RESULT == 1
	PRINTFORMW %CALLNAME:MASTER%用天生的说话技巧想方设法克服了那个场合
	IF RAND:100 >= (EXP:MASTER:会话经验 - CFLAG:住人:収集品死守戦フラグ)
		PRINTFORMW 但是，没能很好地进行欺骗……
		RETURN 1
	ELSE
		IF !移动中
			PRINTFORMW 总算成功地把%CALLNAME:住人%敷衍住了
		ELSE
			PRINTFORMW 成功的敷衍了事，%CALLNAME:MASTER%匆匆忙忙离开了%GETPLACENAME(CFLAG:MASTER:当前位置)%……
		ENDIF
		;一度ごまかしに成功するにつれてごまかされにくくなる
		CFLAG:住人:収集品死守戦フラグ += 10
		RETURN 0
	ENDIF
ELSE
	CLEARLINE 1
	GOTO LOOP_死守戦開始
ENDIF

