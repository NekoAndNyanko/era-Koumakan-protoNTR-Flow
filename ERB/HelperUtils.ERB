;----------------------------------------------------------------------------------------------------
;一些通用的方法
;----------------------------------------------------------------------------------------------------

;--------------------------------------------------------------------------------
;泛用
;--------------------------------------------------------------------------------
;--------------------------------------------------
;返回角色名称
;--------------------------------------------------
@GetCharacterName(ARG)
#FUNCTIONS
IF ARG < CHARANUM
	RETURNF CALLNAME:ARG
ELSEIF ARG == 人物_訪問者
	RETURNF NTR_NAME(0)
ENDIF

;--------------------------------------------------------------------------------
;泛用结束
;--------------------------------------------------------------------------------

;--------------------------------------------------------------------------------
;地图用
;--------------------------------------------------------------------------------
;--------------------------------------------------
;返回场景是否有床铺，访问者场景调用时不考虑玩家是否持有道具。
;--------------------------------------------------
@IsPlaceHasBed(ARG, NTR)
#FUNCTION
#DIM NTR
SELECTCASE ARG
	CASE 位置_红魔馆_帕秋莉私室,位置_红魔馆_小恶魔私室,位置_红魔馆_守卫小屋,位置_红魔馆_咲夜私室,位置_红魔馆_玩家房间,位置_红魔馆_蕾米莉亚私室,位置_红魔馆_地下室,位置_雾之湖_琪露诺的家,位置_雾之湖_大妖精的家,位置_魔法之森_雾雨魔法店,位置_魔法之森_露米娅的树屋,位置_魔法之森_爱丽丝的家,位置_魔法之森_成美的家
		RETURNF 1
	CASE 位置_红魔馆_大浴场
		IF NTR ==1
		;游戏座垫
			SIF ITEM:20
				RETURNF 1
		ENDIF
ENDSELECT
RETURNF 0

;--------------------------------------------------
;返回场景是否有厨房
;--------------------------------------------------
@IsPlaceHasKitchen(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 位置_红魔馆_厨房,位置_雾之湖_琪露诺的家,位置_雾之湖_大妖精的家,位置_魔法之森_雾雨魔法店,位置_魔法之森_爱丽丝的家
		RETURNF 1
ENDSELECT
RETURNF 0

;--------------------------------------------------
;返回场景是否有浴室
;--------------------------------------------------
@IsPlaceHasBathroom(ARG)
#FUNCTION
SELECTCASE ARG
	CASE 位置_红魔馆_大浴场
		RETURNF 1
ENDSELECT
RETURNF 0

;--------------------------------------------------
;是否在室内
;--------------------------------------------------
@IsIndoor(ARG)
#FUNCTION
IF GROUPMATCH(ARG,位置_红魔馆_大厅, 位置_红魔馆_餐厅, 位置_红魔馆_大图书馆, 位置_红魔馆_厨房, 位置_红魔馆_仓库, 位置_红魔馆_帕秋莉私室, 位置_红魔馆_小恶魔私室, 位置_红魔馆_守卫小屋, 位置_红魔馆_后院, 位置_红魔馆_二楼楼梯间, 位置_红魔馆_二楼走廊, 位置_红魔馆_咲夜私室, 位置_红魔馆_妖精女仆办公室, 位置_红魔馆_玩家房间, 位置_红魔馆_蕾米莉亚私室, 位置_红魔馆_地下室, 位置_红魔馆_大浴场, 位置_红魔馆_屋外厕所, 位置_红魔馆_一楼厕所, 位置_红魔馆_二楼厕所, 位置_红魔馆_三楼阳台, 位置_红魔馆_地下室走廊, 位置_红魔馆_会客厅, 位置_红魔馆_更衣室, 位置_红魔馆_地下房间甲, 位置_红魔馆_地下房间乙, 位置_红魔馆_二楼阳台, 位置_雾之湖_琪露诺的家, 位置_雾之湖_大妖精的家, 位置_魔法之森_雾雨魔法店, 位置_魔法之森_露米娅的树屋, 位置_魔法之森_爱丽丝的家)
	RETURNF 1
ELSE
	RETURNF 0
ENDIF

;--------------------------------------------------------------------------------
;地图用结束
;--------------------------------------------------------------------------------

;--------------------------------------------------------------------------------
;消息用
;--------------------------------------------------------------------------------
;--------------------------------------------------
;输出一个爱心❤
;--------------------------------------------------
@MSG_HEART
#FUNCTIONS
RETURNF UNICODE(0x2764)

;--------------------------------------------------
;访问者所在场景站立性行为依靠的应该是树木还是墙壁
;--------------------------------------------------
@MSG_WallOrTree_NTR
#FUNCTIONS
#DIM 位置
位置 = FLAG:访问者当前位置 
IF 位置 == 位置_红魔馆_后院 || 位置 == 位置_雾之湖_湖北部 || 位置 == 位置_雾之湖_湖南部 || 位置 == 位置_魔法之森_森林岔路
	RETURNF "树干"
ELSE
	RETURNF "墙壁"
ENDIF

;--------------------------------------------------
;某个场景站立性行为依靠的应该是树木还是墙壁
;--------------------------------------------------
@MSG_WallOrTree(位置)
#FUNCTIONS
#DIM 位置
IF 位置 == 位置_红魔馆_后院 || 位置 == 位置_雾之湖_湖北部 || 位置 == 位置_雾之湖_湖南部 || 位置 == 位置_魔法之森_森林岔路
	RETURNF "树干"
ELSE
	RETURNF "墙壁"
ENDIF

;--------------------------------------------------
;通用的对缝隙的描写
;--------------------------------------------------
@MSG_SLIT(奴隷)
#FUNCTIONS
#DIM 奴隷
LOCALS = 
IF TALENT:奴隷:处女 == 1
	IF PALAM:润滑 > PALAMLV:1 || PALAM:欲情 >= PALAMLV:3
		LOCALS += "尽管还不识男人滋味，却依然"
	ELSE
		LOCALS += "还不识男人滋味的"
	ENDIF
ENDIF
IF PALAM:欲情 >= PALAMLV:3
	LOCALS += "在放出浓厚雌性的气味"
	IF PALAM:润滑 > PALAMLV:1
		LOCALS += "的同时，"
	ELSE
		LOCALS += "的"
	ENDIF
ENDIF
IF PALAM:润滑 > PALAMLV:4
	LOCALS += "让满溢而出的爱欲之蜜洒满了大腿的"
ELSEIF PALAM:润滑 > PALAMLV:3
	LOCALS += "被粘稠爱液浸透的"
ELSEIF PALAM:润滑 > PALAMLV:2
	LOCALS += "被爱液湿润得非常顺滑的"
ELSEIF PALAM:润滑 > PALAMLV:1
	LOCALS += "散发着湿润热气的"
ENDIF
IF TALENT:奴隷:体型 == -1
	IF RAND:2 == 0
		LOCALS += "惹人怜爱的"
	ELSE
		LOCALS += "稚嫩的"
	ENDIF
ELSEIF TALENT:奴隷:体型 == -2
	SELECTCASE RAND:6
		CASE 0
			LOCALS += "滑溜溜的"
		CASE 1
			LOCALS += "幼小的"
		CASE 2
			LOCALS += "光洁的"
		CASE 3
			LOCALS += "无毛的"
		CASE 4
			LOCALS += "小小的"
		CASEELSE
			LOCALS += "未成熟的"
	ENDSELECT
ENDIF
IF TALENT:奴隷:处女 == 1 && !RAND:3
	LOCALS += "处女"
ELSE
	SELECTCASE RAND:8
		CASE 0
			LOCALS += "秘縫"
		CASE 1
			LOCALS += "小穴"
		CASE 2
			LOCALS += "膣穴"
		CASE 3
			LOCALS += "雌穴"
		CASE 4
			LOCALS += "肉壺"
		CASE 5
			LOCALS += "蜜壺"
		CASE 6
			LOCALS += "阴唇"
		CASEELSE
			LOCALS += "秘裂"
	ENDSELECT
ENDIF
RETURNF LOCALS
;--------------------------------------------------------------------------------
;消息用结束
;--------------------------------------------------------------------------------