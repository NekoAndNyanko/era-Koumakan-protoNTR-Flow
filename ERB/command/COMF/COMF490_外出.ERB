;-------------------------------------------------------------------------------
;外出 - 実行可否判定
; RETURN 0 コマンドの実行不可
; RETURN 1 コマンドの実行可能
@COM_ABLE490
;;肉便器の屈服レイプで、押さえ込まれたら移动不可
SIF CFLAG:MASTER:WC_屈服レイプ押さえ込み
	RETURN 0
;訪問者宅では不可
SIF CFLAG:MASTER:現在位置 == 位置_訪問者宅
	RETURN 0
;一括管理分
IF !TFLAG:100
	RETURN 0
ELSEIF GLOBAL_COMABLE(490)
	RETURN RESULT
ELSEIF CFLAG:MASTER:うふふ == 2
	RETURN 0
ELSE
	;同行(デート)はしばらくお預けで
	FOR LOCAL,1,CHARANUM
		IF CFLAG:(TARGET:LOCAL):同行
			RETURN 0
		ENDIF
	NEXT
	RETURN 1
ENDIF
RETURN 0

;-------------------------------------------------------------------------------
;外出
; RETURN 0 コマンドの実行不可
; RETURN 1 コマンドの実行可能
@COM490
#DIM 奴隷
CALL COM_ABLE490()
IF !RESULT:0
	RETURN 0
ENDIF
PRINTFORML %"那么，到哪里去吧……？"%
PRINTFORM %"[ 0]还是停止",22,LEFT%
SIF CFLAG:MASTER:現在位置 != 位置_紅魔館周辺
	PRINTFORM %"["+TOSTR(位置_紅魔館周辺)+"]"+GETPLACENAME(位置_紅魔館周辺),22,LEFT%
SIF CFLAG:MASTER:現在位置 != 位置_人間の里周辺
	PRINTFORM %"["+TOSTR(位置_人間の里周辺)+"]"+GETPLACENAME(位置_人間の里周辺),22,LEFT%
SIF CFLAG:MASTER:現在位置 != 位置_博麗神社 && (TALENT:人物_灵梦:恋慕 || (TALENT:人物_灵梦:恋人 && TALENT:人物_灵梦:NTR)) && CFLAG:MASTER:開始位置 == 位置_博麗神社
	PRINTFORM %"["+TOSTR(位置_博麗神社)+"]"+GETPLACENAME(位置_博麗神社),22,LEFT%
SIF CFLAG:MASTER:現在位置 > 場所番号最大値
	PRINTFORM %"["+TOSTR(位置_红魔馆_正门)+"]回到红魔馆",22,LEFT%
SIF TALENT:MASTER:NTR
	PRINTFORM %"["+TOSTR(位置_訪問者宅)+"]去访问者的家",22,LEFT%
$COM490_INPUT_LOOP
INPUT
;移动部分の距離計算とか面倒なのでそのうち直します
SELECTCASE RESULT:0
	CASE 0
		RETURN 0
	CASE 位置_红魔馆_正门
		TIME:0 += 30
		CFLAG:MASTER:現在位置 = 位置_红魔馆_正门
		RETURN 1
	CASE 位置_紅魔館周辺
		TIME:0 += 30
		CFLAG:MASTER:現在位置 = 位置_紅魔館周辺
		RETURN 1
	CASE 位置_人間の里周辺
		TIME:0 += 30
		CFLAG:MASTER:現在位置 = 位置_人間の里周辺
		;;肉便器追加処理
		SELECTCASE CFLAG:MASTER:WC_訪問者の今回セクハラ
			CASE 41 TO 45		;;人里系
				SIF FLAG:訪問者の現在位置 == 位置_人間の里 || FLAG:訪問者の現在位置 == 位置_人間の里周辺
					CALL WC_SexHara_SOURCE(CFLAG:MASTER:WC_訪問者の今回セクハラ)
		ENDSELECT
		RETURN 1
	CASE 位置_博麗神社
		IF TALENT:人物_灵梦:恋慕 && CFLAG:MASTER:開始位置 == 位置_博麗神社
			TIME:0 += 170
			CFLAG:MASTER:現在位置 = 位置_博麗神社
			RETURN 1
		ENDIF
	CASE 位置_訪問者宅
		CFLAG:MASTER:現在位置 = 位置_訪問者宅
		CFLAG:MASTER:現在位置詳細 = 1
		TIME:0 += 60
		;訪問者同行中は訪問者も一緒に来る
		IF FLAG:訪問者同行フラグ
			FLAG:訪問者の現在位置 = 位置_訪問者宅
			PRINTFORMW %CALLNAME:PLAYER%牵着%NTR_NAME(0)%的手、邀请一起去%NTR_NAME(0)%的家。
			PRINTFORMW %NTR_NAME(0)%苦笑着答应了，两人挽着手一起向着%NTR_NAME(0)%的家去了……。
		ELSE
			PRINTFORMW %CALLNAME:PLAYER%想和%NTR_NAME(0)%见面、往%NTR_NAME(0)%的家去了。
			IF FLAG:訪問者の現在位置 == 位置_訪問者宅
				IF RAND:2 == 0
					PRINTFORMW %NTR_NAME(0)%对%CALLNAME:PLAYER%的到来感到很惊讶，但很快就邀请到家里来了。
				ELSE
					PRINTFORMW %NTR_NAME(0)%对%CALLNAME:PLAYER%的到来露出满意的笑容，并命令%CALLNAME:PLAYER%进到家里来。
				ENDIF
			ELSE
				PRINTFORMW 很遗憾……%NTR_NAME(0)%不在家。
			ENDIF
		ENDIF
		;バグ可能性があるので全部解錠
		FLAG:訪問者宅の施錠状態  |= 訪問者宅_玄関解錠済
		CFLAG:奴隷:NTR訪問者宅監禁状況 |= 訪問者宅_玄関解錠済
		FLAG:訪問者宅の施錠状態  |= 訪問者宅_牢屋解錠済
		CFLAG:奴隷:NTR訪問者宅監禁状況 |= 訪問者宅_牢屋解錠済
		FLAG:訪問者宅の施錠状態  |= 訪問者宅_通路解錠済
		CFLAG:奴隷:NTR訪問者宅監禁状況 |= 訪問者宅_通路解錠済
		FLAG:訪問者宅の施錠状態  |= 訪問者宅_床扉解錠済
		CFLAG:奴隷:NTR訪問者宅監禁状況 |= 訪問者宅_床扉解錠済
		;訪問者同行フラグは解除
		FLAG:訪問者同行フラグ = 0
	CASEELSE
		CLEARLINE 1
		GOTO COM490_INPUT_LOOP
ENDSELECT

RETURN 0
