;-------------------------------------------------------------------------------
;性交
@NTR_SEX(奴隷,密室度,調教者=人物_訪問者)
#DIM 奴隷
#DIM 密室度
#DIM 調教者
#DIM 体位
;バグ回避用。汚いけど許せ。
IF IS_NTR_KEEP_VIRGIN() && TALENT:奴隷:处女
	CALL NTR_A_SEX(奴隷, 密室度, 調教者)
	RETURN
ENDIF
TCVAR:奴隷:行為者 = 調教者
TCVAR:行為者 = 調教者
TEQUIP:奴隷:Ｖ性爱 = 人物_訪問者
CALL 締り具合変動, 奴隷, 3, 0
IF FLAG:访问者当前行为中止时刻 == 0
	FLAG:访问者行为计时点 = DATETIME()
	CALL NTR_ADD_SURRENDER(奴隷, 10)
	;CFLAG:奴隷:屈服度 += (FLAG:访问者当前行为中止时刻 - DATETIME()) / 10
ENDIF
SIF EXP:奴隷:NTR陷落经验
	CALL NTR_ADD_SURRENDER(奴隷, 2)
;体位の決定(目立たない場所か私室かで体位に違いを)
体位 = 0
SELECTCASE FLAG:访问者当前行为
	CASE NTR_行为_Ｖ正常位 TO NTR_行为_Ｖ背面座位
		体位 = FLAG:访问者当前行为 - NTR_行为_Ｖ正常位
	CASE IS > NTR_行为_Ｖ背面座位
		体位 = FLAG:访问者当前行为 - NTR_行为_Ｖ背面座位
	CASEELSE
		SIF 密室度 == 2
			体位 = 5 + RAND:2
		SIF 密室度 >= 3
			体位 = RAND:5
ENDSELECT
体位 = MIN(体位, 6)
CALLFORM NTR_SEX_{体位}(奴隷)
;汚れ等共通する処理
CALL STAIN_MOVE_BY_PENIS(部位_Ｖ, 奴隷, 人物_訪問者)
CALL STAIN_MOVE_BY_PENIS(部位_膣内, 奴隷, 人物_訪問者)
IF TALENT:奴隷:处女
	SETBIT FLAG:訪問者との汚れ判定に使用,汚れB_破瓜の血
	CALL LOST_VIRGIN(奴隷, 調教者)
	;处女喪失中フラグセット
	CFLAG:奴隷:处女喪失中 = 1
ENDIF
CFLAG:奴隷:和访问者已做爱 = 1
;无知去除
CALL 无知去除(奴隷)
CFLAG:奴隷:精液残留中 = 0
CFLAG:奴隷:与访问者最后做爱的时间 = DATETIME()
RETURN 0

;-------------------------------------------------------------------------------
;性交終了（絶頂）
;@return 常に0
@NTR_SEX_FINISH(奴隷)
#DIM 奴隷
CALL NTR_SEX_FINISH_MSG_KOJO(奴隷)
CALL EJACULATION_V(奴隷, 人物_訪問者)
CALL NTR_EXP_ADD_V_SEX_EX(奴隷,1)
SIF TALENT:奴隷:亲爱
	EXP:奴隷:被强奸经验 ++
;V感覚が高いと绝顶经验加算（強絶頂扱い）
CALL NTR_V_EXP_PLUS(奴隷)
SIF RAND:3 == 0
	CALL NTR_PUNISHMENT_10(奴隷, 0)
RETURN 0