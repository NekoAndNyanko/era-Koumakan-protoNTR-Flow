
;妊娠処理変更パッチ======================================
;新規追加フラグ

;DA:X:Y = Xの子宮内にあるYの総精子量
;TA:X:Y:Z(0~4) = Xの子宮内にあるYの精子量　Zは振り分け番号
;TB:X:Y:Z(0~4) = Xの子宮内にあるZ番のYの精子の残り生存日数
;Yの値、訪問者は300,客は299,村人は298とする

;CFLAG:350 妊娠処理における妊娠確率の分母、便宜上"子宫内体积"と明記

;TFLAG:精子量 射精される精子量

;子宫内体积は体型により変化、幼児体型など通常より小さければ子宫内体积も小さくなり、妊娠しやすくなる
;　長身など体が大きければその逆となる
;妊娠確率は子宮内の総合精子量/子宫内体积
;射精される側(体型:普通)が排卵日の時に射精する側の精力が全快状態(1000)で膣内射精を一回行うと20％の確立で妊娠する



;====================================================

;PREGNASY_S.ERB 出産、成長・ここ～～～～～～～～～～～～～～～～～～
@子宫内体积設定(奴隷)
#DIM 奴隷
#DIM DMY

DMY = 子宫内体积設定F(奴隷)

; 子宫内体积設定をFUNCTION化
; 戻り値は常に0
@子宫内体积設定F(奴隷)
#DIM 奴隷
#FUNCTION
;子宫内体积（体型により変化）
IF TALENT:奴隷:体型 == -3
	CFLAG:奴隷:子宫内体积 = 1500
ELSEIF TALENT:奴隷:体型 == -2
	CFLAG:奴隷:子宫内体积 = 3000
ELSEIF TALENT:奴隷:体型 == -1
	CFLAG:奴隷:子宫内体积 = 4000
ELSEIF TALENT:奴隷:体型 == 1
	CFLAG:奴隷:子宫内体积 = 6000
ELSEIF TALENT:奴隷:体型 == 2
	CFLAG:奴隷:子宫内体积 = 8000
ELSEIF TALENT:奴隷:体型 == 3
	CFLAG:奴隷:子宫内体积 = 12000
	;普通
ELSE
	CFLAG:奴隷:子宫内体积 = 5000
ENDIF
RETURNF CFLAG:奴隷:子宫内体积

@子宫内体积极限設定F(奴隷)
#FUNCTION
#DIM 奴隷
#DIM 体积
;子宫内体积（体型により変化）
IF TALENT:奴隷:体型 == -3
	体积 = 1500
ELSEIF TALENT:奴隷:体型 == -2
	体积 = 3000
ELSEIF TALENT:奴隷:体型 == -1
	体积 = 4000
ELSEIF TALENT:奴隷:体型 == 1
	体积 = 6000
ELSEIF TALENT:奴隷:体型 == 2
	体积 = 8000
ELSEIF TALENT:奴隷:体型 == 3
	体积 = 12000
	;普通
ELSE
	体积 = 5000
ENDIF
体积 *= 3
RETURNF 体积

;SOURCE_SHOOT.ERB・NTR.ERB～～～～～～～～～～～～～～～～～～
;処理上、CALL 精子取得 だと既に精力が減少してしまっているのでこっちで取得
;登録外のキャラもとりあえずこっちで取得
@精力取得(射精者)
#DIM 射精者		; 射精したキャラ 998=訪問者
;精子量取得
;誰か（客）
IF 射精者 == 人物_客 || 射精者 == 人物_村人
	IF RAND(100) == 0
		;稀に濃度の濃い奴
		TFLAG:精子量 = RAND(1000, 2000)
	ELSE
		TFLAG:精子量 = RAND(1, 200)
	ENDIF
ELSEIF 射精者 == 人物_催眠者
	IF RAND:100 == 1
		;稀に濃度の濃い奴
		TFLAG:精子量 = RAND(1000, 2000)
	ELSE
		TFLAG:精子量 = RAND(150, 300)
	ENDIF
	;訪問者
ELSEIF 射精者 == 人物_訪問者
	IF RAND(100) == 0
		TFLAG:精子量 = RAND(2000, 3000)
	ELSE
		TFLAG:精子量 = RAND(500, 1500)
	ENDIF
ELSE
	;減少する前の精力の値
	TFLAG:精子量 = BASE:射精者:精力
	;コマンド[插入子宫口]以外は3割減少
	;TFLAG:50 == 7 特殊COM,插入子宫口
	SIF TFLAG:特殊COM != 7
		TIMES TFLAG:精子量, 0.70
ENDIF


@精子取得(射精者, 被害者)
#DIM 射精者		;射精したキャラ 998=訪問者
#DIM 被害者		;射精されたキャラ
#DIM 子宫内精子量		;子宫内精子量
#DIM 保留量百分比
;村人を298,客を299,訪問者を300とする
SIF 射精者 == 人物_村人
	射精者 = 精子主_村人
SIF 射精者 == 人物_客
	射精者 = 精子主_客
SIF 射精者 == 人物_訪問者
	射精者 = 精子主_訪問者
SIF 射精者 == 人物_催眠者
	射精者 = 精子主_催眠者

;子宮内精子量増加（TFLAG:精子量の分だけ上昇）
REPEAT 9
	IF CFLAG:被害者:淫纹身体改造 == 1
		TA:被害者:射精者:0 += TFLAG:精子量
		TRYCALL 精子取得DEBUG(TFLAG:精子量, TA:被害者:射精者:COUNT, 被害者, 射精者, COUNT)
		BREAK
	ELSE
		;日数が0なら、精子量追加
		IF TB:被害者:射精者:COUNT == 0
			TA:被害者:射精者:COUNT += TFLAG:精子量
			TRYCALL 精子取得DEBUG(TFLAG:精子量, TA:被害者:射精者:COUNT, 被害者, 射精者, COUNT)
			BREAK
		ENDIF
	ENDIF
REND
子宫内精子量 = 0
FOR LOCAL, 0, 301
	REPEAT 9
		IF TA:被害者:LOCAL:COUNT > 0
			子宫内精子量 += TA:被害者:LOCAL:COUNT
		ENDIF
	REND
NEXT
;子宫内的精液量超出子宫极限
IF 子宫内精子量 > 子宫内体积极限設定F(被害者)
	;PRINTFORMW %CALLNAME:被害者%的子宫内精子量:{子宫内精子量}
	保留量百分比 = MIN(子宫内体积极限設定F(被害者) * 100 / 子宫内精子量 + 1,100)
	子宫内精子量 = 0
	LOCAL:2 = 0
	FOR LOCAL, 0, 301
		REPEAT 9
			IF TA:被害者:LOCAL:COUNT > 0
				LOCAL:1 = TA:被害者:LOCAL:COUNT
				TA:被害者:LOCAL:COUNT = TA:被害者:LOCAL:COUNT * 保留量百分比 / 100
				子宫内精子量 += LOCAL:1 - TA:被害者:LOCAL:COUNT * 保留量百分比 / 100
				LOCAL:2 += TA:被害者:LOCAL:COUNT
			ENDIF
		REND
	NEXT
	;PRINTFORMW 保留量百分比:{保留量百分比}%"%"% 溢出的精子量:{子宫内精子量},现在子宫内的精子量:{LOCAL:2}
	IF NTR_CHK_VISIBLE(CFLAG:被害者:現在位置)
		PRINTFORMW %CALLNAME:被害者%的子宫内的精液似乎已经多得撑到极限了
		PRINTFORMW 接受这次授种之后，容纳不下的温暖又黏糊糊的精液一股脑地喷了出来
	ENDIF
ENDIF

@获取子宫内个人精液量(ARG,精液主)
#FUNCTION
#DIM 精液主
#DIM 精液量
精液量 = 0
REPEAT 9
	精液量 += TA:ARG:精液主:COUNT
REND
RETURNF 精液量

;AFTERTRA.ERB～～～～～～～～～～～～～～～～～～

;妊娠判定の前の処理
@精子処理_生存日数振り分け
#DIM 母親候補	;チェック対象
#DIM 父親候補	;チェック対象を孕ませる相手
#DIM 総精子量	;チェック対象の子宮内精子量の合計
#DIM 精子流失量
#DIM 精子过期量
FOR 母親候補, 0, CHARANUM
	精子流失量 = 0
	精子过期量 = 0
	;男(ふたなりは除く)はスキップ
	SIF TALENT:母親候補:性別 == 2
		CONTINUE

	FOR 父親候補, 0, 301
		;全キャラ回せば次に訪問者と客の振り分けを行うようにする
		SIF 父親候補 == CHARANUM
			父親候補 = 精子主_スキップ

		総精子量 = 0
		REPEAT 9
			;日数フラグが0、精子量が０超なら日数セット 日数为0 精子量大于0 则日数设为3
			IF TB:母親候補:父親候補:COUNT == 0 && TA:母親候補:父親候補:COUNT > 0
				;默认保质三天
				TB:母親候補:父親候補:COUNT = 3
				;每多一个淫纹，精子保质期加一天
				SIF GETBIT(CFLAG:母親候補:淫纹,淫纹_恥丘)
					TB:母親候補:父親候補:COUNT++
				SIF GETBIT(CFLAG:母親候補:淫纹,淫纹_太股)
					TB:母親候補:父親候補:COUNT++
				SIF GETBIT(CFLAG:母親候補:淫纹,淫纹_左尻)
					TB:母親候補:父親候補:COUNT++
				SIF GETBIT(CFLAG:母親候補:淫纹,淫纹_胸元)
					TB:母親候補:父親候補:COUNT++
				SIF GETBIT(CFLAG:母親候補:淫纹,淫纹_右頬)
					TB:母親候補:父親候補:COUNT++
				;日数フラグが１、精子量が０超ならリセット 日数为1 精子量大于0 则精子过期设为0 在淫纹身体改造之后，精子不会过期
			ELSEIF TB:母親候補:父親候補:COUNT == 1 && TA:母親候補:父親候補:COUNT > 0 && CFLAG:母親候補:淫纹身体改造 == 0
				精子过期量 += TA:母親候補:父親候補:COUNT
				TA:母親候補:父親候補:COUNT = 0
				TB:母親候補:父親候補:COUNT = 0
				;日数フラグが残っており、精子量が０超なら日数フラグ減少＋精子量20％減
				;日数还有剩余，精子量超过0则每次减少百分比
			ELSEIF TB:母親候補:父親候補:COUNT > 0 && TA:母親候補:父親候補:COUNT > 0
				;完整的淫纹保护下，流失量减少
				IF CFLAG:母親候補:淫纹 == 31
					LOCAL = TA:母親候補:父親候補:COUNT
					TIMES TA:母親候補:父親候補:COUNT, 0.90
					精子流失量 += LOCAL - TA:母親候補:父親候補:COUNT
				ELSE
					LOCAL = TA:母親候補:父親候補:COUNT
					TIMES TA:母親候補:父親候補:COUNT, 0.80
					精子流失量 += LOCAL - TA:母親候補:父親候補:COUNT
				ENDIF
				;日数减少1
				TB:母親候補:父親候補:COUNT --
			ENDIF
			総精子量 += TA:母親候補:父親候補:COUNT
		REND
		;現在の総精子量のセット
		DA:母親候補:父親候補 = 総精子量
		TRYCALL 精子処理DEBUG2(母親候補, 父親候補, 総精子量)

	NEXT
	DEBUGPRINTFORML %CALLNAME:母親候補%\@CFLAG:母親候補:淫纹身体改造 == 1?是淫纹身体改造过的#\@，因此精子过期量:{精子过期量}
	IF CFLAG:母親候補:淫纹 == 31
		DEBUGPRINTFORML %CALLNAME:母親候補%在淫纹的保护下精子流失量:{精子流失量}
	ELSE
		DEBUGPRINTFORML %CALLNAME:母親候補%的精子流失量:{精子流失量}
	ENDIF
NEXT

;受精関連（此処3つで一つ）-------------------------------
@妊娠処理変更パッチ
#DIM 母親候補	; 母親候補(MASTERとキャラ)
#DIM 父親候補	; 父親候補(MASTERときキャラと訪問者と客)
#DIM 総精子量	; 母親候補の子宮内の総合精子量
#DIM 精子種類	; 母親候補の子宮内の精子の種類数

;対象１のチェック
FOR 母親候補, 0, CHARANUM
	;男(ふたなりは除く)はスキップ
	SIF TALENT:母親候補:性別 == 2
		CONTINUE
	;排卵日のみ
	SIF CFLAG:母親候補:卵子生存日数 != 1
		CONTINUE
	;避孕药使用中ならスキップ
	SIF CFLAG:母親候補:避孕药使用 == 3
		CONTINUE

	CSTR:母親候補:妊娠父親判定 =
	総精子量 = 0
	精子種類 = 0
	;何種の精子が子宮内に有るかチェック
	FOR 父親候補, 0, 301
		;全キャラ回せば(ry
		SIF 父親候補 == CHARANUM
			父親候補 = 精子主_スキップ
		;訪問者での妊娠が OFF になっている場合は除外
		SIF 父親候補 == 精子主_スキップ && !IS_NTR_PREGNANCY_ENABLE()
			BREAK

		;子宮内に父親候補の精子が有るか？
		IF DA:母親候補:父親候補 > 0
			;有れば総精子量に加算、種類数＋１
			総精子量 += DA:母親候補:父親候補
			精子種類 ++
			;多生児出産用フラグ、父親候補のキャラ番号と注いだ生存精子数を保存していく
			CSTR:母親候補:妊娠父親判定 += @"{父親候補}/{DA:母親候補:父親候補}/"
		ENDIF
	NEXT

	TRYCALL 妊娠処理変更パッチDEBUG(母親候補, 精子種類)

	;1種でも精子が有るならチェックへ
	SIF 精子種類 > 0
		CALL 受精チェック(母親候補, 総精子量, 精子種類)
NEXT

;中出しされた精子量に応じて確率処理
@受精チェック(母親候補, 総精子量, 精子種類)
#DIM 母親候補	; 対象
#DIM 総精子量	; 子宮内の総合精子量
#DIM 精子種類	; 何種類の精子が存在するか
#DIM 当たり番号	; 受精させた精子番号存

;暫定措置 子宫内体积が0未満のときは処理しない
IF 0 >= CFLAG:母親候補:子宫内体积
	RETURN
ENDIF
;乳児は妊娠しないものとする
IF CFLAG:母親候補:成長度 == 成長度_乳児
	RETURN
ENDIF
;妊娠するかの判定
当たり番号 = RAND:(CFLAG:母親候補:子宫内体积)
;百発百中(デバッグ用)
;当たり番号 = 1
IF !FLAG:禁止受精
	;图像插入：受精
	SETCOLOR C_CREAM
	PRINTFORML 甜腻的白浊装满了%CALLNAME:母親候補%的子宫…
	CALL PRINT_IMAGE_ANIMATION("受精",0,7,FLAG:图像_动画,1)
	CALL PRINT_IMAGE_ANIMATION("受精",0,3,1,0)
	CALL PRINT_IMAGE("受精2")
	WAIT
	RESETCOLOR
ENDIF
TRYCALL 受精チェックDEBUG(総精子量, 当たり番号)
;誰かの精子が当たっていれば誰の子か判定
IF 当たり番号 <= 総精子量
	CALL 誰の子(母親候補, 総精子量, 当たり番号, 精子種類)
ENDIF

@誰の子(母親候補, 総精子量, 当たり番号, 精子種類)
#DIM 母親候補	; 妊娠チェックの対象
#DIM 総精子量	; 子宮内の総合精子量
#DIM 当たり番号	; 受精させた精子番号
#DIM 精子種類	; 何種類の精子が存在するか
#DIM 父親候補	; 父親候補
#DIM 持ち番号	; 父親候補の持ち番号

持ち番号 = 0
;対象２のチェック
FOR 父親候補, 0, 301
	;全キャラ回せば(ry
	SIF 父親候補 == CHARANUM
		父親候補 = 精子主_スキップ

	;対象２の精子がなければ次へ
	SIF DA:母親候補:父親候補 <= 0
		CONTINUE
	;訪問者での妊娠が OFF になっている場合は除外
	SIF 父親候補 >= 精子主_スキップ && !IS_NTR_PREGNANCY_ENABLE()
		CONTINUE

	;精子量追加（前回の持ち番号の数値～今回の持ち番号の数値-1までが現在の対象２の持ち番号）
	持ち番号 += DA:母親候補:父親候補

	TRYCALL 精子量追加DEBUG, 母親候補, 父親候補, 持ち番号, 当たり番号

	;持ち番号に当たり番号の番号があるか
	IF (持ち番号 - 1) >= 当たり番号
		;图像插入：精子游向卵子
		IF !FLAG:禁止受精
			PRINTFORML 游曳在子宫中的精子环抱着%CALLNAME:母親候補%毫无防备的卵子…
			CALL PRINT_IMAGE_ANIMATION("受精",0,12,FLAG:图像_动画,0)
			CALL PRINT_IMAGE("受精11")
		ENDIF
		;有れば対象２の子を孕む
		;訪問者
		IF 父親候補 == 精子主_訪問者
			;妊娠フラグ
			CFLAG:母親候補:にんっしんっ = 人物_訪問者+1
			;誰か
		ELSEIF 父親候補 == 精子主_客
			CFLAG:母親候補:にんっしんっ = 人物_客+1
		ELSEIF 父親候補 == 精子主_村人
			CFLAG:母親候補:にんっしんっ = 人物_村人+1
		ELSEIF 父親候補 == 精子主_催眠者
			CFLAG:母親候補:にんっしんっ = 人物_催眠者
		ELSE
			CFLAG:母親候補:にんっしんっ = 父親候補+1
		ENDIF
		BREAK
	ENDIF
NEXT

;PRINTFORML 母親候補:%CALLNAME:母親候補%  内部精子一覧:%CSTR:母親候補:妊娠父親判定%

;複数人の精子が有るなら誰の子か分からなくする
;@PREGNACY_S_BIRTHで-1000
SIF 精子種類 > 1
	CFLAG:母親候補:にんっしんっ += 1000

;妊娠が確定したのでいらないフラグをリセット
CFLAG:母親候補:卵子生存日数 = 0
CFLAG:母親候補:生理周期 = 0
TALENT:母親候補:危险日 = 0


;PRINT_STATE.ERB～～～～～～～～～～～～～～～～～
;対象の子宮内の精子総量表示(予定)


;子供の成長状態表示
@子供の成長状態(ARG)
#FUNCTIONS

SELECTCASE CFLAG:ARG:成長度
	CASE 1
		RETURNF "<婴儿>"
	CASE 2
		; IF NO:ARG == 148
		; 	RETURNF "<幼児>"
		; ELSE
		; 	RETURNF "<幼女>"
		; ENDIF
		RETURNF "<儿童>"
	CASE 3
		RETURNF "<成长期>"
	CASE 4
		RETURNF "<成年>"
	CASEELSE
		RETURNF "数据错误"
ENDSELECT



;PREGNASY_S.ERB
;双子以上を出産する場合、妊娠判定で得た値を元に非一卵性出産時の父親変更処理
@多生児父親判定(母親候補)
#DIM 母親候補
#DIM Chara_num
#DIM Total
#DIM Total2
#DIM 持ち番号
#DIM 当たり番号

SPLIT CSTR:母親候補:妊娠父親判定, "/", LOCALS
LOCAL:1 = RESULT / 2

;一人しかいない場合はそのまま
SIF LOCAL:1 <= 1
	RETURN -1

;偶数値（キャラ番号）をLOCAL:Nに取得
;奇数値（精子量）の総合値を保存
Total = 0
FOR LOCAL, 0, LOCAL:1
	Total += TOINT(LOCALS:((LOCAL * 2)+ 1))
NEXT

;誰の子になるか
当たり番号 = RAND:Total
持ち番号 = 0

;PRINTFORML 父親変更 総数:{Total} 当たり：{当たり番号} 

FOR LOCAL, 0, LOCAL:1

	;精子量追加
	持ち番号 += TOINT(LOCALS:((LOCAL * 2)+ 1))
	;PRINTFORMW 変更開始 番号{TOINT(LOCALS:(LOCAL * 2))} 持ち番：{持ち番号}
	;持ち番号に当たり番号の番号があるか
	IF (持ち番号 - 1) >= 当たり番号
		;有れば対象２の子を孕む
		;訪問者
		SELECTCASE TOINT(LOCALS:(LOCAL * 2))
			CASE 精子主_訪問者
				RETURN 人物_訪問者
			CASE 精子主_客
				RETURN 人物_客
			CASE 精子主_村人
				RETURN 人物_村人
			CASE 精子主_催眠者
				RETURN 人物_催眠者
			CASEELSE
				RETURN TOINT(LOCALS:(LOCAL * 2))
		ENDSELECT
	ENDIF
NEXT



