;	INTRUDER.ERB
;
;	侵入者処理
;	侵入者といっても特に何か危害を加えてくるわけではなくてふらふら移动して
;	時々些細な悪戯をする程度。

;-------------------------------------------------------------------------------
;侵入者(こいし)移动
;@return 1:移动あり, 0:移动なし
@INTRUDER_MOVEMENT
#DIM LOOP_POS
#DIM 待ちフラグ
#DIM 移动先
#DIM 表示フラグ

待ちフラグ = 0
SIF RAND:(TIME_PROGRESS(TFLAG:300) + 1) <= RAND:60
	待ちフラグ = 1
SIF 待ちフラグ || !FLAG:侵入者の現在位置
	RETURN 0
FOR LOOP_POS,0,MAXROOM()
	;施錠なんて関係ねぇ！
	;IF FLAG:(300 + LOOP_POS)
	;	LOCAL:1 = 100
	;ELSE
	;	LOCAL:1 = 100
	;ENDIF		
	;RESULT:LOCAL = LOCAL:1 * (CAN_MOVE(FLAG:侵入者の現在位置, LOCAL) > 0)
	RESULT:LOOP_POS = 100 * (CAN_MOVE(FLAG:侵入者の現在位置, LOOP_POS) > 0)
	SIF RESULT:LOOP_POS
		RESULT:LOOP_POS = RAND:(RESULT:LOOP_POS) + 1
NEXT
移动先 = 0
FOR LOOP_POS,1,MAXROOM()
	SIF RESULT:移动先 < RESULT:LOOP_POS
		移动先 = LOOP_POS
NEXT
表示フラグ = 0
IF IS_DISP_INTRUDER()
	PRINTFORMW こいしは【%GETPLACENAME(FLAG:侵入者の現在位置)%】から【%GETPLACENAME(移动先)%】に移动した
ELSE
	表示フラグ = 1
ENDIF
SIF 移动先
	FLAG:侵入者の現在位置 = 移动先
IF 表示フラグ && CFLAG:MASTER:現在位置 == FLAG:侵入者の現在位置
	PRINTFORMW こいしが【%GETPLACENAME(移动先)%】に入ってきた
ENDIF
RETURN 1

;-------------------------------------------------------------------------------
;侵入者(こいし)関連の文を表示するかの判定
;@return 1:表示する 0:表示しない
@IS_DISP_INTRUDER
#FUNCTION
IF IS_NTR_SHOW_ALL()	; 筒抜けモード
	RETURNF 1
ENDIF
IF CAN_MOVE(CFLAG:MASTER:現在位置, FLAG:侵入者の現在位置) == 2 || CFLAG:MASTER:現在位置 == FLAG:侵入者の現在位置
	RETURNF 1
ENDIF
RETURNF 0

;-------------------------------------------------------------------------------
;侵入者(こいし)アクション
;何か軽い悪戯
;@return 常に0
@INTRUDER_DO
#DIM LOOP_CHR
#DIM 住人人数	; 住人の人数

;来ていないときはなにもしない
IF 0 == FLAG:侵入者の現在位置
	RETURN 0
ENDIF
住人人数 = GET_N_IN_ROOM(FLAG:侵入者の現在位置, 0, 0, 0)
IF 0 == RAND:10
	PRINTL
	; 一定の確率でアクションを起こす
	; 状況で分岐。分岐した先の処理内で、さらに詳細な状況や場所で分岐
	IF CFLAG:MASTER:現在位置 == FLAG:侵入者の現在位置 && FLAG:訪問者の現在位置 == FLAG:侵入者の現在位置
		; MASTERと訪問者と同室
		IF 1 <= 住人人数
			; MASTER, 訪問者以外に住人がいる
			; 3P目撃
			; 戦闘目撃
			; etc...
			CALL INTRUDER_WITH_MASTER_VISITER_OTHER
		ELSE
			; MASTER, 訪問者のみ
			; 訪問者のMASTERへのセクハラ目撃
			; 戦闘目撃
			; etc...
			CALL INTRUDER_WITH_MASTER_VISITER
		ENDIF
	ELSEIF CFLAG:MASTER:現在位置 == FLAG:侵入者の現在位置
		; MASTERと同室
		IF 1 <= 住人人数
			; MASTER以外に住人がいる
			; うふふ目撃
			; 扫除目撃
			; 战斗训练目撃
			; etc...
			CALL INTRUDER_WITH_MASTER_OTHER
		ELSE
			; MASTERのみ
			; 扫除目撃
			; 战斗训练目撃
			; etc...
			CALL INTRUDER_WITH_MASTER
		ENDIF
	ELSEIF FLAG:訪問者の現在位置 == FLAG:侵入者の現在位置
		; 訪問者と同室
		IF 1 <= 住人人数
			; 訪問者以外に住人がいる
			; 住人との絡み目撃
			; etc...
			CALL INTRUDER_WITH_VISITER_OTHER
		ELSE
			; 訪問者のみ
			; etc...
			CALL INTRUDER_WITH_VISITER
		ENDIF
	ELSEIF 1 <= 住人人数
		; 住人と同室
		; 自慰目撃
		; etc...
		CALL INTRUDER_WITH_OTHER
	ELSE
		; 侵入者以外無人
		;   部屋をちらかす
		;   私室の場合家捜し
		;   etc...
		CALL INTRUDER_DO_SOLO
	ENDIF
ENDIF
RETURN 0

;-------------------------------------------------------------------------------
;侵入者(こいし)アクション
;・MASTER, 訪問者以外に住人がいる
@INTRUDER_WITH_MASTER_VISITER_OTHER
RETURN 0

;-------------------------------------------------------------------------------
;侵入者(こいし)アクション
; MASTER, 訪問者のみ
@INTRUDER_WITH_MASTER_VISITER
RETURN 0

;-------------------------------------------------------------------------------
;侵入者(こいし)アクション
;・MASTER以外に住人がいる
@INTRUDER_WITH_MASTER_OTHER
;コマンドが战斗训练
IF 411 == SELECTCOM
	CALL INTRUDER_TRAINING
ENDIF
RETURN 0

;-------------------------------------------------------------------------------
;侵入者(こいし)アクション
;・MASTERのみ
@INTRUDER_WITH_MASTER
;前回、今回コマンドが覗き
IF 404 == SELECTCOM
	CALL INTRUDER_PEEPING
	;コマンドが战斗训练
ELSEIF 411 == SELECTCOM
	CALL INTRUDER_TRAINING
ENDIF
RETURN 0

;-------------------------------------------------------------------------------
;侵入者(こいし)アクション
;・覗きコマンド
@INTRUDER_PEEPING
IF 0 != TFLAG:現在覗いている場所
	IF IS_DISP_INTRUDER()
		PRINTFORMW こいしは%CALLNAME:MASTER%の背後から一緒に【%GETPLACENAME(TFLAG:現在覗いている場所)%】を覗き込んでいる。
	ENDIF
ENDIF
RETURN 0

;-------------------------------------------------------------------------------
;侵入者(こいし)アクション
;・战斗训练コマンド
@INTRUDER_TRAINING
IF IS_DISP_INTRUDER()
	IF RAND:4 != 0
		PRINTFORML こいしは%CALLNAME:MASTER%の放つ弾幕を巧みに回避している。
		PRINTDATAL
			DATAFORM 「よっ！はっ！とっ！」
			DATAFORM 「荒ぶる鷹のポーズ！！」
			DATAFORM 「グリコ！！」
		ENDDATA
	ELSE
		PRINTFORML こいしは%CALLNAME:MASTER%の放つ弾幕を上体を大きくのけぞらせて回避した。
		PRINTDATAL
			DATAFORM 「まとりっくす！！」
			DATAFORM 「貧乳回避っていうなーー」
		ENDDATA
	ENDIF
	IF RAND:4 != 0
		PRINTFORML しかしこいしの能力のせいで%CALLNAME:MASTER%にも周囲のメイド妖精たちにもその巧みな回避は気付いてもらえず、
		PRINTFORML 誰にも褒めてもらえなかった。
	ELSE
		PRINTFORML ところが、回避し損ねた弾幕がこいしの顔面に命中し、こいしはその場でじたばたもがいている。
		PRINTFORML しかしこいしの能力のせいで%CALLNAME:MASTER%にも周囲のメイド妖精たちにも気付いてもらえず、
		PRINTFORML 誰にも介抱してもらえなかった。
	ENDIF
ENDIF
RETURN 0

;-------------------------------------------------------------------------------
;侵入者(こいし)アクション
;・訪問者以外に住人がいる
@INTRUDER_WITH_VISITER_OTHER
RETURN 0

;-------------------------------------------------------------------------------
;侵入者(こいし)アクション
;・訪問者のみ
@INTRUDER_WITH_VISITER
RETURN 0

;-------------------------------------------------------------------------------
;侵入者(こいし)アクション
;・住人と同室
@INTRUDER_WITH_OTHER
RETURN 0

;-------------------------------------------------------------------------------
;侵入者(こいし)アクション
;・侵入者以外無人
;@return 常に0
@INTRUDER_DO_SOLO
IF 0 == RAND:2
	; 場所がどこであれ部屋をちらかす
	CALL INTRUDER_SCATTER
ELSE
	; 私室であれば家捜し
	CALL INTRUDER_CHK_ROOM
ENDIF
RETURN 0

;-------------------------------------------------------------------------------
;侵入者(こいし)アクション
;・侵入者以外無人
;・場所がどこであれ部屋をちらかす
;@return 常に0
@INTRUDER_SCATTER
FLAG:(100 + FLAG:侵入者の現在位置) += (RAND:100 + 10)
IF IS_DISP_INTRUDER()
	PRINTFORMW こいしは【%GETPLACENAME(FLAG:侵入者の現在位置)%】を散らかした。
ENDIF
RETURN 0

;-------------------------------------------------------------------------------
;侵入者(こいし)アクション
;・侵入者以外無人
;・私室であれば家捜し
;@return 常に0
@INTRUDER_CHK_ROOM
#DIM 住人
#DIM ぱんつ
;誰かの私室の場合その人物のぱんつをチェック（子悪魔どうしよう…）
SELECTCASE FLAG:侵入者の現在位置
	CASE 位置_红魔馆_帕秋莉私室
		住人 = 人物_帕秋莉
	CASE 位置_红魔馆_小恶魔私室
		住人 = 人物_小恶魔
	CASE 位置_红魔馆_守卫小屋
		住人 = 人物_美铃
	CASE 位置_红魔馆_咲夜私室
		住人 = 人物_咲夜
	CASE 位置_红魔馆_蕾米莉亚私室
		住人 = 人物_蕾米莉亚
	CASE 位置_红魔馆_地下室
		住人 = 人物_芙兰朵露
	CASE 位置_琪露诺的家
		住人 = 人物_琪露诺
	CASE 位置_大妖精的家
		住人 = 人物_大妖精
	CASE 位置_魔理沙的家
		住人 = 人物_魔理沙
	CASE 位置_博麗神社
		住人 = 人物_灵梦
	CASE 位置_露米娅的家
		住人 = 人物_露米娅
	CASE 位置_爱丽丝的家
		住人 = 人物_爱丽丝
	CASEELSE
		住人 = 0
ENDSELECT
IF 0 != 住人 
	; 誰かの私室であれば下着チェック
	IF IS_DISP_INTRUDER()
		PRINTFORML こいしは【%GETPLACENAME(FLAG:侵入者の現在位置)%】を漁った。
		;訪問者の方が影響力大
		IF CFLAG:住人:屈服度 > CFLAG:住人:好感度
			IF FLAG:Flow版本
				ぱんつ = NTR_PANTS(住人,1)
			ELSE
				ぱんつ = PANTS_TODAY_ADULT(住人, 1)
			ENDIF
			PRINTFORML こいしは%PANTSNAME(ぱんつ)%をみつけた。
			;相手によってリアクション変化
			SELECTCASE 住人
				CASE 人物_美铃
				CASE 人物_小恶魔
				CASE 人物_帕秋莉
				CASE 人物_咲夜
					PRINTFORML （さすが%CALLNAME:住人%・・おっとなぁ～）
				CASE 人物_蕾米莉亚
					PRINTFORML （さすがお嬢様・・おっとなぁ～。お姉ちゃん、負けてるよぉ？）
				CASEELSE
					PRINTFORML （なんと%CALLNAME:住人%がこんなアダルツな・・）
			ENDSELECT
		ELSE
			IF FLAG:Flow版本
				ぱんつ = NTR_PANTS(住人,0)
			ELSE
				ぱんつ = PANTS_TODAY(住人)
			ENDIF
			PRINTFORMW こいしは%PANTSNAME(ぱんつ)%をみつけた。
			;相手によってリアクション変化させたい
		ENDIF
	ENDIF
ENDIF
RETURN 0
