;-------------------------------------------------------------------------------
;会话コマンド(拡張版) - 妖精メイド増員 実行判定
; RETURN 0 コマンドの実行不可
; RETURN 1 コマンドの実行可能
@COM300_02_ENABLE
RESULTS:0 = %"[ 2]-不增加新的女仆吗？"%
IF (NO:TARGET != 4 ) && (NO:TARGET != 5)
	;咲夜さんか蕾米莉亚お嬢様だけ許可を出せる
	RETURN 0
ELSEIF TCVAR:会话累積値 > 0
	;初回の会话じゃないとダメ(毎回出るとクッソうざいので)
	RETURN 0
ELSEIF 妖精メイド採用状態:(妖精メイド雇用上限-1) != 妖精メイド採用状態_未許可
	;最後のメイドに採用許可が出ていたら、それ以上は増やせない
	RETURN 0
ELSE
	RETURN 1
ENDIF
RETURN 0

;-------------------------------------------------------------------------------
;会话コマンド(拡張版) - 妖精メイド増員
; RETURN 0 コマンドの実行に失敗
; RETURN 1 コマンドの実行に成功
; 咲夜さんかお嬢様しか想定してないんで、口上もここに混ぜ込んでいいよね？
@COM300_02
#DIM 成否判定
;実行判定が通らない時に呼ばれた時対策
CALL COM300_02_ENABLE()
IF !RESULT:0
	RETURN 0
ENDIF
;清掃、戦闘、料理の３技能が伸びてないと部下は増やせません。
成否判定 = ((ABL:MASTER:清扫技能+ABL:MASTER:战斗技能+ABL:MASTER:料理技能)/3) > FairyMaids_HiredCount()
;いつもの会话累積値増加など
TCVAR:会话累積値 += 200 / (3 + ABL:MASTER:话术技能)
TIME += 10
EXP:MASTER:会话经验 ++
TRYCCALLFORM COM300_02_%TOSTR(成否判定,"00")%
	RETURN RESULT:0
CATCH
	RETURN 0
ENDCATCH
RETURN 0

;-------------------------------------------------------------------------------
;会话コマンド(拡張版) - 妖精メイド増員 - 失敗
; RETURN 0 コマンドの実行に失敗
; RETURN 1 コマンドの実行に成功
@COM300_02_00
TRYCCALLFORM COM300_02_00_%TOSTR(TARGET,"00")%
	RETURN RESULT:0
CATCH
	RETURN 0
ENDCATCH
RETURN 0

;-------------------------------------------------------------------------------
;会话コマンド(拡張版) - 妖精メイド増員 - 失敗 - 咲夜の反応
@COM300_02_00_04
PRINTFORML %"「"+CALLNAME:MASTER+"只要再多做些工作就好了吧？」"%
PRINTFORML %"很爽快地被拒绝了……"%
RETURN 0

;-------------------------------------------------------------------------------
;会话コマンド(拡張版) - 妖精メイド増員 - 失敗 - 蕾米莉亚の反応
@COM300_02_00_05
PRINTFORML %"「驳回。想要部下的话，做了更多的工作之后再来说。」"%
PRINTFORML %"很爽快地被拒绝了……"%
RETURN 0

;-------------------------------------------------------------------------------
;会话コマンド(拡張版) - 妖精メイド増員 - 成功
; RETURN 0 コマンドの実行に失敗
; RETURN 1 コマンドの実行に成功
@COM300_02_01
CALLF FairyMaids_addVacancy()
TRYCCALLFORM COM300_02_01_%TOSTR(TARGET,"00")%
	RETURN RESULT:0
CATCH
	RETURN 0
ENDCATCH
RETURN 0

;-------------------------------------------------------------------------------
;会话コマンド(拡張版) - 妖精メイド増員 - 成功 - 咲夜の反応
@COM300_02_01_04
PRINTFORML %"「如果能找到新的妖精的话，可以带过来。不过新人女仆的教育就拜托你了。」"%
PRINTFORML %"「教育别人，对"+CALLNAME:MASTER+"也是有好处的。」"%
RETURN 0

;-------------------------------------------------------------------------------
;会话コマンド(拡張版) - 妖精メイド増員 - 成功 - 蕾米莉亚の反応
@COM300_02_01_05
PRINTFORML %"「女仆啊……嗯，不是很好吗？把看起来不错的妖精抓过来。」"%
PRINTFORML %"「带过来的妖精可以做"+CALLNAME:MASTER+"的部下，但是一定要好好教育。」"%
RETURN 0
