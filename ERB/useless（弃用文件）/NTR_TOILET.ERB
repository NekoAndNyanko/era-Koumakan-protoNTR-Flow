;-----------------------------------------------------------
;公众便所の贞操帯チェック
@NTR_LOCK(奴隷)
#FUNCTION
#DIM 奴隷
IF TALENT:奴隷:公众便所
	;鍵を持っていたらロック解除
	IF FLAG:贞操帯鍵購入フラグ == 奴隷
		RETURNF 0
	ENDIF
	RETURNF 1
ELSE
	RETURNF 0
ENDIF

;-----------------------------------------------------------
;公众便所利用権のお値段を求める
;@param 奴隷     キャラクターの番号
;@param 鍵の種類 鍵の種類（ビット演算）
;    0b00000001:ニプルシールド
;    0b00000010:フルカップ
;    0b00000100:ブラジャー
;    0b00001000:菊花シールド
;    0b00010000:リアシールド
;    0b00100000:自慰防止板
;    0b01000000:フロントシールド
;    0b10000000:ウエストベルト
;@return 鍵のお値段
@NTR_COST_UNLOCK(奴隷, 鍵の種類)
#FUNCTION
#DIM 奴隷
#DIM 鍵の種類
#DIM 基本価格
#DIM 基本倍率
#DIM 購入価格
SIF 鍵の種類 == 0
	鍵の種類 = 0b11111000
;素質により基本価格決定。
基本価格 = MAX(TALENT:奴隷:淫乱*10,10)
基本価格 += LIMIT(TALENT:奴隷:性的魅力*3,0,3)
;部位の技能が高いほどお値段も高くなります。
基本倍率 = 1
IF GETBIT(鍵の種類,0) ;乳首権
	基本倍率 += LIMIT(ABL:奴隷:Ｂ感觉,1,2)
ENDIF
IF GETBIT(鍵の種類,1) ;乳房権
	基本倍率 += LIMIT(ABL:奴隷:胸,0,2)
ENDIF
IF GETBIT(鍵の種類,2) ;上半身権
	基本倍率 += LIMIT(ABL:奴隷:胸,0,2)
ENDIF
IF GETBIT(鍵の種類,3) ;Ａ爱抚権
	基本倍率 += LIMIT(ABL:奴隷:Ａ感觉,1,2)
ENDIF
IF GETBIT(鍵の種類,4) ;Ａ挿入権
	基本倍率 += (LIMIT(ABL:奴隷:菊花,0,3) + LIMIT(ABL:奴隷:腰,0,3)) / ((BASE:奴隷:Ａ松弛度>700)+(BASE:奴隷:Ａ松弛度>450)+(BASE:奴隷:Ａ松弛度>250)+1)
ENDIF
IF GETBIT(鍵の種類,5) ;Ｃ爱抚権
	基本倍率 += LIMIT(ABL:奴隷:Ｃ感觉,1,2)
ENDIF
IF GETBIT(鍵の種類,6) ;Ｖ挿入権
	基本倍率 += (LIMIT(ABL:奴隷:Ｖ感觉,1,2) + LIMIT(ABL:奴隷:膣,0,3) + LIMIT(ABL:奴隷:腰,0,3)) / ((BASE:奴隷:Ｖ松弛度>700)+(BASE:奴隷:Ｖ松弛度>450)+(BASE:奴隷:Ｖ松弛度>250)+1)
ENDIF
IF GETBIT(鍵の種類,7) ;種付け権
	基本倍率 += (LIMIT(ABL:奴隷:Ｖ感觉,1,100)+LIMIT(ABL:奴隷:膣,1,100)+LIMIT(ABL:奴隷:腰,1,100)) / ((BASE:奴隷:Ｖ松弛度>700)+(BASE:奴隷:Ｖ松弛度>450)+(BASE:奴隷:Ｖ松弛度>250)+1)
ENDIF
;妊娠していると半額。逆に妊娠していなくて危险日だと5割増
IF TALENT:奴隷:妊娠
	基本倍率 /= 2
ELSEIF TALENT:奴隷:危险日
	基本倍率 *= 2
	基本倍率 /= 2
ENDIF
DEBUGPRINTFORML %"『"+CALLNAME:奴隷+"』の素質による基本価格:"+TOSTR(基本価格)+"、技能による基本倍率:"+TOSTR(基本倍率)%
SELECTCASE 奴隷
	CASE 人物_美铃
		購入価格 = ( 基本価格 * 基本倍率 *  5 ) 
	CASE 人物_小恶魔
		購入価格 = ( 基本価格 * 基本倍率 *  5 )
	CASE 人物_帕秋莉
		購入価格 = ( 基本価格 * 基本倍率 *  7 )
	CASE 人物_咲夜
		購入価格 = ( 基本価格 * 基本倍率 *  6 )
	CASE 人物_蕾米莉亚
		購入価格 = ( 基本価格 * 基本倍率 * 10 )
	CASE 人物_芙兰朵露
		購入価格 = ( 基本価格 * 基本倍率 * 10 )
	CASE 人物_艾丽娅
		購入価格 = ( 基本価格 * 基本倍率 *  5 )
	CASE 人物_琪露诺
		購入価格 = ( 基本価格 * 基本倍率 *  3 )
	CASE 人物_大妖精
		購入価格 = ( 基本価格 * 基本倍率 *  3 )
	CASE 人物_魔理沙
		購入価格 = ( 基本価格 * 基本倍率 *  5 )
	CASE 人物_灵梦
		購入価格 = ( 基本価格 * 基本倍率 * 10 )
	CASE 人物_露米娅
		購入価格 = ( 基本価格 * 基本倍率 *  3 )
	CASE 人物_爱丽丝
		購入価格 = ( 基本価格 * 基本倍率 *  7 )
	CASEELSE ;その他
		購入価格 = ( 基本価格 * 基本倍率 *  5 )
ENDSELECT
RETURNF 購入価格

;-----------------------------------------------------------
;公众便所利用権のお値段を求める（あなた向け特別価格）
@NTR_COST_UNLOCK_SP(奴隷,価格)
#FUNCTION
#DIM 奴隷
#DIM 価格
;「あなた」向けのペナルティ。寝取り返す事に対するお仕置きとして、陥落回数で金額が飛躍的に伸びます。
DEBUGPRINTFORML %"『"+CALLNAME:奴隷+"』の修正前価格:"+TOSTR(価格)%
価格 = 価格 + ( 価格 * EXP:奴隷:NTR陷落经验*EXP:奴隷:NTR陷落经验 /100 )
;「あなた」向けのペナルティ。恋人だと買えば買うほどお値段割増。
IF TALENT:奴隷:恋慕
	IF TALENT:奴隷:人妻
		価格 = 価格 + ( 価格 * SQRT(EXP:MASTER:买春经验) / 6 )
	ELSE
		価格 = 価格 + ( 価格 * SQRT(EXP:MASTER:买春经验) / 10 )
	ENDIF
ENDIF
;「あなた」向けのペナルティ。浮気公認してるとその分お値段割増。
IF TALENT:奴隷:浮気公認
	価格 = 価格 + ( 価格 * TALENT:奴隷:浮気公認 / 10 )
ENDIF
DEBUGPRINTFORML %"『"+CALLNAME:奴隷+"』の修正後価格:"+TOSTR(価格)%
RETURNF 価格

;-----------------------------------------------------------
;贞操帯の鍵を発見した時の反応
;奴隷…発見したキャラクター
@NTR_LOCK_REACTION(奴隷)
#DIM 奴隷
;鍵を持っていなければ処理なし
IF !FLAG:贞操帯鍵購入フラグ
	RETURN 0
ENDIF
;一度反応しているのならそれ以上は反応しない
IF !CFLAG:奴隷:NTR贞操帯反応
	RETURN 0
ENDIF
;本人の鍵かどうかで反応が変わる
IF FLAG:贞操帯鍵購入フラグ == 奴隷
	;買われた本人の反応
	PRINTFORM %CALLNAME:MASTER%找到了%CALLNAME:奴隷%贞操带的钥匙、
	IF TALENT:奴隷:亲爱
		PRINTFORMW 对能掌握自己的贞操而感到高兴
		CFLAG:奴隷:好感度 += 100
	ELSE
		PRINTFORMW 对能帮助%NTR_NAME(0)%控制自己而感到高兴。
		CFLAG:奴隷:好感度 += 50
	ENDIF
	TRYCALLLIST
		FUNC NTR_KOJO_K{CFLAG:奴隷:口上パターン}_16_0(奴隷)
		FUNC NTR_KOJO_K_16_0(奴隷)
	ENDFUNC
	PRINTFORMW 
ELSE
	;本人以外の鍵を見つけた場合
	PRINTFORM %CALLNAME:MASTER%找到了%CALLNAME:奴隷%贞操带的钥匙、
	IF TALENT:奴隷:公众便所  && TALENT:奴隷:亲爱 
		PRINTFORMW 用悲哀的目光注视着%CALLNAME:MASTER%。
		CFLAG:奴隷:好感度 -= 100
	ELSEIF TALENT:奴隷:公众便所
		PRINTFORMW 很高兴能帮到%NTR_NAME(0)%。
	ELSEIF TALENT:奴隷:NTR
		PRINTFORMW 很高兴能帮到%NTR_NAME(0)%。
	ELSEIF TALENT:奴隷:恋慕
		PRINTFORMW 以轻蔑的目光瞪了%CALLNAME:MASTER%一眼。
		CFLAG:奴隷:好感度 -= 500
	ELSEIF 奴隷==人物_美铃 || 奴隷==人物_帕秋莉 || 奴隷==人物_咲夜 || 奴隷==人物_蕾米莉亚 || 奴隷==人物_魔理沙 || 奴隷==人物_灵梦 || 奴隷==人物_爱丽丝
		PRINTFORMW 以轻蔑的目光瞪了%CALLNAME:MASTER%一眼。
		CFLAG:奴隷:好感度 -= 200
	ELSE
		PRINTFORMW 用惊讶的目光凝视着%CALLNAME:MASTER%。
		CFLAG:奴隷:好感度 -= 100
	ENDIF
	TRYCALLLIST
		FUNC NTR_KOJO_K{CFLAG:奴隷:口上パターン}_16_1(奴隷)
		FUNC NTR_KOJO_K_16_1(奴隷)
	ENDFUNC
	PRINTFORMW 
ENDIF
;反応フラグ解除
CFLAG:奴隷:NTR贞操帯反応 = 0
RETURN 1

;-------------------------------------------------------------------------------
;コマンドに応じた贞操帯鍵購入反応呼び出し
@NTR_LOCK_REACTION_COM(奴隷)
#DIM 奴隷
;贞操帯購入チェックの必要なコマンド一覧
;肛门爱抚、阴蒂爱抚、手指插入
IF SELECTCOM == 314 || SELECTCOM == 315 || SELECTCOM == 316 
	CALL NTR_LOCK_REACTION(奴隷)
ENDIF
RETURN 1

;-------------------------------------------------------------------------------
;贞操帯解除処理
;　戻り値：1…解除成功　0…解除失敗
@NTR_UNLOCK_CHASTITY_BELT(奴隷)
#DIM 奴隷
;そもそも奴隷じゃないので解除不要、成功扱いとする（フェイルセーフ）
SIF !TALENT:奴隷:公众便所
	RETURN 1
;ノーパンなら解除不要、成功扱いとする（フェイルセーフ）
SIF EQUIP:奴隷:下半身下着１ == 0 && EQUIP:奴隷:下半身下着２ == 0
	RETURN 1
;鍵を持ってないと解除不可能
SIF FLAG:贞操帯鍵購入フラグ != 奴隷
	RETURN 0
PRINTFORML %CALLNAME:MASTER%使用从%NTR_NAME(0)%那买来的钥匙，解开了%CALLNAME:奴隷%身上的贞操带。
EQUIP:奴隷:下半身下着１ = 0
EQUIP:奴隷:下半身下着２ = 0
RETURN 1