;-------------------------------------------------------------------------------
;子供の訪問関係.ERB
;
;追加フラグ
;CFLAG:子供訪問フラグ 子供の訪問フラグ(成人した子供限定のフラグ)
;	  0       紅魔館へ訪問する確率が発生
;	  -30~-1  子供の紅魔館滞在日数
;	  -999    紅魔館に永久滞在(解除可能)
;	  1~10    訪問する確率が発生するまでの残り日数
;	  101~130 呼び出したときの訪問日数
;	  999     無期限滞在の前処理
;

;-------------------------------------------------------------------------------
;子供が紅魔館へ帰って来る処理など
;-------------------------------------------------------------------------------
;BEFORETRAIN.ERB
@子供訪問_1
#DIM LOOP_CHR			; LOCAL
#DIM 確率母数			; LOCAL:1 訪問確率の分母
#DIM 訪問人数           ; LOCAL:2

;PRINTFORMW {CHARANUM}
;子供キャラが誰もいないなら処理終了
SIF CHARANUM <= 開始時人数
	RETURN 0

;特殊な訪問処理
FOR LOOP_CHR, 開始時人数, CHARANUM
	IF CFLAG:LOOP_CHR:子供訪問フラグ >= 101
		PRINTFORMW %CALLNAME:LOOP_CHR%を紅魔館へ呼び寄せました。
		;正門
		CFLAG:LOOP_CHR:現在位置 = 位置_红魔馆_正门
		;滞在処理
		IF CFLAG:LOOP_CHR:子供訪問フラグ == 999
			CFLAG:LOOP_CHR:子供訪問フラグ = -999
		ELSE
			CFLAG:LOOP_CHR:子供訪問フラグ = (CFLAG:LOOP_CHR:子供訪問フラグ - 100) * -1
		ENDIF
	ENDIF
NEXT

訪問人数 = 0
;確率が発生するキャラの訪問確率処理
FOR LOOP_CHR, 開始時人数, CHARANUM
	;3人訪問すると終了
	SIF 訪問人数 == 3
		RETURN 0
	;成人以外はスキップ
	SIF CFLAG:LOOP_CHR:成長度 != 成長度_成人
		CONTINUE
	IF CFLAG:LOOP_CHR:子供訪問フラグ == 0
		;プレイヤーとの好感度が高いと確率上昇
		IF CFLAG:LOOP_CHR:好感度 >= 300
			確率母数 = 50
		ELSEIF CFLAG:LOOP_CHR:好感度 >= 150
			確率母数 = 75
		ELSE
			確率母数 = 100
		ENDIF
		;PRINTFORMW {LOOP_CHR}
		;通常で20%の確率で訪問してくる
		IF RAND:確率母数 < 20
			PRINTFORMW %CALLNAME:LOOP_CHR%が紅魔館へ遊びに来たようです。
			;正門
			CFLAG:LOOP_CHR:現在位置 = 位置_红魔馆_正门
			;とりあえず1日滞在処理
			CFLAG:LOOP_CHR:子供訪問フラグ = -1
			訪問人数 ++
		ENDIF
	ENDIF
NEXT



;-------------------------------------------------------------------------------
;子供が人里へ帰って行く処理など
;-------------------------------------------------------------------------------
;AFTERTRA.ERB
@子供訪問_2
#DIM LOOP_CHR			; LOCAL

;子供キャラが誰もいないなら処理終了
SIF CHARANUM <= 開始時人数
	RETURN 0
;訪問確率発生までの残り日数減少、訪問していたら残り滞在日数減少処理
FOR LOOP_CHR, 開始時人数, CHARANUM
	;成人以外はスキップ
	SIF CFLAG:LOOP_CHR:成長度 != 成長度_成人
		CONTINUE
	;無期限滞在中ならスキップ
	SIF CFLAG:LOOP_CHR:子供訪問フラグ == -999
		CONTINUE

	;帰る口上とその処理
	IF CFLAG:LOOP_CHR:子供訪問フラグ == -1
		;お持ち帰りされているなどで移动できるマップ内にいない場合、もう1日紅魔館に残る
		IF CFLAG:LOOP_CHR:現在位置 < 位置_訪問者宅
			PRINTFORMW %CALLNAME:LOOP_CHR%は里へと帰って行きました。
			CFLAG:LOOP_CHR:現在位置 = 位置_里の住処
			CFLAG:LOOP_CHR:子供訪問フラグ = RAND(8, 13)
		ENDIF
		;残り滞在日数減少
	ELSEIF CFLAG:LOOP_CHR:子供訪問フラグ < 0
		CFLAG:LOOP_CHR:子供訪問フラグ ++
		;訪問確率発生までの残り日数減少
	ELSEIF CFLAG:LOOP_CHR:子供訪問フラグ > 0
		CFLAG:LOOP_CHR:子供訪問フラグ --
	ENDIF
NEXT


;-------------------------------------------------------------------------------
;子供の呼び出し
;-------------------------------------------------------------------------------
;SHOP.ERB
@子供訪問_設定

IF GET_TARGET_CHILDREN(MASTER, 4) <= 0
	PRINTFORMW 設定できる子供が一人もいません
	RETURN
ENDIF

CALL 子供訪問_設定2, MASTER


@子供訪問_設定2, 母親番号
#DIM 母親番号           ; ARG
#DIM 子供番号           ; LOCAL
#DIM LOOP_CHR			; LOCAL

PRINTL 子供の呼び出し設定
PRINTL
FOR LOOP_CHR, 開始時人数 ,CHARANUM
	IF 親子関係(母親番号, LOOP_CHR)
		PRINTFORM  %CALLNAME:LOOP_CHR, 18, LEFT%　
		IF CFLAG:LOOP_CHR:子供訪問フラグ == -999
			PRINTFORM %"館に無期限滞在中", 30, LEFT%
		ELSEIF CFLAG:LOOP_CHR:子供訪問フラグ < 0
			PRINTFORM %@"館に滞在中、帰宅まであと{ABS(CFLAG:LOOP_CHR:子供訪問フラグ)}日", 30, LEFT%
		ELSEIF CFLAG:LOOP_CHR:子供訪問フラグ == 999
			PRINTFORM %"館に無期限滞在予定", 30, LEFT%
		ELSEIF CFLAG:LOOP_CHR:子供訪問フラグ >= 101
			PRINTFORM %@"館に{CFLAG:LOOP_CHR:子供訪問フラグ - 100}日間の滞在予定", 30, LEFT%
		ENDIF
		IF CFLAG:LOOP_CHR:子供訪問フラグ >= 101
			PRINTBUTTON "[滞在日数変更]", LOOP_CHR
			PRINT 　
		ELSEIF CFLAG:LOOP_CHR:子供訪問フラグ >= 0
			PRINTBUTTON "[呼び出す]", LOOP_CHR
			PRINT 　　　
		ELSE
			PRINTBUTTON "[滞在日数変更]", LOOP_CHR
			PRINT 　
		ENDIF
		IF CFLAG:LOOP_CHR:子供訪問フラグ == -999
			PRINTBUTTON "[家に帰す]", LOOP_CHR * -1
		ENDIF
		PRINTL
	ENDIF
NEXT
PRINTL
PRINTL [0] 返回
PRINTL
INPUT

SELECTCASE RESULT
	CASE 0
		RETURN
	CASE -(CHARANUM-1) TO -(開始時人数)
		子供番号 = ABS(RESULT)
		IF 親子関係(母親番号, 子供番号)
			CFLAG:子供番号:子供訪問フラグ = -1
			PRINTFORMW %CALLNAME:子供番号%を次の日に館から出るようにしました
		ENDIF

	CASE 開始時人数 TO CHARANUM-1
		子供番号 = RESULT
		IF 親子関係(母親番号, 子供番号)
			PRINTL 館への滞在日数の設定(1~30まで、0で設定しない、99で無期限滞在)
			$LOOP
			INPUT

			SELECTCASE RESULT
				CASE 1 TO 30
					IF CFLAG:子供番号:子供訪問フラグ >= 0
						CFLAG:子供番号:子供訪問フラグ = 100 + RESULT
					ELSE
						CFLAG:子供番号:子供訪問フラグ = -(RESULT)
					ENDIF
				CASE 99
					IF CFLAG:子供番号:子供訪問フラグ >= 0
						CFLAG:子供番号:子供訪問フラグ = 999
					ELSE
						CFLAG:子供番号:子供訪問フラグ = -999
					ENDIF
				CASE 0
					; 何もせず抜ける
				CASEELSE
					CLEARLINE 1
					GOTO LOOP
			ENDSELECT
		ENDIF
ENDSELECT

RESTART

;対象ARGの子供が何人いるか、ARG:1は特定の成長度のみを指すかどうか
@GET_TARGET_CHILDREN(母親番号, 成長度)
#FUNCTION
#DIM 母親番号       ; ARG
#DIM 成長度         ; ARG:1
#DIM LOOP_CHR		; LOCAL
#DIM 子供人数       ; LOCAL:1

SIF CHARANUM <= 開始時人数
	RETURNF -1

子供人数 = 0

FOR LOOP_CHR, 開始時人数, CHARANUM
	SIF 成長度 > 0 && CFLAG:LOOP_CHR:成長度 != 成長度
		CONTINUE
	IF 親子関係(母親番号, LOOP_CHR)
		子供人数 ++
	ENDIF
NEXT

RETURNF 子供人数


;-------------------------------------------------------------------------------
;親子関係の取得
;-------------------------------------------------------------------------------
;BEFORETRAIN.ERB
@親子関係(親, 子)
#FUNCTION
#DIM 親
#DIM 子

; ??? この148は何？
SIF NO:子 < 148
	RETURNF 0
SIF (CFLAG:子:父親 == 親 || CFLAG:子:母親 == 親)
	RETURNF 1
RETURNF 0





