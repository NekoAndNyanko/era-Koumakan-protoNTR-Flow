;-------------------------------------------------------------------------------
;キス、爱抚、侍奉時メッセージ・口上分岐
;主人同席での見せつけも考慮
;奴隷…対象キャラ
;行為…行為コード 2:キス 3:爱抚 4:胸爱抚 5:口交 6:69 7:素股
@NTR_KISS_PET_FELLA_MSG_KOJO_01(奴隷, 行為)
#DIM 奴隷
#DIM 行為
SIF FLAG:口上颜色
	CALL KOJO_COLOR(奴隷)
;主人同席（見せつけ）
IF CFLAG:MASTER:現在位置 == FLAG:访问者当前位置 && 奴隷 != MASTER
	;開始
	IF FLAG:訪問者との行為の終了時刻 == 0
		CALLFORM NTR_MESSAGE_W_{行為}_0(奴隷)
		TRYCALLLIST
			FUNC NTR_KOJO_KW{CFLAG:奴隷:口上パターン}_{行為}_0(奴隷)
			FUNC NTR_KOJO_KW_{行為}_0(奴隷)
			FUNC NTR_KOJO_K{CFLAG:奴隷:口上パターン}_{行為}_0(奴隷)
		ENDFUNC
		;継続
	ELSE
		CALLFORM NTR_MESSAGE_W_{行為}_1(奴隷)
		TRYCALLLIST
			FUNC NTR_KOJO_KW{CFLAG:奴隷:口上パターン}_{行為}_1(奴隷)
			FUNC NTR_KOJO_KW_{行為}_1(奴隷)
			FUNC NTR_KOJO_K{CFLAG:奴隷:口上パターン}_{行為}_1(奴隷)
		ENDFUNC
	ENDIF
	CFLAG:奴隷:同席目撃回数 ++
	CFLAG:奴隷:同席目撃時行為 = MAX(CFLAG:奴隷:同席目撃時行為, 行為)
	;二人きり
ELSE
	IF NTR_CHK_VISIBLE(FLAG:访问者当前位置)
		;開始
		IF FLAG:訪問者との行為の終了時刻 == 0
			CALLFORM NTR_MESSAGE_{行為}_0(奴隷)
			TRYCALLLIST
				FUNC NTR_KOJO_K{CFLAG:奴隷:口上パターン}_{行為}_0(奴隷)
				FUNC NTR_KOJO_K_{行為}_0(奴隷)
			ENDFUNC
			;継続
		ELSE
			CALLFORM NTR_MESSAGE_{行為}_1(奴隷)
			TRYCALLLIST
				FUNC NTR_KOJO_K{CFLAG:奴隷:口上パターン}_{行為}_1(奴隷)
				FUNC NTR_KOJO_K_{行為}_1(奴隷)
			ENDFUNC
		ENDIF
	ENDIF
ENDIF
RESETCOLOR
RETURN 0

;-------------------------------------------------------------------------------
;キス、爱抚、侍奉時メッセージ・口上分岐
;主人同席での見せつけも考慮
;奴隷…対象キャラ
;行為…行為コード 2:キス 3:爱抚 4:胸爱抚 5:口交 6:69 7:素股
@NTR_KISS_PET_FELLA_MSG_KOJO_2(奴隷, 行為)
#DIM 奴隷
#DIM 行為
SIF FLAG:口上颜色
	CALL KOJO_COLOR(奴隷)
;主人同席（見せつけ）
IF CFLAG:MASTER:現在位置 == FLAG:访问者当前位置 && 奴隷 != MASTER
	CALLFORM NTR_MESSAGE_{行為}_2(奴隷)
	TRYCALLLIST
		FUNC NTR_KOJO_KW{CFLAG:奴隷:口上パターン}_{行為}_2(奴隷)
		FUNC NTR_KOJO_KW_{行為}_2(奴隷)
		FUNC NTR_KOJO_K_{行為}_2(奴隷)
	ENDFUNC
	CFLAG:奴隷:同席目撃回数 ++
	CFLAG:奴隷:同席目撃時行為 = MAX(CFLAG:奴隷:同席目撃時行為, 行為)
	;二人きり
ELSE
	IF NTR_CHK_VISIBLE(FLAG:访问者当前位置)
		CALLFORM NTR_MESSAGE_{行為}_2(奴隷)
		TRYCALLLIST
			FUNC NTR_KOJO_K{CFLAG:奴隷:口上パターン}_{行為}_2(奴隷)
			FUNC NTR_KOJO_K_{行為}_2(奴隷)
		ENDFUNC
	ENDIF
ENDIF
RESETCOLOR
RETURN 0

;-------------------------------------------------------------------------------
;性交時メッセージ・口上分岐
;主人同席での見せつけも考慮
;奴隷…対象キャラ
@NTR_SEX_MSG_KOJO(奴隷)
#DIM 奴隷
#DIM 開始継続
#DIM 主人同席
SIF FLAG:口上颜色
	CALL KOJO_COLOR(奴隷)
IF NTR_CHK_VISIBLE(FLAG:访问者当前位置)
	; 開始／継続フラグ
	開始継続 = ( FLAG:訪問者との行為の終了時刻 == 0 ? 0 # 1 )
	; 主人同席（見せつけ）フラグ
	主人同席 = ( CFLAG:MASTER:現在位置 == FLAG:访问者当前位置 ? 1 # 0 )
	CALL NTR_SEX_KOJO_EX(奴隷, 開始継続, 主人同席, "KOJO_PRE")
	CALL NTR_SEX_KOJO_EX(奴隷, 開始継続, 主人同席, "MESSAGE")
	CALL NTR_SEX_KOJO_EX(奴隷, 開始継続, 主人同席, "KOJO")
ENDIF
RESETCOLOR
RETURN 0

;-------------------------------------------------------------------------------
;性交時メッセージ・口上分岐
;主人同席での見せつけも考慮
;奴隷…対象キャラ
@NTR_SEX_FINISH_MSG_KOJO(奴隷)
#DIM 奴隷
#DIM 主人同席
SIF FLAG:口上颜色
	CALL KOJO_COLOR(奴隷)
IF NTR_CHK_VISIBLE(FLAG:访问者当前位置)
	; 主人同席（見せつけ）フラグ
	主人同席 = ( CFLAG:MASTER:現在位置 == FLAG:访问者当前位置 ? 1 # 0 )
	CALL NTR_SEX_KOJO_EX(奴隷, 2, 主人同席, "KOJO_PRE")
	CALL NTR_SEX_KOJO_EX(奴隷, 2, 主人同席, "MESSAGE")
	CALL NTR_SEX_KOJO_EX(奴隷, 2, 主人同席, "KOJO")
ENDIF
RESETCOLOR
RETURN 0

;-------------------------------------------------------------------------------
;性交時メッセージ分岐
;奴隷…対象キャラ
;行為段階…0=開始 1=継続 2=終了
;主人同席…0=二人きり 1=主人同席
;関数種別…口上または地の文の関数名の一部
@NTR_SEX_KOJO_EX(奴隷, 行為段階, 主人同席, 関数種別)
#DIM 奴隷
#DIM 行為段階
#DIM 主人同席
#DIMS 関数種別
#DIM 体位
SIF FLAG:口上テキスト設定 <= 0 && 関数種別 == "KOJO_PRE"
	RETURN 0
SIF FLAG:口上テキスト設定 <= 0 && 関数種別 == "KOJO"
	RETURN 0
SIF FLAG:情景テキスト設定 <= 0 && 関数種別 == "MESSAGE"
	RETURN 0
SIF 関数種別 != "MESSAGE" && 関数種別 != "KOJO" && 関数種別 != "KOJO_PRE"
	RETURN 0
体位 = FLAG:訪問者との行為 - NTR_行为_Ｖ正常位
SIF FLAG:訪問者との行為 > NTR_行为_Ｖ背面座位
	体位 = FLAG:訪問者との行為 - NTR_行为_Ｖ背面座位
IF 主人同席 != 0
	CFLAG:奴隷:同席目撃回数 ++
	CFLAG:奴隷:同席目撃時行為 = MAX(CFLAG:奴隷:同席目撃時行為, FLAG:訪問者との行為)
ENDIF


SIF FLAG:口上颜色
	CALL KOJO_COLOR(奴隷)
IF (NTR_KOJO_SEX_FIRSTTIME(奴隷,体位) || NTR_KOJO_SEX_FIRSTTIME(奴隷,1000)) && CFLAG:奴隷:(2000+体位) != 3 && (関数種別 == "KOJO" || 関数種別 == "KOJO_PRE")
	;简化口上书写，不写PRE（前口上）
	;在确定有第一次口上的情况下，跳过PRE绝对没什么问题，也防止输出两次
	IF 関数種別 == "KOJO"
		TRYCALLLIST
			FUNC NTR_KOJO_SEX_FIRSTTIME_K{奴隷}_{体位}_{行為段階}(奴隷)
			FUNC NTR_KOJO_SEX_FIRSTTIME_KU_{体位}_{行為段階}(奴隷)
			;通用体位
			FUNC NTR_KOJO_SEX_FIRSTTIME_K{奴隷}_U_{行為段階}(奴隷)
			FUNC NTR_KOJO_SEX_FIRSTTIME_KU_U_{行為段階}(奴隷)
[SKIPSTART]
			FUNC NTR_%関数種別%_%LOCALS%{奴隷}_8_{体位}_{行為段階}(奴隷)
			FUNC NTR_%関数種別%_%LOCALS%{奴隷}_8_U_{行為段階}(奴隷)
			FUNC NTR_%関数種別%_%LOCALS%_8_{体位}_{行為段階}(奴隷)
			FUNC NTR_%関数種別%_%LOCALS%_8_U_{行為段階}(奴隷)
			FUNC NTR_%関数種別%_%LOCALS%_8_0_{行為段階}(奴隷)
[SKIPEND]
		ENDFUNC
		CFLAG:奴隷:(2000+体位) += 1
	ENDIF
ELSE
	; ここで使われているLOCALSの用途が不明 <- 他说他不知道这里的字符串是干嘛用的 等会不是你写的这坨屎山吗？！——超级猫猫
	TRYCALLLIST
		;キャラ専用・体位専用
		FUNC NTR_%関数種別%_%LOCALS%{奴隷}_8_{体位}_{行為段階}(奴隷)
		;キャラ専用・体位汎用
		FUNC NTR_%関数種別%_%LOCALS%{奴隷}_8_U_{行為段階}(奴隷)
		;キャラ汎用・体位専用
		FUNC NTR_%関数種別%_%LOCALS%_8_{体位}_{行為段階}(奴隷)
		;キャラ汎用・体位汎用
		FUNC NTR_%関数種別%_%LOCALS%_8_U_{行為段階}(奴隷)
		;キャラ汎用・体位汎用がなければ正常位で代用
		FUNC NTR_%関数種別%_%LOCALS%_8_0_{行為段階}(奴隷)
	ENDFUNC
ENDIF
IF 主人同席 != 0
	; 同席のときは _KW, _K の順で検索
	TRYCALLLIST
		;キャラ専用・体位専用
		FUNC NTR_%関数種別%_KW{CFLAG:奴隷:口上パターン}_8_{体位}_{行為段階}(奴隷)
		FUNC NTR_%関数種別%_K{CFLAG:奴隷:口上パターン}_8_{体位}_{行為段階}(奴隷)
		;キャラ専用・体位汎用
		FUNC NTR_%関数種別%_KW{CFLAG:奴隷:口上パターン}_8_U_{行為段階}(奴隷)
		FUNC NTR_%関数種別%_K{CFLAG:奴隷:口上パターン}_8_U_{行為段階}(奴隷)
		;キャラ汎用・体位専用
		FUNC NTR_%関数種別%_KW_8_{体位}_{行為段階}(奴隷)
		FUNC NTR_%関数種別%_K_8_{体位}_{行為段階}(奴隷)
		;キャラ汎用・体位汎用
		FUNC NTR_%関数種別%_KW_8_U_{行為段階}(奴隷)
		FUNC NTR_%関数種別%_K_8_U_{行為段階}(奴隷)
		;キャラ汎用・体位汎用がなければ正常位で代用
		FUNC NTR_%関数種別%_KW_8_0_{行為段階}(奴隷)
		FUNC NTR_%関数種別%_K_8_0_{行為段階}(奴隷)
	ENDFUNC
ELSE
	; 同席でないときは _K の関数のみ検索
	TRYCALLLIST
		;キャラ専用・体位専用
		FUNC NTR_%関数種別%_K{CFLAG:奴隷:口上パターン}_8_{体位}_{行為段階}(奴隷)
		;キャラ専用・体位汎用
		FUNC NTR_%関数種別%_K{CFLAG:奴隷:口上パターン}_8_U_{行為段階}(奴隷)
		;キャラ汎用・体位専用
		FUNC NTR_%関数種別%_K_8_{体位}_{行為段階}(奴隷)
		;キャラ汎用・体位汎用
		FUNC NTR_%関数種別%_K_8_U_{行為段階}(奴隷)
		;キャラ汎用・体位汎用がなければ正常位で代用
		FUNC NTR_%関数種別%_K_8_0_{行為段階}(奴隷)
	ENDFUNC
ENDIF
RESETCOLOR
RETURN 0

;-------------------------------------------------------------------------------
;A性交時メッセージ・口上分岐
;主人同席での見せつけも考慮
;奴隷:対象キャラ
@NTR_A_SEX_MSG_KOJO(奴隷)
#DIM 奴隷
#DIM 開始継続
#DIM 主人同席
SIF FLAG:口上颜色
	CALL KOJO_COLOR(奴隷)
IF NTR_CHK_VISIBLE(FLAG:访问者当前位置)
	; 開始／継続フラグ
	開始継続 = ( FLAG:訪問者との行為の終了時刻 == 0 ? 0 # 1 )
	; 主人同席（見せつけ）フラグ
	主人同席 = ( CFLAG:MASTER:現在位置 == FLAG:访问者当前位置 ? 1 # 0 )
	CALL NTR_A_SEX_KOJO_EX(奴隷, 開始継続, 主人同席, "KOJO")
	CALL NTR_A_SEX_KOJO_EX(奴隷, 開始継続, 主人同席, "MESSAGE")
ENDIF
RESETCOLOR
RETURN 0

;-------------------------------------------------------------------------------
;A性交時メッセージ・口上分岐
;主人同席での見せつけも考慮
;奴隷:対象キャラ
@NTR_A_SEX_FINISH_MSG_KOJO(奴隷)
#DIM 奴隷
#DIM 主人同席
SIF FLAG:口上颜色
	CALL KOJO_COLOR(奴隷)
IF NTR_CHK_VISIBLE(FLAG:访问者当前位置)
	; 主人同席（見せつけ）フラグ
	主人同席 = ( CFLAG:MASTER:現在位置 == FLAG:访问者当前位置 ? 1 # 0 )
	CALL NTR_A_SEX_KOJO_EX(奴隷, 2, 主人同席, "KOJO_PRE")
	CALL NTR_A_SEX_KOJO_EX(奴隷, 2, 主人同席, "MESSAGE")
	CALL NTR_A_SEX_KOJO_EX(奴隷, 2, 主人同席, "KOJO")
ENDIF
RESETCOLOR
RETURN 0

;-------------------------------------------------------------------------------
;A性交時メッセージ分岐
;奴隷…対象キャラ
;行為段階…0=開始 1=継続 2=終了
;主人同席…0=二人きり 1=主人同席
;関数種別…口上または地の文の関数名の一部
@NTR_A_SEX_KOJO_EX(奴隷, 行為段階, 主人同席, 関数種別)
#DIM 奴隷
#DIM 行為段階
#DIM 主人同席
#DIMS 関数種別
#DIM 体位
SIF FLAG:口上テキスト設定 <= 0 && 関数種別 == "KOJO_PRE"
	RETURN 0
SIF FLAG:口上テキスト設定 <= 0 && 関数種別 == "KOJO"
	RETURN 0
SIF FLAG:情景テキスト設定 <= 0 && 関数種別 == "MESSAGE"
	RETURN 0
SIF 関数種別 != "MESSAGE" && 関数種別 != "KOJO" && 関数種別 != "KOJO_PRE"
	RETURN 0
体位 = FLAG:訪問者との行為 - NTR_行为_Ａ正常位
SIF FLAG:訪問者との行為 > NTR_行为_Ａ背面座位
	体位 = FLAG:訪問者との行為 - NTR_行为_Ａ背面座位
IF 主人同席 != 0
	CFLAG:奴隷:同席目撃回数 ++
	CFLAG:奴隷:同席目撃時行為 = MAX(CFLAG:奴隷:同席目撃時行為, FLAG:訪問者との行為)
ENDIF
SIF FLAG:口上颜色
	CALL KOJO_COLOR(奴隷)
IF 主人同席 != 0
	; 同席のときは _KW, _K の順で検索
	TRYCALLLIST
		;キャラ専用・体位専用
		FUNC NTR_%関数種別%_KW{CFLAG:奴隷:口上パターン}_9_{体位}_{行為段階}(奴隷)
		FUNC NTR_%関数種別%_K{CFLAG:奴隷:口上パターン}_9_{体位}_{行為段階}(奴隷)
		;キャラ専用・体位汎用
		FUNC NTR_%関数種別%_KW{CFLAG:奴隷:口上パターン}_9_U_{行為段階}(奴隷)
		FUNC NTR_%関数種別%_K{CFLAG:奴隷:口上パターン}_9_U_{行為段階}(奴隷)
		;キャラ汎用・体位専用
		FUNC NTR_%関数種別%_KW_9_{体位}_{行為段階}(奴隷)
		FUNC NTR_%関数種別%_K_9_{体位}_{行為段階}(奴隷)
		;キャラ汎用・体位汎用
		FUNC NTR_%関数種別%_KW_9_U_{行為段階}(奴隷)
		FUNC NTR_%関数種別%_K_9_U_{行為段階}(奴隷)
		;キャラ汎用・体位汎用がなければ正常位で代用
		FUNC NTR_%関数種別%_KW_9_0_{行為段階}(奴隷)
		FUNC NTR_%関数種別%_K_9_0_{行為段階}(奴隷)
	ENDFUNC
ELSE
	IF (NTR_KOJO_SEX_A_FIRSTTIME(奴隷,体位) || NTR_KOJO_SEX_A_FIRSTTIME(奴隷,1000)) && CFLAG:奴隷:(2050+体位) != 3 && (関数種別 == "KOJO" || 関数種別 == "KOJO_PRE")
		IF 関数種別 == "KOJO"
			TRYCALLLIST
				FUNC NTR_KOJO_SEX_A_FIRSTTIME_K{奴隷}_{体位}_{行為段階}(奴隷)
				FUNC NTR_KOJO_SEX_A_FIRSTTIME_KU_{体位}_{行為段階}(奴隷)
				FUNC NTR_KOJO_SEX_A_FIRSTTIME_K{奴隷}_U_{行為段階}(奴隷)
				FUNC NTR_KOJO_SEX_A_FIRSTTIME_KU_U_{行為段階}(奴隷)
			ENDFUNC
		ENDIF
		CFLAG:奴隷:(2050+体位) += 1
	ELSE
		; 同席でないときは _K の関数のみ検索
		TRYCALLLIST
			;キャラ専用・体位専用
			FUNC NTR_%関数種別%_K{CFLAG:奴隷:口上パターン}_9_{体位}_{行為段階}(奴隷)
			;キャラ専用・体位汎用
			FUNC NTR_%関数種別%_K{CFLAG:奴隷:口上パターン}_9_U_{行為段階}(奴隷)
			;キャラ汎用・体位専用
			FUNC NTR_%関数種別%_K_9_{体位}_{行為段階}(奴隷)
			;キャラ汎用・体位汎用
			FUNC NTR_%関数種別%_K_9_U_{行為段階}(奴隷)
			;キャラ汎用・体位汎用がなければ正常位で代用
			FUNC NTR_%関数種別%_K_9_0_{行為段階}(奴隷)
		ENDFUNC
	ENDIF
ENDIF
RESETCOLOR
RETURN 0

;调用来自@NTR_COUNTER_DO
;呼出CNT口上
@NTR_CNT_KOJO(对象, 行为编号)
#DIM 对象
#DIM 行为编号
TRYCALLLIST
	FUNC NTR_CNT_{对象}_{行为编号}(对象)
	FUNC NTR_CNT_U_{行为编号}(对象)
ENDFUNC