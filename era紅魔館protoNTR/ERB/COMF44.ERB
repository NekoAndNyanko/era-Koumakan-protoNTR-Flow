;-------------------------------------------------
;バイブ
;道具使用コマンド、レベル2
;-------------------------------------------------
@COM44

TRYCALL COM417_is_Protected(PLAYER,TARGET,娼婦紋_太股)
SIF RESULT
	RETURN 0

;着脱の処理
TEQUIP:バイブ = !TEQUIP:バイブ
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:体力 += 30
DOWNBASE:気力 += 100

SOURCE:快Ｖ = 250
SOURCE:苦痛 = 50
SOURCE:露出 = 20
SOURCE:逸脱 = 40
SOURCE:反感 = 40
CALL 締り具合変動, TARGET, 2, 0


;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:自己愛 < 0 || TALENT:抵抗
	SOURCE:鬱屈 += 40

;処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:処女 == 1
	SOURCE:苦痛 += 2000
	SOURCE:反感 += 3000
ENDIF

;再生処女だった場合は、苦痛のソースと反発のソースを追加する
IF TALENT:処女 == 2
	SOURCE:苦痛 += 500
	SOURCE:反感 += 1000
ENDIF

;Vへの苦痛は先に処理しておく
CALL PAIN_CHECK_V(TARGET)

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;膣内に破瓜の血の汚れを付加
SIF TALENT:処女
	STAIN:膣内 |= 汚れ_破瓜の血

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;挿入時はＶ経験+
EXP:Ｖ経験 ++

;異常経験
IF TALENT:処女 == 1 && !GETBIT(CFLAG:3,3)
	EXP:異常経験 ++
	SETBIT CFLAG:異常経験,3
ENDIF

;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 1
	EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 1
ENDIF

EXP:PLAYER:道具使用経験 ++

RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM44(ARG)
;バイブ挿入中
PRINTL ＜バイブ挿入中＞

DOWNBASE:ARG:体力 += 10
DOWNBASE:ARG:気力 += 50

SOURCE:ARG:快Ｖ += 125
SOURCE:ARG:露出 += 10
SOURCE:ARG:逸脱 += 20

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:ARG:自己愛 < 0 || TALENT:ARG:抵抗
	SOURCE:ARG:鬱屈 += 20

;Vへの苦痛は先に処理しておく
;EXP:Ｖ経験をみる（処女はありえない）
LOCAL = 25
LOCAL:1 = 20
IF EXP:ARG:Ｖ経験 < EXPLV:2
	TIMES LOCAL , 0.80
	TIMES LOCAL:1 , 1.20
ELSEIF EXP:ARG:Ｖ経験 < EXPLV:3
	TIMES LOCAL , 0.40
ELSEIF EXP:ARG:Ｖ経験 < EXPLV:4
	TIMES LOCAL , 0.10
ELSEIF EXP:ARG:Ｖ経験 < EXPLV:5
	TIMES LOCAL , 0.00
ELSEIF EXP:ARG:Ｖ経験 >= EXPLV:5
	TIMES LOCAL , 0.00
ENDIF

;PALAM:潤滑をみる
IF PALAM:ARG:潤滑 < PALAMLV:1
	TIMES LOCAL , 1.20
	TIMES LOCAL:1 , 1.50
ELSEIF PALAM:ARG:潤滑 < PALAMLV:2
	TIMES LOCAL , 0.70
	TIMES LOCAL:1 , 1.20
ELSEIF PALAM:ARG:潤滑 < PALAMLV:3
	TIMES LOCAL , 0.40
ELSEIF PALAM:ARG:潤滑 < PALAMLV:4
	TIMES LOCAL , 0.10
ELSEIF PALAM:ARG:潤滑 >= PALAMLV:4
	TIMES LOCAL , 0.10
ENDIF
SOURCE:ARG:苦痛 += LOCAL
SOURCE:ARG:反感 += LOCAL:1

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;自慰実行時は経験＋１
SIF EXP_UP(22,TARGET)
	EXP:ARG:Ｖ経験 += 1

RETURN 1

