@SHOW_SHOP
	TRYCALL COM461_PUNISHMENT_END
	TRYCALL 追加パッチVerUP

	;奴隷購入ページフラグ
	TFLAG:100 = 0
	DRAWLINE
	PRINTFORML %日付_月(暦法月)%{暦法日}日[{DAY}日目](%GET_DAY()%)
	PRINTFORML (所持金:${MONEY})
	PRINT 体力:
	BAR BASE:MASTER:0,MAXBASE:MASTER:0,16
	PRINTFORM ({BASE:MASTER:0,4}/{MAXBASE:MASTER:0,4}) 
	PRINT 気力:
	BAR BASE:MASTER:1,MAXBASE:MASTER:1,16
	PRINTFORM ({BASE:MASTER:1,4}/{MAXBASE:MASTER:1,4})
	PRINTL

	PRINTL [100] - 起床
	PRINTL [101] - 能力表示
	SIF STRLENS(SAVESTR:10) > 1
		PRINTL [102] - コレクション
	PRINTL [103] - 通信販売
	PRINTL [104] - 設定
	PRINTL [200] - SAVE
	PRINTL [300] - LOAD
	PRINTL [400] - OPTION
	PRINTL [500] - 一括ふたなり
	TRYCALL 妊娠パッチDEBUG88

@USERSHOP

	TRYCALL 妊娠パッチDEBUG88COM

	IF RESULT == 100
		BEGIN TRAIN
		RETURN 1
	ELSEIF RESULT == 101
		CALL SHOW_CHARADATA
	ELSEIF RESULT == 102 && STRLENS(SAVESTR:10) > 1
		CALL SHOW_COLLECTION
	ELSEIF RESULT == 103
		CALL ITEM_BUY
	ELSEIF RESULT == 104
		CALL SCHEDULE
	ELSEIF RESULT == 200
		SAVEGAME
	ELSEIF RESULT == 300
		LOADGAME
	ELSEIF RESULT == 400
		CALL OPTION
	ELSEIF RESULT == 500
		CALL SET_FUTANARI_ALL
	ENDIF


@SCHEDULE
	#DIM LOOP_CHR
	
	PRINTFORML [0] - 起床時間設定　　現在の設定：{CFLAG:MASTER:通常起床時間 / 60, 3}時
	SIF FLAG:陥落人数
		PRINTFORML [1] - 私室設定　　　　現在の設定：%GETPLACENAME(CFLAG:MASTER:開始位置)%
	PRINTFORML [2] - %NTR_NAME(0)%ステータス設定
	PRINTFORML [3] - 子供の呼び出し
	PRINTL [100] - 戻る
	INPUT
	IF RESULT == 100
		RETURN 0
	ELSEIF RESULT == 0
		LOCAL = CFLAG:MASTER:通常起床時間 / 60 + 1000
		$INPUT_LOOP
		PRINTFORML 今の起床時間は{LOCAL - 1000, 3}時です
		PRINTL 何時に起床しますか？
		PRINTBUTTON "[−]", (LOCAL == 1000 ? 1023 # LOCAL - 1)
		PRINTBUTTON "[＋]", (LOCAL == 1023 ? 1000 # LOCAL + 1)
		PRINTL 
		PRINTL [100] - 設定完了
		INPUT
		IF RESULT == 100
			CFLAG:MASTER:通常起床時間 = (LOCAL - 1000) * 60
		ELSEIF RESULT >= 0 && RESULT < 24
			CFLAG:MASTER:通常起床時間 = RESULT * 60
		ELSE
			SIF RESULT >= 1000 && RESULT < 1024
				LOCAL = RESULT
			CLEARLINE 5
			GOTO INPUT_LOOP
		ENDIF
		RESTART
	ELSEIF RESULT == 1 && FLAG:陥落人数
		PRINTL 寝起きする部屋を変えることが出来ます
		PRINTL [0] - 自分の部屋
		FOR LOOP_CHR,1,CHARANUM
			SIF TALENT:LOOP_CHR:恋慕
				PRINTFORML [{LOOP_CHR,3}] - %CALLNAME:LOOP_CHR%の部屋
		NEXT
		$INPUT_LOOP2
		INPUT
		
		IF RESULT < 0 || RESULT >= CHARANUM
			CLEARLINE 1
			GOTO INPUT_LOOP2
		ELSEIF RESULT == 0
			CFLAG:MASTER:開始位置 = 15
		ELSEIF TALENT:RESULT:恋慕
			CFLAG:MASTER:開始位置 = CFLAG:RESULT:311
		ELSE
			CLEARLINE 1
			GOTO INPUT_LOOP2
		ENDIF
		PRINTW 変更は翌日から反映されます
		RESTART
	ELSEIF RESULT == 2
		CALL MAN_SET
		RESTART
	ELSEIF RESULT == 3
		CALL 子供訪問_設定
		RESTART
	ENDIF

RETURN 1

@SET_FUTANARI_ALL
	#DIM LOOP_CHR
	#DIM IS_TARGET_CHILD
	#DIM FUTA

	$INPUT_LOOP
	PRINTL [100] - 戻る
	PRINTL [101] - 娘 ふたなり0%
	PRINTL [102] - 娘 ふたなり50%
	PRINTL [103] - 娘 ふたなり100%
	PRINTL [104] - 初期メンバー ふたなり0%
	PRINTL [105] - 初期メンバー ふたなり50%
	PRINTL [106] - 初期メンバー ふたなり100%

	INPUT
	IF RESULT == 100
		RETURN 1
	ELSEIF RESULT >= 101 && RESULT <= 106
		IF RESULT >= 101 && RESULT <= 103
			IS_TARGET_CHILD = 1
		ELSE
			IS_TARGET_CHILD = 0
		ENDIF
		FOR LOOP_CHR,0,CHARANUM
			SIF LOOP_CHR == MASTER
				CONTINUE
			IF CHILDREM_CHECK(LOOP_CHR)
				SIF IS_TARGET_CHILD == 0
					CONTINUE
			ELSE
				SIF IS_TARGET_CHILD != 0
					CONTINUE
			ENDIF
			SIF TALENT:LOOP_CHR:2 == 2
				CONTINUE
			IF RESULT == 101 || RESULT == 104
				FUTA = 0
			ELSEIF RESULT == 102 || RESULT == 105
				FUTA = RAND:2
			ELSE
				FUTA = 1
			ENDIF
			; 生やしたり消したりする処理はSHOP_ITEM.ERBからコピペ＆改変
			IF FUTA == 0 && TALENT:LOOP_CHR:2 == 3
				TALENT:LOOP_CHR:2 = 1
				MAXBASE:LOOP_CHR:2 = 0
				MAXBASE:LOOP_CHR:精力 = 0
			ELSEIF FUTA != 0 && TALENT:LOOP_CHR:2 == 1
				TALENT:LOOP_CHR:2 |= 2
				MAXBASE:LOOP_CHR:射精 = 10000
				IF MAXBASE:LOOP_CHR:精力 > 0
					MAXBASE:LOOP_CHR:精力 = LIMIT(MAXBASE:LOOP_CHR:精力,200,1000)
				ELSE
					MAXBASE:LOOP_CHR:精力 = 1000
				ENDIF
				IF MAXBASE:LOOP_CHR:勃起 > 0
					MAXBASE:LOOP_CHR:勃起 = LIMIT(MAXBASE:LOOP_CHR:勃起,500,1500)
				ELSE
					MAXBASE:LOOP_CHR:勃起 = 1500
				ENDIF
			ENDIF
		NEXT
	ENDIF

	GOTO INPUT_LOOP

