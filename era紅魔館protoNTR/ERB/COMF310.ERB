@COM310
;尻を撫でる

TRYCALL COM417_is_Protected(PLAYER,TARGET,娼婦紋_左尻)
SIF RESULT
	RETURN 0

LOCAL = RAND:110
LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
SIF LOCAL:1 > 109
	LOCAL:1 = 109
IF LOCAL <= 9
	;大成功
	TFLAG:コマンド成功度 = 成功度_大成功
ELSEIF LOCAL >= LOCAL:1
	;失敗
	TFLAG:コマンド成功度 = 成功度_失敗
ELSE
	;残りは成功
	TFLAG:コマンド成功度 = 成功度_成功
ENDIF

;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
TCVAR:TARGET:116 = PLAYER
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;先制発生時は失敗しない
SIF TFLAG:COMABLE管理 == 3
	TFLAG:コマンド成功度 = MAX(TFLAG:コマンド成功度,成功度_成功)
DOWNBASE:気力 += 50
DOWNBASE:MASTER:気力 += 50

;固定で獲得するソース
SOURCE:歓楽 = 100
SOURCE:情愛 = 100
SOURCE:露出 = 100
SOURCE:反感 = 200

;ABL:親密をみる
IF ABL:親密 <= 3
	SOURCE:歓楽 += 100 + (ABL:親密 * 40)
	SOURCE:情愛 += 200 + (ABL:親密 * 30)
ELSEIF ABL:親密 <= 5
	SOURCE:歓楽 += 400 + (ABL:親密 * 50)
	SOURCE:情愛 += 400 + (ABL:親密 * 30)
ELSEIF ABL:親密 <= 8
	SOURCE:歓楽 += 700 + (ABL:親密 * 60)
	SOURCE:情愛 += 600 + (ABL:親密 * 40)
ELSEIF ABL:親密 <= 11
	SOURCE:歓楽 += 1200 + (ABL:親密 * 80)
	SOURCE:情愛 += 800 + (ABL:親密 * 40)
ELSE
	SOURCE:歓楽 += 2000 + (ABL:親密 * 50)
	SOURCE:情愛 += 1800 + (ABL:親密 * 20)
ENDIF

;好感度をみる
IF CFLAG:好感度 <= 500
	SOURCE:歓楽 += CFLAG:2
ELSEIF CFLAG:好感度 <= 5000
	SOURCE:歓楽 += 500 + (CFLAG:好感度 - 500) / 3
ELSE
	SOURCE:歓楽 += 2000 + (CFLAG:好感度 - 5000) / 5
ENDIF
SIF SOURCE:歓楽 < 0
	SOURCE:歓楽 = 0

LOCAL = 0
;親密が低いと好感度低下
IF ABL:親密 == 0
	LOCAL -= 3
ELSEIF ABL:親密 == 1
	LOCAL -= 2
ELSEIF ABL:親密 == 2
	LOCAL -= 1
ENDIF
;好感度が低いと好感度低下
IF CFLAG:好感度 <= 50
	LOCAL -= 3
ELSEIF CFLAG:好感度 <= 100
	LOCAL -= 2
ELSEIF CFLAG:好感度 <= 250
	LOCAL -= 1
ENDIF

SOURCE:欲情 = 100 + 200 * ABL:欲望
SOURCE:受動 = 100 + 200 * ABL:従順

;好感度変化
TFLAG:97 += LOCAL

IF TFLAG:コマンド成功度 == 成功度_失敗
	TIMES SOURCE:歓楽 , 0.10
	TIMES SOURCE:情愛 , 0.50
	TIMES SOURCE:受動 , 0.50
	TIMES SOURCE:欲情 , 0.50
	TIMES SOURCE:反感 , 1.50
ELSEIF TFLAG:コマンド成功度 == 成功度_成功
	TIMES SOURCE:歓楽 , 1.00
	TIMES SOURCE:情愛 , 1.00
	TIMES SOURCE:受動 , 1.00
	TIMES SOURCE:欲情 , 1.00
ELSE
	TIMES SOURCE:歓楽 , 2.00
	TIMES SOURCE:情愛 , 2.00
	TIMES SOURCE:受動 , 2.00
	TIMES SOURCE:欲情 , 2.00
	TIMES SOURCE:反感 , 0.75
ENDIF
TIME += 10

RETURN 1

