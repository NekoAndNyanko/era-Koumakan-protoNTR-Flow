;値ごとに頻度（確率）を設定できるランダム選択処理
;
;使用例
;   #DIM 値,5       ; 選択したい値の配列（任意の値）
;   #DIM 頻度,5     ; 値毎の頻度の配列（0以上の値であること）
;   #DIM 選択       ; 選択結果を得る変数。
;   #DIM 選択値     ; 選択された値
;
;   値:0 = 1
;   値:1 = 2
;   値:2 = 3
;   値:3 = 4
;   値:4 = 5
;   頻度:0 = 1
;   頻度:1 = 1
;   頻度:2 = 3
;   頻度:3 = 1
;   頻度:4 = 1
;   選択 = SEL_ARRAY(頻度)
;   選択値 = 値:選択
;   
@SEL_ARRAY(頻度)
    #FUNCTION
    #DIM REF 頻度, 0
    #DIM 件数     ; 配列「頻度」の件数
    #DIM SEL      ; 選択された結果
    #DIM LOOP_I   ; ループ変数
    #DIM WK       ; RAND実行結果ワーク
    #DIM V_MIN    ; 比較用最小値
    #DIM V_MAX    ; 比較用最大値
    
    件数 = VARSIZE("頻度")      ; 頻度の配列サイズを求める。
    WK = RAND( SUMARRAY(頻度) ) ; 頻度の合計値の範囲内でランダムな選択を行う。
    V_MIN = 0                   ; 比較用最小値・最大値初期化。
    V_MAX = 0                   ;
    SEL = 0                     ;選択番号を初期化。
    ; LOOP_IがXのとき、最小値・最大値は以下の値とする。
    ; V_MIN = 頻度:0 + ... + 頻度:(X-1)
    ; V_MAX = 頻度:0 + ... + 頻度:X
    ; V_MIN <= WK && WK < V_MAXとなる SEL を求める。
    FOR LOOP_I,0,件数
        IF (0 < 頻度:LOOP_I)
            V_MAX = V_MIN + 頻度:LOOP_I ; 最大値更新 
        ENDIF
        IF (V_MIN <= WK && WK < V_MAX)
            SEL = LOOP_I
            BREAK
        ENDIF
        V_MIN = V_MAX   　　　　　　　  ; 最小値更新
    NEXT
    ;選択されたものを返す。
RETURNF SEL
