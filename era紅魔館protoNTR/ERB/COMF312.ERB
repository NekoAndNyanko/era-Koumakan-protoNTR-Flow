@COM312
;キスする

TRYCALL COM417_is_Protected(PLAYER,TARGET,娼婦紋_右頬)
IF RESULT
	RETURN 0
ENDIF

;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:PLAYER:116 = TARGET
TCVAR:TARGET:116 = PLAYER

LOCAL = RAND:110
LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
SIF LOCAL:1 > 109
	LOCAL:1 = 109
IF LOCAL <= 9
	;10％で大成功
	TFLAG:コマンド成功度 = 成功度_大成功
ELSEIF LOCAL >= LOCAL:1
	;10〜1％で失敗
	TFLAG:コマンド成功度 = 成功度_失敗
ELSE
	;残りは成功
	TFLAG:コマンド成功度 = 成功度_成功
ENDIF
;先制発生時は失敗しない
SIF TFLAG:COMABLE管理 == 3
	TFLAG:コマンド成功度 = MAX(TFLAG:コマンド成功度,成功度_成功)

;恋慕つき住人の口が主人以外の精液で汚れていると失敗(NTR)
;IF CFLAG:射精者 != 0 && STAIN:口 >= 1 && TALENT:恋慕
IF CHK_COM312_STAIN(TARGET, MASTER)
	TFLAG:コマンド成功度 = 成功度_失敗NTR
	RETURN 1
ENDIF

DOWNBASE:気力 += 80
DOWNBASE:MASTER:気力 += 50

;固定で獲得するソース
SOURCE:歓楽 = 180
SOURCE:情愛 = 200
SOURCE:露出 = 80
SOURCE:反感 = 300

;ABL:親密をみる
IF ABL:親密 <= 1
	SOURCE:情愛 += (ABL:親密 * 30)
ELSEIF ABL:親密 <= 3
	SOURCE:歓楽 += 120 + (ABL:親密 * 40)
	SOURCE:情愛 += 150 + (ABL:親密 * 30)
	SOURCE:欲情 += 65 + (ABL:親密 * 20)
ELSEIF ABL:親密 <= 5
	SOURCE:歓楽 += 250 + (ABL:親密 * 50)
	SOURCE:情愛 += 280 + (ABL:親密 * 30)
	SOURCE:欲情 += 130 + (ABL:親密 * 26)
	SOURCE:露出 += 80 + (ABL:親密 * 15)
ELSEIF ABL:親密 <= 8
	SOURCE:歓楽 += 330 + (ABL:親密 * 60)
	SOURCE:情愛 += 380 + (ABL:親密 * 40)
	SOURCE:欲情 += 170 + (ABL:親密 * 30)
	SOURCE:露出 += 110 + (ABL:親密 * 18)
ELSEIF ABL:親密 <= 11
	SOURCE:歓楽 += 420 + (ABL:親密 * 80)
	SOURCE:情愛 += 480 + (ABL:親密 * 40)
	SOURCE:欲情 += 230 + (ABL:親密 * 35)
	SOURCE:露出 += 160 + (ABL:親密 * 20)
ELSE
	SOURCE:歓楽 += 680 + (ABL:親密 * 55)
	SOURCE:情愛 += 760 + (ABL:親密 * 23)
	SOURCE:欲情 += 350 + (ABL:親密 * 27)
	SOURCE:露出 += 260 + (ABL:親密 * 17)
ENDIF

;好感度をみる
IF CFLAG:好感度 <= 500
	SOURCE:情愛 += CFLAG:2
ELSEIF CFLAG:好感度 <= 5000
	SOURCE:情愛 += 400 + (CFLAG:好感度 - 500) / 4
ELSE
	SOURCE:情愛 += 1200 + (CFLAG:好感度 - 5000) / 6
ENDIF
SIF SOURCE:情愛 < 0
	SOURCE:情愛 = 0

LOCAL = 0
;好感度が低いと好感度低下
IF CFLAG:好感度 <= 50
	LOCAL -= 3
ELSEIF CFLAG:好感度 <= 100
	LOCAL -= 2
ELSEIF CFLAG:好感度 <= 250
	LOCAL -= 1
ENDIF


SOURCE:受動 = 150 + 300 * ABL:従順
SOURCE:征服 = 150 + 300 * ABL:サドっ気

;好感度変化
TFLAG:97 += LOCAL

IF TFLAG:コマンド成功度 == 成功度_失敗 || TFLAG:コマンド成功度 == 成功度_失敗NTR
	TIMES SOURCE:歓楽 , 0.10
	TIMES SOURCE:情愛 , 0.50
	TIMES SOURCE:征服 , 0.50
	TIMES SOURCE:受動 , 0.50
	TIMES SOURCE:露出 , 0.40
ELSEIF TFLAG:コマンド成功度 == 成功度_成功
	TIMES SOURCE:歓楽 , 1.00
	TIMES SOURCE:情愛 , 1.00
	TIMES SOURCE:征服 , 1.00
	TIMES SOURCE:受動 , 1.00
	TIMES SOURCE:露出 , 0.90
ELSE
	TIMES SOURCE:歓楽 , 2.00
	TIMES SOURCE:情愛 , 2.00
	TIMES SOURCE:征服 , 2.00
	TIMES SOURCE:受動 , 2.00
	TIMES SOURCE:露出 , 1.80
ENDIF
TIME += 10

EXP:キス経験 += 1
EXP:MASTER:キス経験 += 1

;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 2
	EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 2
ENDIF

RETURN 1

;恋慕つき住人の口が主人以外の精液で汚れていると失敗
;　要はあなた以外の相手との行為の発覚を恐れての隠蔽ということで
;　戻り値 1:失敗 0:成功
@CHK_COM312_STAIN(奴隷, 行為者)
	#FUNCTION
	#DIM 奴隷
	#DIM 行為者
	IF CFLAG:奴隷:射精者M != 行為者 && (STAIN:奴隷:口 & (汚れ_ペニス|汚れ_精液)) && TALENT:奴隷:恋慕
		RETURNF 1
	ELSE
		RETURNF 0
	ENDIF
