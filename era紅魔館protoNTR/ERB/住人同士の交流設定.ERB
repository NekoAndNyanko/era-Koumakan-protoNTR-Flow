;--------------------------------------------------------------------
;受け攻めの決定にサドっ気, マゾっ気, 地位で計算されます
;計算後の値が同じだった場合は立場で強制的に決定するので立場には同じ数値を使わないでください
;数値が大きい方が攻め側になりやすくなります

@INHABITANT_SETTINGS_FIRST
	LOCAL:1 = 0
	LOCAL:2 = INHABITANT_LOOP_MAX()
	SIF !GETBIT(FLAG:住人同士イベント設定,0)
		RETURN 0
	FOR LOCAL:0,1,LOCAL:2
		LOCAL:1 += CFLAG:(LOCAL:0):住人同士イベント地位
		LOCAL:1 += CFLAG:(LOCAL:0):住人同士イベント立場
	NEXT
	IF LOCAL:1 == 0
		CFLAG:人物_美鈴:住人同士イベント地位 = 1
		CFLAG:人物_小悪魔:住人同士イベント地位 = 1
		CFLAG:人物_パチュリー:住人同士イベント地位 = 3
		CFLAG:人物_咲夜:住人同士イベント地位 = 2
		CFLAG:人物_レミリア:住人同士イベント地位 = 4
		CFLAG:人物_フラン:住人同士イベント地位 = 3
		CFLAG:人物_子悪魔:住人同士イベント地位 = 1
		CFLAG:人物_チルノ:住人同士イベント地位 = 0
		CFLAG:人物_大妖精:住人同士イベント地位 = 0
		CFLAG:人物_魔理沙:住人同士イベント地位 = 1
		CFLAG:人物_霊夢:住人同士イベント地位 = 3
		CFLAG:人物_ルーミア:住人同士イベント地位 = 0
		CFLAG:人物_アリス:住人同士イベント地位 = 3
		
		CFLAG:人物_美鈴:住人同士イベント立場 = 5
		CFLAG:人物_小悪魔:住人同士イベント立場 = 4
		CFLAG:人物_パチュリー:住人同士イベント立場 = 9
		CFLAG:人物_咲夜:住人同士イベント立場 = 7
		CFLAG:人物_レミリア:住人同士イベント立場 = 12
		CFLAG:人物_フラン:住人同士イベント立場 = 10
		CFLAG:人物_子悪魔:住人同士イベント立場 = 3
		CFLAG:人物_チルノ:住人同士イベント立場 = 2
		CFLAG:人物_大妖精:住人同士イベント立場 = 0
		CFLAG:人物_魔理沙:住人同士イベント立場 = 6
		CFLAG:人物_霊夢:住人同士イベント立場 = 11
		CFLAG:人物_ルーミア:住人同士イベント立場 = 1
		CFLAG:人物_アリス:住人同士イベント立場 = 8
		
		CFLAG:人物_美鈴:体を許す好感度 = 500
		CFLAG:人物_小悪魔:体を許す好感度 = 500
		CFLAG:人物_パチュリー:体を許す好感度 = 500
		CFLAG:人物_咲夜:体を許す好感度 = 500
		CFLAG:人物_レミリア:体を許す好感度 = 500
		CFLAG:人物_フラン:体を許す好感度 = 500
		CFLAG:人物_子悪魔:体を許す好感度 = 500
		CFLAG:人物_チルノ:体を許す好感度 = 500
		CFLAG:人物_大妖精:体を許す好感度 = 500
		CFLAG:人物_魔理沙:体を許す好感度 = 500
		CFLAG:人物_霊夢:体を許す好感度 = 500
		CFLAG:人物_ルーミア:体を許す好感度 = 500
		CFLAG:人物_アリス:体を許す好感度 = 500
				
		CFLAG:人物_美鈴:行為の上限 = 2000
		CFLAG:人物_小悪魔:行為の上限 = 2000
		CFLAG:人物_パチュリー:行為の上限 = 2000
		CFLAG:人物_咲夜:行為の上限 = 2000
		CFLAG:人物_レミリア:行為の上限 = 2000
		CFLAG:人物_フラン:行為の上限 = 2000
		CFLAG:人物_子悪魔:行為の上限 = 2000
		CFLAG:人物_チルノ:行為の上限 = 2000
		CFLAG:人物_大妖精:行為の上限 = 2000
		CFLAG:人物_魔理沙:行為の上限 = 2000
		CFLAG:人物_霊夢:行為の上限 = 2000
		CFLAG:人物_ルーミア:行為の上限 = 2000
		CFLAG:人物_アリス:行為の上限 = 2000
	ENDIF
	FOR LOCAL:0,1,LOCAL:2
		SIF CFLAG:(LOCAL:0):体を許す好感度 == 0 
			CFLAG:(LOCAL:0):体を許す好感度 = 1000
		SIF CFLAG:(LOCAL:0):行為の上限 == 0
			CFLAG:(LOCAL:0):行為の上限 = 2000
	NEXT
RETURN 0

@INHABITANT_SETTINGS
	CALL INHABITANT_SETTINGS_FIRST
	$INPUT_INHABITANT_SETTINGS_START
	LOCAL:2 = 0
	IF GETBIT(FLAG:住人同士イベント設定,0)
		LOCAL:2 = INHABITANT_LOOP_MAX()
	ENDIF
	DRAWLINE
	PRINTL 受け攻めの決定に各ステータスと地位で計算されます
	PRINTL 計算後の値が同じだった場合は立場で強制的に決定するので立場には同じ数値を使わないでください
	PRINTL 数値が大きい方が攻め側になりやすくなります
	DRAWLINE
	PRINTFORML [0] 住人同士イベント：\@ GETBIT(FLAG:住人同士イベント設定,0) ?ON#OFF\@
	IF GETBIT(FLAG:住人同士イベント設定,0)
		PRINTFORM 対象者：初期メンバー
		SIF GETBIT(FLAG:住人同士イベント設定,1)
			PRINTFORM 、娘\@ GETBIT(FLAG:住人同士イベント設定,3) ?、息子#\@
		PRINTFORML
	ENDIF
	DRAWLINE
	FOR LOCAL:0,1,LOCAL:2
		IF GETBIT(TALENT:(LOCAL:0):性別,0) || GETBIT(FLAG:住人同士イベント設定,3)
			PRINTFORM [{LOCAL:0,2}]
			PRINTFORM %CALLNAME:(LOCAL:0),16,LEFT%
			PRINTFORM 地位：{CFLAG:(LOCAL:0):住人同士イベント地位,2}　
			PRINTFORM 立場：{CFLAG:(LOCAL:0):住人同士イベント立場,2}　
			PRINTFORM 体を許す好感度：{CFLAG:(LOCAL:0):体を許す好感度,4}　
			PRINTFORML 行為の上限：{CFLAG:(LOCAL:0):行為の上限,4}
		ENDIF
	NEXT
	DRAWLINE
	SIF GETBIT(FLAG:住人同士イベント設定,0)
		PRINT [999] 好感度表示　
	PRINTL [1000] 戻る
	$INPUT_INHABITANT_SETTINGS_0
	INPUT
	LOCAL:1 = RESULT
	IF RESULT == 0
		$INPUT_INHABITANT_SETTINGS_1_1
		DRAWLINE
		PRINTFORML %"[0] - 住人同士イベント",28, LEFT%：\@ GETBIT(FLAG:住人同士イベント設定,0) ?ON#OFF\@
		IF GETBIT(FLAG:住人同士イベント設定,0)
			PRINTFORML %"[1] - 子供もイベント対象",28, LEFT%：\@ GETBIT(FLAG:住人同士イベント設定,1) ?ON#OFF\@
			IF GETBIT(FLAG:住人同士イベント設定,1)
			PRINTFORML %"[2] - 子供の性別対象",28, LEFT%：\@ GETBIT(FLAG:住人同士イベント設定,3) ?娘と息子#娘のみ\@
			ELSE
				PRINTFORML
			ENDIF
			IF GETBIT(FLAG:住人同士イベント設定,1)
				PRINTFORML %"[3] - 子供による屈服度増加",28, LEFT%：\@ GETBIT(FLAG:住人同士イベント設定,4) ?ON#OFF\@
			ELSE
				PRINTFORML
			ENDIF
				PRINTFORML %"[4] - 筒抜けモード",28, LEFT%：\@ GETBIT(FLAG:住人同士イベント設定,2) ?ON#OFF\@
		ELSE
			PRINTFORML
			PRINTFORML
			PRINTFORML
		ENDIF
		DRAWLINE
		PRINTL [1000] 戻る
		$INPUT_INHABITANT_SETTINGS_1_2
		INPUT
		IF RESULT == 0
			PRINTL 住人同士イベントのON/OFFを切り替えます
			PRINTL [0] ON　[1] OFF
			INPUT
			IF RESULT == 0
				SETBIT FLAG:住人同士イベント設定,0
				CALL INHABITANT_SETTINGS_FIRST
			ELSE
				CLEARBIT FLAG:住人同士イベント設定,0
			ENDIF
			GOTO INPUT_INHABITANT_SETTINGS_1_1
		ELSEIF RESULT == 1
			PRINTL 住人同士イベントで子供も対象にしますか？
			PRINTL [0] ON　[1] OFF
			INPUT
			IF RESULT == 0
				SETBIT FLAG:住人同士イベント設定,1
				CALL INHABITANT_SETTINGS_FIRST
			ELSE
				CLEARBIT FLAG:住人同士イベント設定,1
			ENDIF
			GOTO INPUT_INHABITANT_SETTINGS_1_1
		ELSEIF RESULT == 2
			PRINTL 住人同士イベントで息子も対象に含めますか？
			PRINTL [0] ON　[1] OFF
			INPUT
			IF RESULT == 0
				SETBIT FLAG:住人同士イベント設定,3
				CALL INHABITANT_SETTINGS_FIRST
			ELSE
				CLEARBIT FLAG:住人同士イベント設定,3
			ENDIF
			GOTO INPUT_INHABITANT_SETTINGS_1_1
		ELSEIF RESULT == 3
			PRINTFORML 素質[訪問者の血統]を持った子供と交流した場合、好感度の増加に伴い屈服度も増加するようになる
			PRINTL [0] ON　[1] OFF
			INPUT
			IF RESULT == 0
				SETBIT FLAG:住人同士イベント設定,4
				PRINTFORMW 屈服度増加処理を行う様になりました
			ELSE
				CLEARBIT FLAG:住人同士イベント設定,4
				PRINTFORMW 屈服度増加処理を行わない様になりました
			ENDIF
			GOTO INPUT_INHABITANT_SETTINGS_1_1
		ELSEIF RESULT == 4
			PRINTFORML 住人同士の行動を筒抜けにしますか？
			PRINTL [0] ON　[1] OFF
			INPUT
			IF RESULT == 0
				SETBIT FLAG:住人同士イベント設定,2
				PRINTFORMW 住人の行動が筒抜けになりました
			ELSE
				CLEARBIT FLAG:住人同士イベント設定,2
				PRINTFORMW 住人の行動が見えないようになりました
			ENDIF
			GOTO INPUT_INHABITANT_SETTINGS_1_1
		ELSEIF RESULT == 1000
			GOTO INPUT_INHABITANT_SETTINGS_START
		ENDIF
		GOTO INPUT_INHABITANT_SETTINGS_1_2
	ELSEIF RESULT > 0 && RESULT <= LOCAL:2
		$INPUT_INHABITANT_SETTINGS_3
		DRAWLINE
		PRINTFORML %CALLNAME:(LOCAL:1)%
		PRINTFORML [0] 地位：{CFLAG:(LOCAL:1):住人同士イベント地位}
		PRINTFORML [1] 立場：{CFLAG:(LOCAL:1):住人同士イベント立場}
		PRINTFORML [2] 体を許す好感度：{CFLAG:(LOCAL:1):体を許す好感度}
		PRINTFORML [3] 行為の上限：{CFLAG:(LOCAL:1):行為の上限}
		DRAWLINE
		PRINTFORML [1000] 戻る
		$INPUT_INHABITANT_SETTINGS_2
		INPUT
		IF RESULT == 0
			PRINTFORML %CALLNAME:(LOCAL:1)%の地位の値を入力してください
			INPUT
			CFLAG:(LOCAL:1):住人同士イベント地位 = RESULT
			GOTO INPUT_INHABITANT_SETTINGS_3
		ELSEIF RESULT == 1
			PRINTFORML %CALLNAME:(LOCAL:1)%の立場の値を入力してください
			INPUT
			CFLAG:(LOCAL:1):住人同士イベント立場 = RESULT
			GOTO INPUT_INHABITANT_SETTINGS_3
		ELSEIF RESULT == 2
			PRINTFORML %CALLNAME:(LOCAL:1)%の体を許す好感度の値を入力してください
			PRINTFORML 好感度がこの値に満たない状態で性交されると強姦扱いになります
			INPUT
			CFLAG:(LOCAL:1):体を許す好感度 = RESULT
			GOTO INPUT_INHABITANT_SETTINGS_3
		ELSEIF RESULT == 3
			PRINTFORML %CALLNAME:(LOCAL:1)%の行為の上限を選択してください
			PRINTL [0] 会話・お茶　[1] スキンシップ　[2] 尻を撫でる　[3] 抱きつく　[4] キス　[5] 胸愛撫
			PRINTL [6] アナル愛撫　[7] クリ愛撫　　　[8] 指挿れ　　　[9] 性交　　 [10] 性交多め
			PRINTL [11] 直接入力する
			$INPUT_INHABITANT_SETTINGS_4
			INPUT
			IF RESULT == 0
				CFLAG:(LOCAL:1):行為の上限 = 49
			ELSEIF RESULT == 1
				CFLAG:(LOCAL:1):行為の上限 = 199
			ELSEIF RESULT == 2
				CFLAG:(LOCAL:1):行為の上限 = 299
			ELSEIF RESULT == 3
				CFLAG:(LOCAL:1):行為の上限 = 499
			ELSEIF RESULT == 4
				CFLAG:(LOCAL:1):行為の上限 = 599
			ELSEIF RESULT == 5
				CFLAG:(LOCAL:1):行為の上限 = 699
			ELSEIF RESULT == 6
				CFLAG:(LOCAL:1):行為の上限 = 799
			ELSEIF RESULT == 7
				CFLAG:(LOCAL:1):行為の上限 = 899
			ELSEIF RESULT == 8
				CFLAG:(LOCAL:1):行為の上限 = 999
			ELSEIF RESULT == 9
				CFLAG:(LOCAL:1):行為の上限 = 1099
			ELSEIF RESULT == 10
				CFLAG:(LOCAL:1):行為の上限 = 2000
			ELSEIF RESULT == 11
				PRINTFORML %CALLNAME:(LOCAL:1)%の行為の上限の値を入力してください
				PRINTFORML 参考：500で自らキスを、600から徐々に愛撫を、1000から自ら性交をするようになります
				PRINTL 数値を高くすればするほど性交確率が上がります(好感度が同様に高くないといけませんが)
				INPUT
				CFLAG:(LOCAL:1):行為の上限 = RESULT
			ELSE
				GOTO INPUT_INHABITANT_SETTINGS_4
			ENDIF
			GOTO INPUT_INHABITANT_SETTINGS_3
		ELSEIF RESULT == 1000
			GOTO INPUT_INHABITANT_SETTINGS_START
		ELSE
			GOTO INPUT_INHABITANT_SETTINGS_2
		ENDIF
	ELSEIF RESULT == 999
		CALL INHABITANT_DISPLAY
		GOTO INPUT_INHABITANT_SETTINGS_START
	ELSEIF RESULT == 1000
		RETURN 0
	ELSE
		GOTO INPUT_INHABITANT_SETTINGS_0
	ENDIF
RETURN 0

@INHABITANT_DISPLAY
	LOCAL:1 = 0
	LOCAL:2 = 0
	IF GETBIT(FLAG:住人同士イベント設定,0)
		LOCAL:2 = INHABITANT_LOOP_MAX()
	ENDIF
	$INPUT_INHABITANT_DISPLAY
	DRAWLINE
	PRINTFORML %CALLNAME:(LOCAL:1)%の住人同士好感度
	PRINTFORM 体を許す好感度：{CFLAG:(LOCAL:1):体を許す好感度,4}　
	PRINTFORML 行為の上限：{CFLAG:(LOCAL:1):行為の上限,4}
	DRAWLINE
	FOR LOCAL:0,0,LOCAL:2
		IF GETBIT(TALENT:(LOCAL:0):性別,0) || GETBIT(FLAG:住人同士イベント設定,3)
			SIF LOCAL:0 == LOCAL:1
				SETCOLOR 色設定_兄弟_自分
			PRINTFORM [{LOCAL:0,2}]
			PRINTFORM %CALLNAME:(LOCAL:0),16,LEFT%
			PRINTFORM 対象へ：{TA:(LOCAL:1):(LOCAL:0):TA_好感度,5}　
			PRINTFORM 対象から：{TA:(LOCAL:0):(LOCAL:1):TA_好感度,5}　
			PRINTFORML %TUDUKIGARA_OUTPUT_ALL(LOCAL:1 ,LOCAL:0)%
			RESETCOLOR
		ENDIF
	NEXT
	DRAWLINE
	PRINTFORML [1000] 戻る
	$INPUT_INHABITANT_DISPLAY_RETRY
	INPUT
	IF 0 <= RESULT && RESULT <= LOCAL:2
		LOCAL:1 = RESULT
		GOTO INPUT_INHABITANT_DISPLAY
	ELSEIF RESULT == 1000
		RETURN 0
	ELSE
		GOTO INPUT_INHABITANT_DISPLAY_RETRY
	ENDIF
RETURN 0

@INHABITANT_LOOP_MAX
	#FUNCTION
	IF GETBIT(FLAG:住人同士イベント設定,1)
		RETURNF CHARANUM
	ELSE
		RETURNF 人物_アリス+1
	ENDIF
