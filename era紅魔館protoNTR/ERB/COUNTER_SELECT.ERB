;-------------------------------------------------------------------------------
;FIXME:関数の説明を書いてください
;@return 1:xxx,0:xxx
@POSITION_OCCUPATION(ARG)
	#FUNCTION
	FOR LOCAL,1,CHARANUM
		SIF TARGET:LOCAL <= 0
			CONTINUE
		SIF TCVAR:(TARGET:LOCAL):位置関係 == ARG
			RETURNF 1
	NEXT

;-------------------------------------------------------------------------------
;TARGETによるカウンター行動を実行する
;@return 1:xxx,0:xxx
@EVENT_COUNTER(加害者,行動順)
	#DIM 加害者
	#DIM 行動順
	#DIM セクハラ内容_ソフト
	#DIM セクハラ内容_着衣
	#DIM セクハラ内容_脱衣愛撫
	#DIM セクハラ内容_脱衣強要
	#DIM セクハラ内容_セックス
	#DIM セクハラ成分_恥じらい
	#DIM セクハラ成分_誘惑
	#DIM セクハラ成分_積極性
	#DIM セクハラ成分_タチ
	#DIM DYNAMIC LOOP_CNT
	#DIM DYNAMIC LOOP_CHR
	;----------------------------------------------------
	;セクハラしてこない場合を先にチェック
	;直前のコマンドが連れ出す(351)か移動(400)か外出(490)なら発生しない
	SELECTCASE SELECTCOM
		CASE 351,400,490
			RETURN 0
	ENDSELECT
	;あなたはセクハラしない
	SIF 加害者 <= 0
		RETURN 0
	;寝てるキャラはセクハラしない
	SIF CFLAG:加害者:睡眠
		RETURN 0
	;同じ場所に訪問者がいる場合
	IF CFLAG:加害者:現在位置 == FLAG:訪問者の現在位置
		;NTRだとカウンター抑制
		SIF TALENT:加害者:NTR
			RETURN 0
		;主人が空気だと抑制
		SIF IS_AIR_MASTER(加害者)
			RETURN 0
		;訪問者の方が影響が大きいとある程度抑制
		SIF CFLAG:加害者:屈服度 > CFLAG:加害者:好感度 && RAND:2
			RETURN 0
	ENDIF
	;２重実行はさせない
	TCVAR:加害者:カウンター行動決定フラグ ++
	SIF TCVAR:加害者:カウンター行動決定フラグ > 1
		RETURN 0
	;----------------------------------------------------
	;特殊な処理
	VARSET LOCAL
	IF CFLAG:加害者:うふふ == 1
		;うふふ中のリアクション(うふふ=1、うふふ)
		CALL EVENT_COUNTER_REACTION(加害者,行動順)
		RETURN 0
	ELSEIF CFLAG:加害者:うふふ == 2
		;押し倒し中のリアクション(うふふ=2、押し倒され)
		;押し倒し中に満足した場合のリアクション
		IF 行動順 == 1 && CFLAG:加害者:MASTERの弱味 && BASE:加害者:満足 == MAXBASE:加害者:満足
			CALL EVENT_COUNTER_PUNISHMENT(加害者,行動順)
			;全員をリセットする必要はあるのか……？
			FOR LOOP_CHR,0,CHARANUM
				CFLAG:LOOP_CHR:うふふ = 0
				CALL CLOTHES_RESET(LOOP_CHR)
				CALL CLOTHES_SETTING_TRAIN(LOOP_CHR)
			NEXT
			RETURN 0
		ENDIF
	ENDIF
	;----------------------------------------------------
	;セクハラ内容の選択
	セクハラ内容_ソフト = 0
	セクハラ内容_着衣 = 0
	セクハラ内容_脱衣愛撫 = 0
	セクハラ内容_脱衣強要 = 0
	セクハラ内容_セックス = 0
	SELECTCASE TFLAG:COMABLE管理
		CASE 1
			;日常
			IF TCVAR:馴れ合い強度
				セクハラ内容_ソフト = 100
			ENDIF
		CASE 2
			;うふふ中
		CASE 3
			;押し倒し中
			;脱衣により着衣セクハラ率が決定
			SELECTCASE TEQUIP:PLAYER:上半身着衣状況
				CASE IS < 1
					セクハラ内容_着衣 = 80
				CASE IS < 2
					セクハラ内容_着衣 = 90
				CASEELSE
					セクハラ内容_着衣 = 100
			ENDSELECT
			セクハラ内容_脱衣愛撫 = 80
			;ムードが良ければ脱衣愛撫率が上がる
			SELECTCASE BASE:加害者:ムード
				CASE IS > 1000
					セクハラ内容_脱衣愛撫 += 15
				CASE IS > 500
					セクハラ内容_脱衣愛撫 += 10
			ENDSELECT
			;理性が低ければ脱衣愛撫率が上がる
			SELECTCASE BASE:加害者:理性
				CASE IS <= 0
					セクハラ内容_脱衣愛撫 += 15
				CASE IS <= 500
					セクハラ内容_脱衣愛撫 += 10
			ENDSELECT
		CASEELSE
	ENDSELECT
	セクハラ内容_脱衣強要 = セクハラ内容_脱衣愛撫
	セクハラ内容_セックス = セクハラ内容_脱衣愛撫
	;恥じらい成分
	セクハラ成分_恥じらい = TALENT:加害者:目立ちたがり + TALENT:加害者:自己愛 - TALENT:加害者:自制心 - TALENT:加害者:貞操 - 2 * TALENT:加害者:羞恥心
	;誘惑成分
	セクハラ成分_誘惑 = TALENT:加害者:魅力 + TALENT:加害者:魅惑 + TALENT:加害者:謎の魅力 + TALENT:加害者:バストサイズ + TALENT:加害者:体型 - TALENT:加害者:貞操 + TALENT:加害者:小悪魔 - TALENT:加害者:幼稚
	;積極性
	セクハラ成分_積極性 = TALENT:加害者:度胸 + TALENT:加害者:陽気／陰気 - TALENT:加害者:無関心 - TALENT:加害者:感情乏しい + TALENT:加害者:性的興味 - TALENT:加害者:一線越えない + TALENT:加害者:献身的
	;タチ成分
	セクハラ成分_タチ = TALENT:加害者:応答 + TALENT:加害者:プライド + TALENT:加害者:献身的 + 2 * (TALENT:加害者:サド - TALENT:加害者:マゾ)
	;50〜69 脱衣セクハラ(愛撫)
	SIF セクハラ内容_脱衣愛撫
		セクハラ内容_脱衣愛撫 += 2 * (セクハラ成分_タチ + セクハラ成分_積極性)
	;70〜79 脱衣セクハラ(強要)
	SIF セクハラ内容_脱衣強要
		セクハラ内容_脱衣強要 += 2 * (セクハラ成分_誘惑 - セクハラ成分_積極性)
	;80〜99 性交
	SIF セクハラ内容_セックス
		セクハラ内容_セックス += 2 * (セクハラ成分_タチ + セクハラ成分_積極性)
	;------------------------------------------
	;10〜29 ソフト
	;------------------------------------------
	;色っぽい仕草
	LOCAL:CNT_色っぽい仕草 = 10 + セクハラ成分_誘惑 - セクハラ成分_恥じらい
	;見つめる
	LOCAL:CNT_見つめる = 10 + セクハラ成分_恥じらい
	;体を摺り寄せる
	LOCAL:CNT_体を摺り寄せる = 10 + セクハラ成分_誘惑 + セクハラ成分_積極性
	;スカートを捲る
	LOCAL:CNT_スカートを捲る = 10 + セクハラ成分_積極性 + セクハラ成分_タチ - セクハラ成分_恥じらい
	;耳元に息を吹きかける
	LOCAL:CNT_耳元に息を吹きかける = 10 + セクハラ成分_誘惑 + セクハラ成分_積極性
	;尻を撫でる
	LOCAL:CNT_尻を撫でる = 10 + セクハラ成分_積極性 + セクハラ成分_タチ
	;乳搾り手コキ(男性のみ、相手の浮気を公認してるか、相手にサドっ気3LV以上が必要、レベルの高さで発生率上昇)
	;ふたなり非対応なのは、前立腺の事を考えるとなんだか不自然っぽい感じがしたからです。わかりますね？
	IF TALENT:MASTER:性別 == 2
		;搾り発生率は２段階判定。
		;まずはサドっ気が2LVを超える毎に5%、浮気公認は１段階毎に10%（累積しない）
		IF ( MAX(TALENT:加害者:浮気公認*2,(ABL:加害者:サドっ気-2) ) > RAND:10)
			;指、器用な指、サド、サドっ気、精力などから算出。
			LOCAL:CNT_乳搾り手コキ = MIN(BASE:MASTER:精力, (ABL:加害者:指+5)*(TALENT:加害者:器用な指+1)+(TALENT:加害者:サド+1)*MIN(ABL:加害者:サドっ気,5)+BASE:MASTER:精力/50)
		ENDIF
		;ただし勃起しない時はしてこない
		IF MAXBASE:MASTER:勃起 < 300	; ED状態
			LOCAL:CNT_乳搾り手コキ = 0
		ENDIF
	ENDIF
	FOR LOOP_CNT,10,30
		LOCAL:LOOP_CNT *= セクハラ内容_ソフト
	NEXT
	;------------------------------------------
	;30〜49 着衣セクハラ
	;------------------------------------------
	;キス
	LOCAL:CNT_キス = 10 + セクハラ成分_積極性
	;後ろから抱きつく
	LOCAL:CNT_背面抱擁 = 10 + セクハラ成分_恥じらい - セクハラ成分_積極性
	;前から抱きつく
	LOCAL:CNT_対面抱擁 = 10 + セクハラ成分_誘惑 + セクハラ成分_積極性
	;胸を揉ませる
	LOCAL:CNT_胸を揉ませる = 10 + セクハラ成分_誘惑 - セクハラ成分_積極性
	;股間をまさぐる
	LOCAL:CNT_股間をまさぐる = 10 + セクハラ成分_積極性 + セクハラ成分_タチ
	;スカートの中を見せる
	LOCAL:CNT_スカートの中を見せる = 10 + セクハラ成分_恥じらい + セクハラ成分_誘惑
	;後ろから胸もみもみ
	LOCAL:CNT_胸を揉む = 10 + セクハラ成分_積極性 + セクハラ成分_タチ
	FOR LOOP_CNT,30,50
		LOCAL:LOOP_CNT *= セクハラ内容_着衣
	NEXT
	;------------------------------------------
	;50〜69 脱衣セクハラ(愛撫)
	;------------------------------------------
	;手淫
	LOCAL:CNT_手淫 = 10 + 2 * TALENT:加害者:器用な指 + ABL:加害者:指 + BASE:MASTER:勃起 / 500
	;フェラチオ、クンニ
	LOCAL:CNT_フェラチオ = 10 + TALENT:加害者:汚臭耐性 + 2 * TALENT:加害者:舌使い + ABL:加害者:舌 + BASE:MASTER:勃起 / 500
	;素股,貝合わせ
	LOCAL:CNT_素股 = 10 + ABL:加害者:腰 + 2 * TALENT:加害者:濡れやすさ + BASE:MASTER:勃起 / 500
	;パイズリ
	LOCAL:CNT_パイズリ = 10 + TALENT:加害者:バストサイズ + 2 * TALENT:加害者:淫乳 + ABL:加害者:胸 + BASE:MASTER:勃起 / 500
	;足コキ
	LOCAL:CNT_足コキ = 10 + 2 * TALENT:加害者:サド + MIN(ABL:加害者:サドっ気 , 5) + BASE:MASTER:勃起 / 500
	;アナル愛撫
	LOCAL:CNT_アナル愛撫 = 10 + 2 * TALENT:加害者:器用な指 + ABL:加害者:指
	;アナル舐め
	LOCAL:CNT_アナル舐め = 10 + 2 * TALENT:加害者:舌使い + ABL:加害者:舌 + MIN(TALENT:加害者:汚臭耐性, 0)
	;指挿入
	LOCAL:CNT_指挿入 = 10 + 2 * TALENT:加害者:器用な指 + ABL:加害者:指
	;胸愛撫
	LOCAL:CNT_胸愛撫 = 10 + 2 * TALENT:加害者:器用な指 + ABL:加害者:指
	;乳首吸い
	LOCAL:CNT_乳首吸い = 10 + 2 * TALENT:加害者:舌使い + ABL:加害者:舌 + 2 * TALENT:加害者:幼稚
	;ディープキス
	LOCAL:CNT_ディープキス = 10 + 2 * TALENT:加害者:舌使い + ABL:加害者:舌
	FOR LOOP_CNT,50,70
		LOCAL:LOOP_CNT = LOCAL:LOOP_CNT * セクハラ内容_脱衣愛撫 * (90 + RAND:21) / 100
	NEXT
	;------------------------------------------
	;70〜79 脱衣セクハラ(強要)
	;------------------------------------------
	;クンニ
	LOCAL:CNT_クンニ強制 = 10 + ABL:加害者:Ｃ感覚
	;フェラチオ
	LOCAL:CNT_フェラチオ強制 = 10 + ABL:加害者:Ｃ感覚
	;アナル奉仕
	LOCAL:CNT_アナル奉仕 = 10 + ABL:加害者:Ａ感覚
	;ぱふぱふ
	LOCAL:CNT_ぱふぱふ = 10 + ABL:加害者:Ｂ感覚
	;乳合わせ
	LOCAL:CNT_乳合わせ = 10 + ABL:加害者:Ｂ感覚
	;タワシ洗い
	LOCAL:CNT_タワシ洗い = 10 + ABL:加害者:Ｃ感覚
	FOR LOOP_CNT,70,80
		LOCAL:LOOP_CNT = LOCAL:LOOP_CNT * セクハラ内容_脱衣強要 * (90 + RAND:21) / 100
	NEXT
	;------------------------------------------
	;80〜99 性交
	;------------------------------------------
	セクハラ内容_セックス = 0
	;正常位
	LOCAL:CNT_正常位挿入される = 10 + ABL:加害者:Ｃ感覚
	LOCAL:CNT_騎乗位A挿入される = MIN(0, (BASE:加害者:5 - 500) / 100)
	LOCAL:CNT_後背位A挿入される = MIN(0, (BASE:加害者:5 - 500) / 100)
	LOCAL:CNT_背面座位A挿入される = MIN(0, (BASE:加害者:5 - 500) / 100)
	FOR LOOP_CNT,80,99
		LOCAL:LOOP_CNT = LOCAL:LOOP_CNT * セクハラ内容_セックス * (90 + RAND:21) / 100
	NEXT
	;ランダマイズ
	FOR LOOP_CNT,1,100
		LOCAL:LOOP_CNT = RAND:(MAX(LOCAL:LOOP_CNT,1))
	NEXT
	;実行判定
	FOR LOOP_CNT,1,100
		LOCAL:LOOP_CNT *= ACTABLE(加害者,LOOP_CNT)
	NEXT
	;------------------------------------------
	;実行判定
	;------------------------------------------
	LOCAL:300 = 0
	FOR LOOP_CNT,1,100
		IF LOCAL:LOOP_CNT > LOCAL:300
			LOCAL:300 = LOCAL:LOOP_CNT
			セクハラ内容_ソフト = LOOP_CNT
		ENDIF
	NEXT
	SIF LOCAL:300 > 0
		TCVAR:加害者:20 = セクハラ内容_ソフト
