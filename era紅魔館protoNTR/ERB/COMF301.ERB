;お茶を淹れる
@COM301
LOCAL = RAND:100
LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
SIF LOCAL:1 > 99
	LOCAL:1 = 99
IF LOCAL <= 9
	;10％で大成功
	EXP:MASTER:会話経験 ++
	TFLAG:コマンド成功度 = 成功度_大成功
ELSEIF LOCAL >= LOCAL:1
	;10〜1％で失敗
	TFLAG:コマンド成功度 = 成功度_失敗
ELSE
	;残りは成功
	TFLAG:コマンド成功度 = 成功度_成功
ENDIF
;先制発生時は失敗しない
SIF TCVAR:20
	TFLAG:コマンド成功度 = MAX(TFLAG:コマンド成功度,成功度_成功)
DOWNBASE:MASTER:気力 += 100
DOWNBASE:MASTER:体力 += 50

;その場に居るTARGET全員で回す
LOCAL:1 = TARGET
FOR LOCAL,1,CHARANUM
	SIF TARGET:LOCAL <= 0
		CONTINUE
	SIF CFLAG:(TARGET:LOCAL):睡眠
		CONTINUE
	TARGET = TARGET:LOCAL
	CALL COM301_1
NEXT
TARGET = LOCAL:1

TIME += 30
RETURN 1

@COM301_1

DOWNBASE:気力 += 10

;固定で獲得するソース
SOURCE:歓楽 = 500

;ABL:従順をみる
IF ABL:従順 <= 1
	SOURCE:歓楽 += (ABL:従順 * 60)
ELSEIF ABL:親密 <= 3
	SOURCE:歓楽 += 200 + (ABL:従順 * 60)
ELSEIF ABL:親密 <= 5
	SOURCE:歓楽 += 500 + (ABL:従順 * 60)
ELSEIF ABL:親密 <= 8
	SOURCE:歓楽 += 750 + (ABL:従順 * 75)
ELSEIF ABL:親密 <= 11
	SOURCE:歓楽 += 1000 + (ABL:従順 * 75)
ELSE
	SOURCE:歓楽 += 2000 + (ABL:従順 * 30)
ENDIF

;好感度をみる
IF CFLAG:好感度 <= 500
	SOURCE:歓楽 += CFLAG:2
ELSEIF CFLAG:好感度 <= 5000
	SOURCE:歓楽 += 500 + (CFLAG:好感度 - 500) / 3
ELSE
	SOURCE:歓楽 += 2000 + (CFLAG:好感度 - 5000) / 5
ENDIF
SIF SOURCE:歓楽 < 0
	SOURCE:歓楽 = 0


SOURCE:受動 = 200 + 100 * ABL:従順
SOURCE:征服 = 200 + 100 * ABL:サドっ気

IF TFLAG:コマンド成功度 == 成功度_失敗
	TIMES SOURCE:歓楽 , 0.10
	TIMES SOURCE:征服 , 0.50
	TIMES SOURCE:受動 , 0.50
ELSEIF TFLAG:コマンド成功度 == 成功度_成功
	TIMES SOURCE:歓楽 , 1.00
	TIMES SOURCE:征服 , 1.00
	TIMES SOURCE:受動 , 1.00
ELSE
	TIMES SOURCE:歓楽 , 2.00
	TIMES SOURCE:征服 , 2.00
	TIMES SOURCE:受動 , 2.00
ENDIF

;お茶を飲んだフラグ
TCVAR:飲茶日時 = DATETIME()

;お手洗い勝手に拡張パッチ対応
;水分過剰摂取量上昇。単位はマイクロリットル。
TCVAR:水分過剰摂取量 += 110000

;お茶を飲むことで口の汚れ解消
STAIN:口 = 汚れ_なし
