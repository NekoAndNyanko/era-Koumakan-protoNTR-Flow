;-------------------------------------------------------------------------------
;会話コマンド(拡張版) - 妖精メイド増員 実行判定
; RETURN 0 コマンドの実行不可
; RETURN 1 コマンドの実行可能
@COM300_02_ENABLE
	RESULTS:0 = %"[ 2]-新しいメイドを増やしませんか？"%
	IF (NO:TARGET != 4 ) && (NO:TARGET != 5)
		;咲夜さんかレミリアお嬢様だけ許可を出せる
		RETURN 0
	ELSEIF TCVAR:会話累積値 > 0
		;初回の会話じゃないとダメ(毎回出るとクッソうざいので)
		RETURN 0
	ELSEIF 妖精メイド採用状態:(妖精メイド雇用上限-1) != 妖精メイド採用状態_未許可
		;最後のメイドに採用許可が出ていたら、それ以上は増やせない
		RETURN 0
	ELSE
		RETURN 1
	ENDIF
RETURN 0

;-------------------------------------------------------------------------------
;会話コマンド(拡張版) - 妖精メイド増員
; RETURN 0 コマンドの実行に失敗
; RETURN 1 コマンドの実行に成功
; 咲夜さんかお嬢様しか想定してないんで、口上もここに混ぜ込んでいいよね？
@COM300_02
	#DIM 成否判定
	;実行判定が通らない時に呼ばれた時対策
	CALL COM300_02_ENABLE()
	IF !RESULT:0
		RETURN 0
	ENDIF
	;清掃、戦闘、料理の３技能が伸びてないと部下は増やせません。
	成否判定 = ((ABL:MASTER:清掃技能+ABL:MASTER:戦闘能力+ABL:MASTER:料理技能)/3) > FairyMaids_HiredCount()
	;いつもの会話累積値増加など
	TCVAR:会話累積値 += 200 / (3 + ABL:MASTER:話術技能)
	TIME += 10
	EXP:MASTER:会話経験 ++
	TRYCCALLFORM COM300_02_%TOSTR(成否判定,"00")%
		RETURN RESULT:0
	CATCH
		RETURN 0
	ENDCATCH
RETURN 0

;-------------------------------------------------------------------------------
;会話コマンド(拡張版) - 妖精メイド増員 - 失敗
; RETURN 0 コマンドの実行に失敗
; RETURN 1 コマンドの実行に成功
@COM300_02_00
	TRYCCALLFORM COM300_02_00_%TOSTR(TARGET,"00")%
		RETURN RESULT:0
	CATCH
		RETURN 0
	ENDCATCH
RETURN 0

;-------------------------------------------------------------------------------
;会話コマンド(拡張版) - 妖精メイド増員 - 失敗 - 咲夜の反応
@COM300_02_00_04
	PRINTFORML %"「"+CALLNAME:MASTER+"がもっと仕事をしてくれれば済む話でしょう？」"%
	PRINTFORML %"あっさりと拒否されてしまった……"%
RETURN 0

;-------------------------------------------------------------------------------
;会話コマンド(拡張版) - 妖精メイド増員 - 失敗 - レミリアの反応
@COM300_02_00_05
	PRINTFORML %"「却下。部下が欲しいのなら、もっと仕事ができるようになってから言いなさい」"%
	PRINTFORML %"あっさりと拒否されてしまった……"%
RETURN 0

;-------------------------------------------------------------------------------
;会話コマンド(拡張版) - 妖精メイド増員 - 成功
; RETURN 0 コマンドの実行に失敗
; RETURN 1 コマンドの実行に成功
@COM300_02_01
	CALLF FairyMaids_addVacancy()
	TRYCCALLFORM COM300_02_01_%TOSTR(TARGET,"00")%
		RETURN RESULT:0
	CATCH
		RETURN 0
	ENDCATCH
RETURN 0

;-------------------------------------------------------------------------------
;会話コマンド(拡張版) - 妖精メイド増員 - 成功 - 咲夜の反応
@COM300_02_01_04
	PRINTFORML %"「妖精に心当たりがあるなら連れてきてもいいわ。ついでに新人メイドの教育もお願いしようかしら」"%
	PRINTFORML %"「誰かに教える方が、"+CALLNAME:MASTER+"のためにもなりそうだし」"%
RETURN 0

;-------------------------------------------------------------------------------
;会話コマンド(拡張版) - 妖精メイド増員 - 成功 - レミリアの反応
@COM300_02_01_05
	PRINTFORML %"「メイドねえ……ま、いいんじゃない？良さそうな妖精を見繕って捕まえておいで」"%
	PRINTFORML %"「連れてきた妖精は"+CALLNAME:MASTER+"の部下にしてもいいけど、教育はちゃんとするようにね」"%
RETURN 0
