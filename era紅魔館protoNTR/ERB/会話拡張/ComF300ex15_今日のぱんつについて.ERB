;-------------------------------------------------------------------------------
;会話コマンド(拡張版) - 今日のぱんつについて 実行判定
; RETURN 0 コマンドの実行不可
; RETURN 1 コマンドの実行可能
@COM300_15_ENABLE
	#DIM 奴隷
	#DIM 下着１ = GETNUM(EQUIP,"下半身下着１")
	#DIMS 下着名
	奴隷 = TARGET:0
	下着名 = %下着名１_２(奴隷)%
	RESULTS:0 = %"[15]-"+CALLNAME:奴隷+"の"+下着名+"について"%
	IF TCVAR:奴隷:会話不能
		RETURN 0
	ELSEIF Unknown下着(奴隷, 下着１)
		;下着不明ならダメ
		RETURN 0
	ELSEIF SHOW_BOTTOM(奴隷) == ""
		;下半身丸出しだとダメ
		RETURN 0
	ELSEIF CFLAG:奴隷:ぱんつ確認 == 3
		;既にぱんつについて会話してたらダメ(1であれば脱がしたこと、2であれば覗きによる確認)
		RETURN 0
	ELSE
		RETURN 1
	ENDIF
RETURN 0

;-------------------------------------------------------------------------------
;会話コマンド(拡張版) - 今日のぱんつについて話す
; RETURN 0 コマンドの実行に失敗
; RETURN 1 コマンドの実行に成功
@COM300_15
	#DIM 調教者
	#DIM 奴隷
	調教者 = PLAYER:0
	奴隷 = TARGET:0
	IF TALENT:奴隷:公衆便所
		CALL MESSAGE_COM_300_15_01(調教者,奴隷)
	ELSEIF TALENT:奴隷:小悪魔 || TALENT:奴隷:淫乱
		CALL MESSAGE_COM_300_15_02(調教者,奴隷)
	ELSEIF CFLAG:奴隷:屈服度 > CFLAG:奴隷:好感度 && CFLAG:奴隷:屈服度 > 999
		CALL MESSAGE_COM_300_15_03(調教者,奴隷)
	ELSEIF CFLAG:奴隷:ぱんつ確認 == 2
		CALL MESSAGE_COM_300_15_04(調教者,奴隷)
	ELSE
		CALL MESSAGE_COM_300_15_05(調教者,奴隷)
	ENDIF
RETURN 1

;-------------------------------------------------------------------------------
;会話拡張：ぱんつについて：貞操帯
;　前提：公衆便所
@MESSAGE_COM_300_15_01(調教者,奴隷)
	#DIM 調教者
	#DIM 奴隷
	#DIMS 下着名
	#DIM ノーパン
	
	CFLAG:奴隷:ぱんつ確認 = 3
	;はいてないかチェック
	ノーパン = CHK_NO_PANTS(奴隷)
	IF 1 == ノーパン
		PRINTFORML %CALLNAME:調教者+"が"+CALLNAME:奴隷+"が下に穿いていないことについて尋ねると、"%
		PRINTFORML 今は身に着けていないけど%CALLNAME:奴隷+"の身体は"+NTR_NAME(0)+"に管理される『商品』なのだから、"%
		PRINTFORML 本来なら%CALLNAME:調教者+"が勝手に使う事ができないように"+下着名+"を穿かされていて、"%
		PRINTFORML %"もしも誰かに"+NTR_NAME(0)+"から買った鍵を見せられたのなら、"%
		PRINTFORML %"時間の許す限りどんな変態的なプレイでも受け入れなければならないのだ、と説明してくれた……。"%
	ELSE
		下着名 = %下着名１_２(奴隷)%
		PRINTFORML %CALLNAME:調教者+"が"+CALLNAME:奴隷+"の"+下着名+"について尋ねると、"%
		PRINTFORML %CALLNAME:奴隷+"の身体は"+NTR_NAME(0)+"に管理される『商品』なのだから、"%
		PRINTFORML %CALLNAME:調教者+"が勝手に使う事ができないように"+下着名+"を穿かされていて、"%
		PRINTFORML %"もしも誰かに"+NTR_NAME(0)+"から買った鍵を見せられたのなら、"%
		PRINTFORML %"時間の許す限りどんな変態的なプレイでも受け入れなければならないのだ、と説明してくれた……。"%
		PRINTFORML %""%
		ENDIF
	TRYCALLLIST
		FUNC KOJO_MESSAGE_COM_K{CFLAG:奴隷:297}_300_15_01(奴隷, 調教者, ノーパン)
		FUNC KOJO_MESSAGE_COM_KU_300_15_01(奴隷, 調教者, ノーパン)
	ENDFUNC
RETURN 1

;-------------------------------------------------------------------------------
;会話拡張：ぱんつについて：ADULT下着（小悪魔/淫乱)
;　前提：小悪魔または淫乱
@MESSAGE_COM_300_15_02(調教者,奴隷)
	#DIM 調教者
	#DIM 奴隷
	#DIMS 下着名
	#DIM ノーパン
	
	CFLAG:奴隷:ぱんつ確認 = 3
	;はいてないかチェック
	ノーパン = CHK_NO_PANTS(奴隷)
	IF 1 == ノーパン
		PRINTFORML %CALLNAME:調教者+"が"+CALLNAME:奴隷+"が下に穿いていないことについて尋ねると、"%
		PRINTFORML %CALLNAME:奴隷+"はこうしていると"+CALLNAME:調教者+"は興奮するのかと問い返してきた。"%
	ELSE
		下着名 = %下着名１_２(奴隷)%
		PRINTFORML %CALLNAME:調教者+"が"+CALLNAME:奴隷+"の"+下着名+"について尋ねると、"%
		PRINTFORML %CALLNAME:奴隷+"はこの"+下着名+"が似合っているか、"+CALLNAME:調教者+"は興奮するのかと問い返してきた。"%
	ENDIF
	TRYCALLLIST
		FUNC KOJO_MESSAGE_COM_K{CFLAG:奴隷:297}_300_15_02(奴隷, 調教者, ノーパン)
		FUNC KOJO_MESSAGE_COM_KU_300_15_02(奴隷, 調教者, ノーパン)
	ENDFUNC
RETURN 1

;-------------------------------------------------------------------------------
;会話拡張：ぱんつについて：ADULT下着（NTR)
;　前提：屈服度が好感度より高く、屈服度が999以上
@MESSAGE_COM_300_15_03(調教者,奴隷)
	#DIM 調教者
	#DIM 奴隷
	#DIMS 下着名
	#DIM ノーパン
	#DIM 積極度

	CFLAG:奴隷:ぱんつ確認 = 3
	;はいてないかチェック
	ノーパン = CHK_NO_PANTS(奴隷)
	;積極度チェック
	IF NTR_CHK_FAVORABLY(奴隷, FAV_寝取られ) || NTR_CHK_FAVORABLY(奴隷, FAV_寝取られ寸前) || NTR_CHK_FAVORABLY(奴隷, FAV_寝取られそう)
		積極度 = 1
	ELSEIF TALENT:奴隷:小悪魔 || TALENT:奴隷:淫乱
		積極度 = 1
	ELSEIF TALENT:奴隷:浮気公認 > 0
		IF TALENT:奴隷:サド
			積極度 = 1
		ELSE
			積極度 = 0
		ENDIF
	ELSE
		積極度 = 0
	ENDIF
	;はいてないかチェック
	IF 1 == ノーパン
		PRINTFORML %CALLNAME:調教者+"が"+CALLNAME:奴隷+"が下に穿いていないことについて尋ねると、"%
		IF 1 == 積極度
			PRINTFORML %CALLNAME:奴隷+"は恥ずかしいけれど、"+NTR_NAME(0)+"に言われてやっているのだと言った。"%
		ELSE
			PRINTFORML %CALLNAME:奴隷+"は恥ずかしいけれど、"+NTR_NAME(0)+"に言われて仕方なくやっているのだと言った。"%
		ENDIF
	ELSE
		下着名 = %下着名１_２(奴隷)%
		PRINTFORML %CALLNAME:調教者+"が"+CALLNAME:奴隷+"の"+下着名+"について尋ねると、"%
		IF 1 == 積極度
			PRINTFORML %CALLNAME:奴隷+"は恥ずかしいけれど、"+NTR_NAME(0)+"に言われてこれを穿いているのだと言った。"%
		ELSE
			PRINTFORML %CALLNAME:奴隷+"は恥ずかしいけれど、"+NTR_NAME(0)+"に言われて仕方なく穿いているのだと言った。"%
		ENDIF
	ENDIF
	TRYCALLLIST
		FUNC KOJO_MESSAGE_COM_K{CFLAG:奴隷:297}_300_15_03(奴隷, 調教者, ノーパン, 積極度)
		FUNC KOJO_MESSAGE_COM_KU_300_15_03(奴隷, 調教者, ノーパン, 積極度)
	ENDFUNC
RETURN 1

;-------------------------------------------------------------------------------
;会話拡張：ぱんつについて：普通の下着(覗きで確認した場合)
@MESSAGE_COM_300_15_04(調教者,奴隷)
	#DIM 調教者
	#DIM 奴隷
	#DIMS 下着名
	
	CFLAG:奴隷:ぱんつ確認 = 3
	;はいてないかチェック
	IF 1 == CHK_NO_PANTS(奴隷)
		PRINTFORML %CALLNAME:調教者+"が"+CALLNAME:奴隷+"が下に穿いていないことについて尋ねると、"%
		PRINTFORML %CALLNAME:奴隷+"はなぜそれを知っているのか、いつ覗いたのかと怒り出した。"%
	ELSE
		下着名 = %下着名１_２(奴隷)%
		PRINTFORML %CALLNAME:調教者+"が"+CALLNAME:奴隷+"の"+下着名+"について尋ねると、"%
		PRINTFORML %CALLNAME:奴隷+"はなぜそれを知っているのか、いつ覗いたのかと怒り出した。"%
	ENDIF
	;怒りは好感度で相殺できる
	IF 0 != CFLAG:奴隷:好感度
		SOURCE:奴隷:反感 = LIMIT(100000/CFLAG:奴隷:好感度,0,1000)
	ELSE
		SOURCE:奴隷:反感 = 1000
	ENDIF
	TRYCALLLIST
		FUNC KOJO_MESSAGE_COM_K{CFLAG:奴隷:297}_300_15_04(奴隷, 調教者)
		FUNC KOJO_MESSAGE_COM_KU_300_15_04(奴隷, 調教者)
	ENDFUNC
RETURN 1

;-------------------------------------------------------------------------------
;会話拡張：ぱんつについて：普通の下着(脱がせて確認した場合)
@MESSAGE_COM_300_15_05(調教者,奴隷)
	#DIM 調教者
	#DIM 奴隷
	#DIMS 下着名
	
	CFLAG:奴隷:ぱんつ確認 = 3
	;はいてないかチェック
	IF 1 == CHK_NO_PANTS(奴隷)
		PRINTFORML %CALLNAME:調教者+"が"+CALLNAME:奴隷+"が下に穿いていないことについて尋ねると、"%
		PRINTFORML %CALLNAME:奴隷+"は人には言わないで欲しいと顔を赤らめながら言った。"%
	ELSE
		下着名 = %下着名１_２(奴隷)%
		PRINTFORML %CALLNAME:調教者+"が"+CALLNAME:奴隷+"の"+下着名+"について尋ねると、"%
		PRINTFORML %CALLNAME:奴隷+"はあまりそういう話はしないで欲しいと"+CALLNAME:調教者+"を咎めた。"%
	ENDIF
	TRYCALLLIST
		FUNC KOJO_MESSAGE_COM_K{CFLAG:奴隷:297}_300_15_05(奴隷, 調教者)
		FUNC KOJO_MESSAGE_COM_KU_300_15_05(奴隷, 調教者)
	ENDFUNC
RETURN 1
