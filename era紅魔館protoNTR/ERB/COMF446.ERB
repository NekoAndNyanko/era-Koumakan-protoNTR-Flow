;-------------------------------------------------
;撮影
;-------------------------------------------------
@COM446
	#DIM キャラ番号
	#DIM 部屋番号
	#DIM 撮影する部屋
	PRINTL 何処を覗きますか？
	FOR 部屋番号,0,MAXROOM()
		IF CAN_MOVE(CFLAG:MASTER:現在位置,部屋番号)
			FOR キャラ番号,1,CHARANUM
				IF CFLAG:キャラ番号:現在位置 == 部屋番号
					SETCOLOR 色設定_マップ_他人
				ENDIF
			NEXT
			PRINTFORML %"["%{部屋番号,2}%"] - "+GETPLACENAME(部屋番号)%
			RESETCOLOR
		ENDIF
	NEXT
	PRINTL [999] - 戻る
	$INPUT_LOOP
	INPUT
	撮影する部屋 = RESULT
	TFLAG:現在覗いている場所 = 0
	IF 撮影する部屋 == 999
		RETURN 0
	ENDIF
	IF CAN_MOVE(CFLAG:MASTER:現在位置,撮影する部屋)
		CALL PEEP(撮影する部屋)
		TIME += 3
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP
	ENDIF
RETURN 1

;-------------------------------------------------------------------------------
;撮影処理
@PEEP(部屋番号)
	#DIM キャラ番号
	#DIM 部屋番号
	#DIM 撮影枚数
	#DIM 写真内容
	#DIM 写真詳細
	#DIM 攻め側
	#DIM 受け側

	撮影枚数 = 0


	; 鍵がかかっている場合
	IF isLocked(部屋番号)
		IF canUnLock(MASTER, 部屋番号)
			PRINTFORML %CALLNAME:MASTER + "は鍵を使って扉を少しだけ開き、中の様子をカメラに収めた。"%
		ELSE
			IF isLockedByBolt(部屋番号)
				PRINTFORM どうやら内側から閂がかけられているらしく、
			ELSE
				PRINTFORM 鍵がかかっていて、
			ENDIF
			PRINTFORML 扉を開けることができなかった。
			IF IS_NTR_SHOW_ALL()
				PRINTFORML %CALLNAME:MASTER + "は仕方なく、採光用の小さな窓から" + GETPLACENAME(部屋番号) + "の様子を撮影した……。"%
			ELSE
				HTML_PRINT @"<font color='#" +TOSTR(色設定_テキスト_備考,"x") +"'>ＮＴＲ筒抜けモードを有効にすると、採光窓から中を覗く事ができるようになります。</font>"
				RETURN 1
			ENDIF
		ENDIF
	ENDIF

	TFLAG:現在覗いている場所 = 部屋番号
		;住人同士の交流は先に判定する
	IF ITEM:フィルム > 1
		FOR キャラ番号,1,CHARANUM
			IF CFLAG:キャラ番号:現在位置 == 部屋番号
				IF 0 != CFLAG:キャラ番号:住人同士イベント内容 
						;攻め・受けを判定
					IF CFLAG:キャラ番号:住人同士イベント内容 % 10 == 0
						受け側 = キャラ番号
						攻め側 = CFLAG:キャラ番号:住人同士イベント相手
					ELSE
						受け側 = CFLAG:キャラ番号:住人同士イベント相手
						攻め側 = キャラ番号
					ENDIF
					撮影枚数++
					写真内容 = CHOOSE_PHOTO2(攻め側,受け側,部屋番号)
					IF 写真内容 <= 写真_住人同士_キス
						PRINTFORML %CALLNAME:攻め側%と%CALLNAME:受け側%の%PHOTO_NAME(写真内容)%を撮影した
						TSTR:3 = %CALLNAME:攻め側%と%CALLNAME:受け側%{写真内容}PH/
					ELSE
						PRINTFORML %CALLNAME:攻め側%が%CALLNAME:受け側%に%PHOTO_NAME(写真内容)%を撮影した
						TSTR:3 = %CALLNAME:攻め側%が%CALLNAME:受け側%{写真内容}PH/
					ENDIF
						;PRINTFORML %CALLNAME:攻め側%と%CALLNAME:受け側%の{写真内容}を撮影した
					CFLAG:MASTER:収集品死守戦フラグ ++
					CSTR:MASTER:収集品 += TSTR:3
					ITEM:フィルム--
					BREAK
				ENDIF
			ENDIF
		NEXT
	ENDIF
		;住人同士の交流で撮影できなければ通常の処理
	IF 0 == 撮影枚数
		FOR キャラ番号,1,CHARANUM
			IF ITEM:フィルム < 1
					;フィルムが無ければ終了
				RETURN 0
			ELSEIF CFLAG:キャラ番号:現在位置 == 部屋番号
				撮影枚数++
				写真内容 = CHOOSE_PHOTO(キャラ番号,部屋番号)
				写真詳細 = CHOOSE_PHOTO_DETAIL(キャラ番号,部屋番号)
				PRINTFORML %CALLNAME:キャラ番号%の%PHOTO_NAME(写真内容, 写真詳細)%を撮影した
				CFLAG:MASTER:収集品死守戦フラグ ++
				TSTR:3 = %CALLNAME:キャラ番号 + TOSTR(写真内容) + "PH" + TOSTR(写真詳細, "#")  + "/"%
				CSTR:MASTER:収集品 += TSTR:3
				ITEM:フィルム--
			ENDIF
		NEXT
	ENDIF
	IF !撮影枚数
		PRINTL 誰もいなかった・・・・・・
		RETURN 0
	ENDIF
RETURN 1

;-------------------------------------------------------------------------------
;NTRお持ち帰り写真を取得する
@GET_NTR_TAKEOUT_PHOTO(奴隷, 写真内容, 写真詳細)
	#DIM 奴隷
	#DIM 写真内容
	#DIM 写真詳細
	#DIM 写真追加
	
	写真追加 = 1

	SELECTCASE 写真内容
	CASE 写真_NTR_売春
		SELECTCASE 写真詳細
			CASE 写真詳細_売春_処女競売0 TO 写真詳細_売春_結婚式3
				写真追加 = 1
			CASEELSE
				写真追加 = 0
		ENDSELECT
	CASEELSE
		写真追加 = 0
	ENDSELECT

	IF (写真追加 == 1) 
		; 住人から貰った写真と盗撮した写真の区別が面倒なので死守戦なしでコレクション追加
		PRINTFORML %CALLNAME:奴隷%の%PHOTO_NAME(写真内容, 写真詳細)%を入手、コレクションに追加した
		;CFLAG:MASTER:収集品死守戦フラグ ++
		TSTR:3 = %CALLNAME:奴隷 + TOSTR(写真内容) + "PH" + TOSTR(写真詳細, "#")  + "/"%
		;CSTR:MASTER:収集品 += TSTR:3
		SIF SAVESTR:10 == ""
			SAVESTR:10 = /
		SAVESTR:10 += TSTR:3
	ELSE
		RETURN 0
	ENDIF

RETURN 1

;-------------------------------------------------------------------------------
;写真の名称を取得する
@PHOTO_NAME(写真番号, 写真詳細=0)
	#FUNCTIONS
	#DIM 写真番号
	#DIM 写真詳細
	SELECTCASE 写真番号
		CASE 写真_NTR_キス TO 写真_NTR_Ａセックス, 写真_NTR_接触, 写真_NTR_抱擁, 写真_NTR_売春
			RETURNF PHOTO_DETAIL_NAME(写真番号, 写真詳細)
		CASE 写真_普段
			RETURNF "普段"
		CASE 写真_着替え
			RETURNF "お着替え"
		CASE 写真_入浴
			RETURNF "入浴"
		CASE 写真_寝顔
			RETURNF "寝顔"
		CASE 写真_料理
			RETURNF "お料理姿"
		CASE 写真_居眠り
			RETURNF "居眠り"
		CASE 写真_読書
			RETURNF "読書姿"
		CASE 写真_トイレ
			RETURNF "おトイレ姿"
		CASE 写真_自慰
			RETURNF "自慰姿"
		CASE 写真_住人同士_会話
			RETURNF "会話している姿"
		CASE 写真_住人同士_お茶
			RETURNF "お茶を飲んでいる姿"
		CASE 写真_住人同士_接触
			RETURNF "親密に接触している姿"
		CASE 写真_住人同士_抱擁
			RETURNF "抱きついている姿"
		CASE 写真_住人同士_キス
			RETURNF "キスしている姿"
		CASE 写真_住人同士_愛撫
			RETURNF "股間を愛撫している姿"
		CASE 写真_住人同士_Ａ愛撫
			RETURNF "アナルを愛撫している姿"
		CASE 写真_住人同士_胸愛撫
			RETURNF "胸を愛撫している姿"
		CASE 写真_住人同士_Ｖ性交
			RETURNF "セックス姿"
		CASE 写真_住人同士_Ａ性交
			RETURNF "アナルセックス姿"
	ENDSELECT

;写真の詳細を返す。あまり長く描写するとはみ出るので全角16文字程度に収めること
@PHOTO_DETAIL_NAME(写真番号, 写真詳細)
	#FUNCTIONS
	#DIM 写真番号
	#DIM 写真詳細
	SELECTCASE 写真番号
		CASE 写真_NTR_キス
			SELECTCASE 写真詳細
				CASE 写真詳細_おねだり
					; 16文字"--------------------------------"
					RETURNF "舌を出して自分からキスをねだる姿"
				CASE 写真詳細_快楽
					RETURNF "蕩けた顔でキスされるがままの姿"
				CASE 写真詳細_絶頂
					RETURNF "キスだけで絶頂に達してしまう姿"
				CASE 写真詳細_恋人
					RETURNF "恋人のように熱いキスを交わす姿"
				CASEELSE
					RETURNF "キスしている姿"
			ENDSELECT
		CASE 写真_NTR_愛撫
			SELECTCASE 写真詳細
				CASE 写真詳細_おねだり
					; 16文字"--------------------------------"
					RETURNF "はしたなく股を開き愛撫をねだる姿"
				CASE 写真詳細_快楽
					RETURNF "蕩けた顔で愛撫されるのを楽しむ姿"
				CASE 写真詳細_絶頂
					RETURNF "愛撫されて絶頂し強く抱き付く姿"
				CASE 写真詳細_恋人
					RETURNF "仲睦まじく互いの愛撫を楽しむ姿"
				CASEELSE
					RETURNF "愛撫されている姿"
			ENDSELECT
		CASE 写真_NTR_胸愛撫
			SELECTCASE 写真詳細
				CASE 写真詳細_おねだり
					; 16文字"--------------------------------"
					RETURNF "胸を押し付けて愛撫をねだる姿"
				CASE 写真詳細_快楽
					RETURNF "胸を揉まれて蕩けた顔を見せる姿"
				CASE 写真詳細_絶頂
					RETURNF "乳首を摘まみあげられて絶頂する姿"
				CASE 写真詳細_恋人
					RETURNF "自ら胸を吸わせ２人で戯れあう姿"
				CASEELSE
					RETURNF "胸を愛撫されている姿"
			ENDSELECT
		CASE 写真_NTR_フェラチオ
			SELECTCASE 写真詳細
				CASE 写真詳細_おねだり
					; 16文字"--------------------------------"
					RETURNF "口を開いたままペニスをねだる姿"
				CASE 写真詳細_快楽
					RETURNF "蕩けた顔でペニスをしゃぶる姿"
				CASE 写真詳細_絶頂
					RETURNF "ペニスを咥えたまま絶頂に震える姿"
				CASE 写真詳細_恋人
					RETURNF "愛おしそうにペニスにキスする姿"
				CASEELSE
					RETURNF "フェラチオする姿"
			ENDSELECT
		CASE 写真_NTR_シックスナイン
			SELECTCASE 写真詳細
				CASE 写真詳細_おねだり
					; 16文字"--------------------------------"
					RETURNF "ペニスを咥え顔に腰を押し付ける姿"
				CASE 写真詳細_快楽
					RETURNF "快楽に負けじとペニスをしゃぶる姿"
				CASE 写真詳細_絶頂
					RETURNF "ペニスを舐めながら絶頂に震える姿"
				CASE 写真詳細_恋人
					RETURNF "互いの陰部に口づけて快楽を貪る姿"
				CASEELSE
					RETURNF "６９で奉仕する姿"
			ENDSELECT
		CASE 写真_NTR_素股
			SELECTCASE 写真詳細
				CASE 写真詳細_おねだり
					; 16文字"--------------------------------"
					RETURNF "素股では我慢できず挿入をねだる姿"
				CASE 写真詳細_快楽
					RETURNF "蕩けた顔のまま素股で奉仕する姿"
				CASE 写真詳細_絶頂
					RETURNF "素股で腰を振りながら絶頂する姿"
				CASE 写真詳細_恋人
					RETURNF "愛おしそうに腰を振って素股する姿"
				CASEELSE
					RETURNF "素股で奉仕する姿"
			ENDSELECT
		CASE 写真_NTR_Ｖセックス
			SELECTCASE 写真詳細
				CASE 写真詳細_おねだり
					; 16文字"--------------------------------"
					RETURNF "股を開き秘所を拡げ挿入をねだる姿"
				CASE 写真詳細_快楽
					RETURNF "セックスの快感に酔いしれ蕩ける姿"
				CASE 写真詳細_絶頂
					RETURNF "激しいセックスで絶頂に震える姿"
				CASE 写真詳細_恋人
					RETURNF "恋人同士のようなセックスをする姿"
				CASEELSE
					RETURNF "セックスする姿"
			ENDSELECT
		CASE 写真_NTR_Ａセックス
			SELECTCASE 写真詳細
				CASE 写真詳細_おねだり
					; 16文字"--------------------------------"
					RETURNF "突き出した尻を振り挿入をねだる姿"
				CASE 写真詳細_快楽
					RETURNF "アナルセックスの快楽に震える姿"
				CASE 写真詳細_絶頂
					RETURNF "アナルへの挿入で絶頂してしまう姿"
				CASE 写真詳細_恋人
					RETURNF "アナルセックスを楽しみ戯れあう姿"
				CASEELSE
					RETURNF "アナルセックスする姿"
			ENDSELECT
		CASE 写真_NTR_接触
			SELECTCASE 写真詳細
				CASE 写真詳細_おねだり
					; 16文字"--------------------------------"
					RETURNF "腕を絡ませスキンシップを求める姿"
				CASE 写真詳細_恋人
					RETURNF "恋人同士のように身体を寄せ合う姿"
				CASEELSE
					RETURNF "親密に接触している姿"
			ENDSELECT
		CASE 写真_NTR_抱擁
			SELECTCASE 写真詳細
				CASE 写真詳細_おねだり
					; 16文字"--------------------------------"
					RETURNF "腕を開いて抱擁をねだる姿"
				CASE 写真詳細_恋人
					RETURNF "互いに抱き合い熱い視線を交わす姿"
				CASEELSE
					RETURNF "抱きついている姿"
			ENDSELECT
		CASE 写真_NTR_売春	; 公衆便所お持ち帰り時
			SELECTCASE 写真詳細
				CASE 写真詳細_売春_処女競売0
					; 16文字"--------------------------------"
					RETURNF "ステージで競りにかけられている姿"
				CASE 写真詳細_売春_処女競売1
					RETURNF "客に抱き寄せられている姿"
				CASE 写真詳細_売春_処女競売2
					RETURNF "客とキスしている姿"
				CASE 写真詳細_売春_結婚式0
					RETURNF "ウェディングドレス姿"
				CASE 写真詳細_売春_結婚式1
					RETURNF "客と指輪交換している姿"
				CASE 写真詳細_売春_結婚式2
					RETURNF "客と誓いのキスをしている姿"
				CASE 写真詳細_売春_結婚式3
					RETURNF "ドレスのまま客に抱かれている姿"
				CASEELSE
					RETURNF "客に抱きついている姿"
			ENDSELECT
	ENDSELECT

;-------------------------------------------------------------------------------
;撮影した写真の内容取得
;@return 写真内容コード
@CHOOSE_PHOTO(キャラ番号,部屋番号)
	#FUNCTION
	#DIM キャラ番号
	#DIM 部屋番号
		;訪問者と一緒で、何かしらの行為中であれば寝取られ姿の撮影
	IF CFLAG:キャラ番号:現在位置 == FLAG:訪問者の現在位置 && FLAG:訪問者との行為 != 0
		SELECTCASE FLAG:訪問者との行為
			CASE NTR_行為_キス
				RETURNF 写真_NTR_キス
			CASE NTR_行為_愛撫
				RETURNF 写真_NTR_愛撫
			CASE NTR_行為_胸愛撫
				RETURNF 写真_NTR_胸愛撫
			CASE NTR_行為_フェラチオ
				RETURNF 写真_NTR_フェラチオ
			CASE NTR_行為_シックスナイン
				RETURNF 写真_NTR_シックスナイン
			CASE NTR_行為_素股
				RETURNF 写真_NTR_素股
			CASE NTR_行為_Ｖ正常位 TO NTR_行為_Ｖ背面立位
				RETURNF 写真_NTR_Ｖセックス
			CASE NTR_行為_Ａ正常位 TO NTR_行為_Ａ背面立位
				RETURNF 写真_NTR_Ａセックス
		ENDSELECT
	ENDIF
		;訪問者と一緒で、住人からのカウンターセクハラの場合
	IF CFLAG:キャラ番号:現在位置 == FLAG:訪問者の現在位置 && FLAG:訪問者との行為 == 0
		SELECTCASE CFLAG:キャラ番号:NTRカウンターセクハラ
			CASE NTR_CNT_キス, NTR_CNT_ディープキス
				RETURNF 写真_NTR_キス
			CASE NTR_CNT_胸を揉ませる
				RETURNF 写真_NTR_胸愛撫
			CASE NTR_CNT_身体摺り寄せ, NTR_CNT_耳元に息, NTR_CNT_股間をまさぐる
				RETURNF 写真_NTR_接触
			CASE NTR_CNT_背面抱擁, NTR_CNT_対面抱擁
				RETURNF 写真_NTR_抱擁
		ENDSELECT
	ENDIF
		;訪問者との行為中でなければ通常の撮影
	SELECTCASE 部屋番号
		CASE GET_PRIVATE_ROOM(キャラ番号)
			IF CFLAG:(キャラ番号):睡眠
				RETURNF 写真_寝顔
			ELSEIF CFLAG:キャラ番号:自慰中
				RETURNF 写真_自慰
			ELSEIF RAND(2) == 1
				RETURNF 写真_着替え
			ENDIF
		CASE 場所_正門
			IF CSVNAME(NO:キャラ番号) == "紅美鈴" && RAND(3) == 1
				RETURNF 写真_居眠り
			ENDIF
		CASE 場所_図書室
			IF RAND(2) == 1
				RETURNF 写真_読書
			ENDIF
		CASE 場所_厨房
			RETURNF 写真_料理
		CASE 場所_大浴場
			RETURNF 写真_入浴
		CASE 場所_屋外トイレ,場所_1Fトイレ,場所_2Fトイレ
			RETURNF 写真_トイレ
		CASEELSE
			RETURNF 写真_普段
	ENDSELECT
RETURNF 写真_普段


;-------------------------------------------------------------------------------
;撮影した写真の内容取得
;@return 写真内容コード
@CHOOSE_PHOTO_DETAIL(キャラ番号, 部屋番号)
	#FUNCTION
	#DIM キャラ番号
	#DIM 部屋番号
	;訪問者と一緒で、何かしらの行為中でなければ詳細なし
	IF FLAG:訪問者との行為 == 0
		RETURNF 写真詳細_なし
	ENDIF
	IF CFLAG:キャラ番号:現在位置 != FLAG:訪問者の現在位置
		RETURNF 写真詳細_なし
	ENDIF

	IF FLAG:訪問者との行為の終了時刻 <= (DATETIME() + 3)
		RETURNF 写真詳細_絶頂
	ENDIF

	IF TCVAR:キャラ番号:快楽強度
		RETURNF 写真詳細_快楽
	ENDIF

	IF 70 < RAND( MAX(1, CFLAG:キャラ番号:性欲))
		RETURNF 写真詳細_おねだり
	ENDIF

	IF NTR_CHK_FAVORABLY(キャラ番号, FAV_寝取られそう)
		RETURNF 写真詳細_恋人
	ENDIF

RETURNF 写真詳細_なし


;-------------------------------------------------------------------------------
;撮影した写真の内容取得
;@return 写真内容コード
@CHOOSE_PHOTO2(キャラ番号,キャラ番号2,部屋番号)
	#FUNCTION
	#DIM キャラ番号
	#DIM キャラ番号2
	#DIM 部屋番号
	#DIM 行為

	行為 = CFLAG:キャラ番号:住人同士イベント内容 / 10
	SELECTCASE 行為
		CASE 住人同士行為_会話
			RETURNF 写真_住人同士_会話
		CASE 住人同士行為_お茶
			RETURNF 写真_住人同士_お茶
		CASE 住人同士行為_スキンシップ,住人同士行為_尻を撫でる
			RETURNF 写真_住人同士_接触
		CASE 住人同士行為_抱きつく
			RETURNF 写真_住人同士_抱擁
		CASE 住人同士行為_キスする
			RETURNF 写真_住人同士_キス
		CASE 住人同士行為_クリ愛撫,住人同士行為_指挿れ
			RETURNF 写真_住人同士_愛撫
		CASE 住人同士行為_アナル愛撫
			RETURNF 写真_住人同士_Ａ愛撫
		CASE 住人同士行為_胸愛撫
			RETURNF 写真_住人同士_胸愛撫
		CASE 住人同士行為_Ｖ性交Ｎ,住人同士行為_Ｖ性交Ｃ,住人同士行為_Ｖ後戯Ｎ,住人同士行為_Ｖ後戯Ｃ
			RETURNF 写真_住人同士_Ｖ性交
		CASE 住人同士行為_Ａ性交Ｎ,住人同士行為_Ａ性交Ｃ,住人同士行為_Ａ後戯Ｎ,住人同士行為_Ａ後戯Ｃ
			RETURNF 写真_住人同士_Ａ性交
	ENDSELECT
	;フェイルセーフ
RETURNF 写真_住人同士_会話
