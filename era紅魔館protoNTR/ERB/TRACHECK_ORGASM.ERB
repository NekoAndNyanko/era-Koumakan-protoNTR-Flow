;-------------------------------------------------------------------------------
;絶頂系の処理など
;-------------------------------------------------
;絶頂処理
;-------------------------------------------------
@ORGASM_ADD(奴隷,調教者=0)
	#DIM 奴隷
	#DIM 調教者
	#DIM 絶頂要求係数
	#DIM 総合快感値
	#DIM 部位快感値
	#DIM 絶頂候補
	#DIM Ａ絶頂強度
	#DIM Ｂ絶頂強度
	#DIM Ｃ絶頂強度
	#DIM Ｖ絶頂強度
	#DIM 浮気快楽強度
	
	絶頂候補 = 0
	絶頂候補 += ((CUP:奴隷:快Ｃ + PALAM:奴隷:快Ｃ) >= PALAMLV:4)
	絶頂候補 += ((CUP:奴隷:快Ｖ + PALAM:奴隷:快Ｖ) >= PALAMLV:4)
	絶頂候補 += ((CUP:奴隷:快Ａ + PALAM:奴隷:快Ａ) >= PALAMLV:4)
	絶頂候補 += ((CUP:奴隷:快Ｂ + PALAM:奴隷:快Ｂ) >= PALAMLV:4)
	SELECTCASE 絶頂候補
		CASE 4
			総合快感値 = 7000
		CASE 3
			総合快感値 = 5000
		CASE 2
			総合快感値 = 3000
		CASE 1
			総合快感値 = 2000
	ENDSELECT
	;視認性の問題で残しておく
	Ｃ絶頂強度 = 0
	Ｖ絶頂強度 = 0
	Ａ絶頂強度 = 0
	Ｂ絶頂強度 = 0
	
	;訪問者以外の調教で、快楽刻印よりも浮気快楽刻印のレベルが高い場合、絶頂しづらくなる
	IF 調教者 != 998 && GET_MARK_LEVEL(奴隷,刻印番号_快楽刻印,調教者) < MARK:奴隷:浮気快楽刻印
		絶頂要求係数 = MAX( 100 * MARK:奴隷:浮気快楽刻印 / MAX(1,GET_MARK_LEVEL(奴隷,刻印番号_快楽刻印,調教者)) , 100 )
	ELSE
		絶頂要求係数 = 100
	ENDIF
	
	;-------------------------------------------------------------------
	;絶頂Ｃ
	部位快感値 = (CUP:奴隷:快Ｃ > 0) * 総合快感値
	IF CUP:奴隷:快Ｃ + PALAM:奴隷:快Ｃ + 部位快感値 >= PALAMLV:4 * 2 * 絶頂要求係数 / 100
		Ｃ絶頂強度 = 2
		CDOWN:奴隷:快Ｃ = PALAMLV:4 * 2 - 部位快感値
	ELSEIF CUP:奴隷:快Ｃ + PALAM:奴隷:快Ｃ + 部位快感値 >= PALAMLV:4 * 絶頂要求係数 / 100
		Ｃ絶頂強度 = 1
		CDOWN:奴隷:快Ｃ = PALAMLV:4 - 部位快感値
	ELSEIF CUP:奴隷:快Ｃ + PALAM:奴隷:快Ｃ + 部位快感値 >= PALAMLV:4
		;イけるはずなのにイけなかった場合
		NOWEX:奴隷:絶頂演技++
	ENDIF
	;CDOWN:奴隷:快Ｃで下げても絶頂以上なら
	;その値-1になるように調整（10000で絶頂なら9999）
	SIF CUP:奴隷:快Ｃ + PALAM:奴隷:快Ｃ - CDOWN:奴隷:快Ｃ >= PALAMLV:4
		CDOWN:奴隷:快Ｃ = CUP:奴隷:快Ｃ + PALAM:奴隷:快Ｃ - PALAMLV:4 + 1
	;-------------------------------------------------------------------
	;絶頂Ｖ
	部位快感値 = (CUP:奴隷:快Ｖ > 0) * 総合快感値
	IF CUP:奴隷:快Ｖ + PALAM:奴隷:快Ｖ + 部位快感値 >= PALAMLV:4 * 2 * 絶頂要求係数 / 100
		Ｖ絶頂強度 = 2
		CDOWN:奴隷:快Ｖ = PALAMLV:4 * 2 - 部位快感値
	ELSEIF CUP:奴隷:快Ｖ + PALAM:奴隷:快Ｖ + 部位快感値 >= PALAMLV:4 * 絶頂要求係数 / 100
		Ｖ絶頂強度 = 1
		CDOWN:奴隷:快Ｖ = PALAMLV:4 - 部位快感値
	ELSEIF CUP:奴隷:快Ｖ + PALAM:奴隷:快Ｖ + 部位快感値 >= PALAMLV:4
		;イけるはずなのにイけなかった場合
		NOWEX:奴隷:絶頂演技++
	ENDIF
	;CDOWN:奴隷:快Ｖで下げても絶頂以上なら
	;その値-1になるように調整（10000で絶頂なら9999）
	SIF CUP:奴隷:快Ｖ + PALAM:奴隷:快Ｖ - CDOWN:奴隷:快Ｖ >= PALAMLV:4
		CDOWN:奴隷:快Ｖ = CUP:奴隷:快Ｖ + PALAM:奴隷:快Ｖ - PALAMLV:4 + 1
	;-------------------------------------------------------------------
	;絶頂Ａ
	部位快感値 = (CUP:奴隷:快Ａ > 0) * 総合快感値
	IF CUP:奴隷:快Ａ + PALAM:奴隷:快Ａ + 部位快感値 >= PALAMLV:4 * 2 * 絶頂要求係数 / 100
		Ａ絶頂強度 = 2
		CDOWN:奴隷:快Ａ = PALAMLV:4 * 2 - 部位快感値
	ELSEIF CUP:奴隷:快Ａ + PALAM:奴隷:快Ａ + 部位快感値 >= PALAMLV:4 * 絶頂要求係数 / 100
		Ａ絶頂強度 = 1
		CDOWN:奴隷:快Ａ = PALAMLV:4 - 部位快感値
	ELSEIF CUP:奴隷:快Ａ + PALAM:奴隷:快Ａ + 部位快感値 >= PALAMLV:4
		;イけるはずなのにイけなかった場合
		NOWEX:奴隷:絶頂演技++
	ENDIF
	;CDOWN:奴隷:快Ａで下げても絶頂以上なら
	;その値-1になるように調整（10000で絶頂なら9999）
	SIF CUP:奴隷:快Ａ + PALAM:奴隷:快Ａ - CDOWN:奴隷:快Ａ >= PALAMLV:4
		CDOWN:奴隷:快Ａ = CUP:奴隷:快Ａ + PALAM:奴隷:快Ａ - PALAMLV:4 + 1
	;-------------------------------------------------------------------
	;絶頂Ｂ
	部位快感値 = (CUP:奴隷:快Ｂ > 0) * 総合快感値
	IF CUP:奴隷:快Ｂ + PALAM:奴隷:快Ｂ + 部位快感値 >= PALAMLV:4 * 2 * 絶頂要求係数 / 100
		Ｂ絶頂強度 = 2
		CDOWN:奴隷:快Ｂ = (PALAMLV:4 * 2 - 1000) - 部位快感値
	ELSEIF CUP:奴隷:快Ｂ + PALAM:奴隷:快Ｂ + 部位快感値 >= PALAMLV:4 * 絶頂要求係数 / 100
		Ｂ絶頂強度 = 1
		CDOWN:奴隷:快Ｂ = (PALAMLV:4 - 1000) - 部位快感値
	ELSEIF CUP:奴隷:快Ｂ + PALAM:奴隷:快Ｂ + 部位快感値 >= PALAMLV:4
		;イけるはずなのにイけなかった場合
		NOWEX:奴隷:絶頂演技++
	ENDIF
	;CDOWN:奴隷:快Ｂで下げても絶頂以上なら
	;その値-1になるように調整（10000で絶頂なら9999）
	SIF CUP:奴隷:快Ｂ + PALAM:奴隷:快Ｂ - CDOWN:奴隷:快Ｂ >= PALAMLV:4
		CDOWN:奴隷:快Ｂ = CUP:奴隷:快Ｂ + PALAM:奴隷:快Ｂ - PALAMLV:4 + 1
	;-------------------------------------------------
	;絶頂時の追加処理
	;-------------------------------------------------
	;快楽刻印の処理
	;絶頂の部位と強度の合計が一定以上で刻印を取得する
	LOCAL = Ｃ絶頂強度 + Ｖ絶頂強度 * 2 + Ａ絶頂強度 * 2 + Ｂ絶頂強度
	;Rが4以上で快楽刻印３に相当
	IF LOCAL >= 3 && MARK:奴隷:快楽刻印 < 3
		TCVAR:奴隷:快楽強度 = 3
		;Rが3以上で快楽刻印２に相当
	ELSEIF LOCAL >= 2 && MARK:奴隷:快楽刻印 < 2
		TCVAR:奴隷:快楽強度 = 2
		;Rが2以上で快楽刻印１に相当
	ELSEIF LOCAL >= 1 && MARK:奴隷:快楽刻印 < 1
		TCVAR:奴隷:快楽強度 = 1
	ELSE
		TCVAR:奴隷:快楽強度 = 0
	ENDIF
	IF Ｃ絶頂強度 && Ｖ絶頂強度 && Ａ絶頂強度 && Ｂ絶頂強度
		Ｃ絶頂強度 *= 8
		Ｖ絶頂強度 *= 8
		Ａ絶頂強度 *= 8
		Ｂ絶頂強度 *= 8
		NOWEX:奴隷:四重絶頂 = 1
	ELSEIF Ｃ絶頂強度 && Ｖ絶頂強度 && Ａ絶頂強度
		Ｃ絶頂強度 *= 4
		Ｖ絶頂強度 *= 4
		Ａ絶頂強度 *= 4
		NOWEX:奴隷:三重絶頂 = 1
	ELSEIF Ｂ絶頂強度 && Ｖ絶頂強度 && Ａ絶頂強度
		Ｂ絶頂強度 *= 4
		Ｖ絶頂強度 *= 4
		Ａ絶頂強度 *= 4
		NOWEX:奴隷:三重絶頂 = 2
	ELSEIF Ｃ絶頂強度 && Ｂ絶頂強度 && Ａ絶頂強度
		Ｃ絶頂強度 *= 4
		Ｂ絶頂強度 *= 4
		Ａ絶頂強度 *= 4
		NOWEX:奴隷:三重絶頂 = 3
	ELSEIF Ｃ絶頂強度 && Ｖ絶頂強度 && Ｂ絶頂強度
		Ｃ絶頂強度 *= 4
		Ｖ絶頂強度 *= 4
		Ｂ絶頂強度 *= 4
		NOWEX:奴隷:三重絶頂 = 4
	ELSEIF Ｃ絶頂強度 && Ｖ絶頂強度
		Ｃ絶頂強度 *= 2
		Ｖ絶頂強度 *= 2
		NOWEX:奴隷:二重絶頂 = 1
	ELSEIF Ｃ絶頂強度 && Ａ絶頂強度
		Ｃ絶頂強度 *= 2
		Ａ絶頂強度 *= 2
		NOWEX:奴隷:二重絶頂 = 2
	ELSEIF Ｖ絶頂強度 && Ａ絶頂強度
		Ｖ絶頂強度 *= 2
		Ａ絶頂強度 *= 2
		NOWEX:奴隷:二重絶頂 = 3
	ELSEIF Ｃ絶頂強度 && Ｂ絶頂強度
		Ｃ絶頂強度 *= 2
		Ｂ絶頂強度 *= 2
		NOWEX:奴隷:二重絶頂 = 4
	ELSEIF Ｖ絶頂強度 && Ｂ絶頂強度
		Ｖ絶頂強度 *= 2
		Ｂ絶頂強度 *= 2
		NOWEX:奴隷:二重絶頂 = 5
	ELSEIF Ａ絶頂強度 && Ｂ絶頂強度
		Ａ絶頂強度 *= 2
		Ｂ絶頂強度 *= 2
		NOWEX:奴隷:二重絶頂 = 6
	ENDIF
	IF Ｃ絶頂強度
		SOURCE:奴隷:露出 += 500 * Ｃ絶頂強度
		SOURCE:奴隷:屈従 += 200 * Ｃ絶頂強度
		SOURCE:奴隷:反感 += 150 * Ｃ絶頂強度
		DOWNBASE:奴隷:体力 += 20
		DOWNBASE:奴隷:気力 += 10
	ENDIF
	IF Ｖ絶頂強度
		SOURCE:奴隷:欲情 += 800 * Ｖ絶頂強度
		SOURCE:奴隷:恭順 += 500 * Ｖ絶頂強度
		SOURCE:奴隷:露出 += 700 * Ｖ絶頂強度
		SOURCE:奴隷:屈従 += 400 * Ｖ絶頂強度
		SOURCE:奴隷:反感 += 300 * Ｖ絶頂強度
		DOWNBASE:奴隷:体力 += 40
		DOWNBASE:奴隷:気力 += 20
	ENDIF
	IF Ａ絶頂強度
		SOURCE:奴隷:欲情 += 500 * Ａ絶頂強度
		SOURCE:奴隷:恭順 += 700 * Ａ絶頂強度
		SOURCE:奴隷:露出 += 900 * Ａ絶頂強度
		SOURCE:奴隷:屈従 += 500 * Ａ絶頂強度
		SOURCE:奴隷:反感 += 400 * Ａ絶頂強度
		DOWNBASE:奴隷:体力 += 60
		DOWNBASE:奴隷:気力 += 30
	ENDIF
	IF Ｂ絶頂強度
		SOURCE:奴隷:露出 += 500 * Ｂ絶頂強度
		SOURCE:奴隷:屈従 += 200 * Ｂ絶頂強度
		SOURCE:奴隷:反感 += 150 * Ｂ絶頂強度
		DOWNBASE:奴隷:体力 += 20
		DOWNBASE:奴隷:気力 += 10
	ENDIF
	;NOWEXにデータを入れる（絶頂時口上に使う）
	NOWEX:奴隷:Ｃ絶頂 = Ｃ絶頂強度
	NOWEX:奴隷:Ｖ絶頂 = Ｖ絶頂強度
	NOWEX:奴隷:Ａ絶頂 = Ａ絶頂強度
	NOWEX:奴隷:Ｂ絶頂 = Ｂ絶頂強度
	;絶頂回数を増やす
	EX:奴隷:Ｃ絶頂 += Ｃ絶頂強度
	EX:奴隷:Ｖ絶頂 += Ｖ絶頂強度
	EX:奴隷:Ａ絶頂 += Ａ絶頂強度
	EX:奴隷:Ｂ絶頂 += Ｂ絶頂強度
	;絶頂経験を増
	EXP:奴隷:絶頂経験 += Ｃ絶頂強度 + Ｖ絶頂強度 + Ａ絶頂強度 + Ｂ絶頂強度
	EXP:奴隷:Ｃ絶頂経験 += Ｃ絶頂強度
	EXP:奴隷:Ｖ絶頂経験 += Ｖ絶頂強度
	EXP:奴隷:Ａ絶頂経験 += Ａ絶頂強度
	EXP:奴隷:Ｂ絶頂経験 += Ｂ絶頂強度
	;ビデオ
	IF TEQUIP:奴隷:ビデオ撮影
		IF NOWEX:奴隷:四重絶頂
			LOCALS = O2/O3/O4/
			TSTR:動画撮影記録 += LOCALS
		ENDIF
		IF NOWEX:奴隷:三重絶頂
			LOCALS = O2/O3/
			TSTR:動画撮影記録 += LOCALS
		ENDIF
		IF NOWEX:奴隷:二重絶頂
			LOCALS = O2/
			TSTR:動画撮影記録 += LOCALS
		ENDIF
		IF NOWEX:奴隷:Ｃ絶頂 == 2
			LOCALS = C/C/
			TSTR:動画撮影記録 += LOCALS
		ELSEIF NOWEX:奴隷:Ｃ絶頂 == 1
			LOCALS = C/
			TSTR:動画撮影記録 += LOCALS
		ENDIF
		IF NOWEX:奴隷:Ｖ絶頂 == 2
			LOCALS = V/V/
			TSTR:動画撮影記録 += LOCALS
		ELSEIF NOWEX:奴隷:Ｖ絶頂 == 1
			LOCALS = V/
			TSTR:動画撮影記録 += LOCALS
		ENDIF
		IF NOWEX:奴隷:Ａ絶頂 == 2
			LOCALS = A/A/
			TSTR:動画撮影記録 += LOCALS
		ELSEIF NOWEX:奴隷:Ａ絶頂 == 1
			LOCALS = A/
			TSTR:動画撮影記録 += LOCALS
		ENDIF
		IF NOWEX:奴隷:Ｂ絶頂 == 2
			LOCALS = B/B/
			TSTR:動画撮影記録 += LOCALS
		ELSEIF NOWEX:奴隷:Ｂ絶頂 == 1
			LOCALS = B/
			TSTR:動画撮影記録 += LOCALS
		ENDIF
	ENDIF

RETURN LOCAL

;-------------------------------------------------------------------------------
;FIXME:関数の説明を書いてください
;@return 1:xxx,0:xxx
@MESSAGE_PALAMCNG_B2
	IF NOWEX:6
		IF !GETBIT(CFLAG:3,2)
			EXP:異常経験 ++
			SETBIT CFLAG:異常経験,2
		ENDIF
		PRINTFORML %"四 重 絶 頂"%
		PRINTFORML %"(それぞれ8倍の珠が得られます)"%
	ELSEIF NOWEX:5
		SELECTCASE NOWEX:5
			CASE 1
				PRINTFORML %"Ｃ＆Ｖ＆Ａ絶頂"%
				PRINTFORML %"(それぞれ4倍の珠が得られます)"%
			CASE 2
				PRINTFORML %"Ｂ＆Ｖ＆Ａ絶頂"%
				PRINTFORML %"(それぞれ4倍の珠が得られます)"%
			CASE 3
				PRINTFORML %"Ｃ＆Ｂ＆Ａ絶頂"%
				PRINTFORML %"(それぞれ4倍の珠が得られます)"%
			CASE 4
				PRINTFORML %"Ｃ＆Ｖ＆Ｂ絶頂"%
				PRINTFORML %"(それぞれ4倍の珠が得られます)"%
		ENDSELECT
	ELSEIF NOWEX:4
		SELECTCASE NOWEX:4
			CASE 1
				PRINTFORML %"Ｃ＆Ｖ絶頂"%
				PRINTFORML %"(それぞれ2倍の珠が得られます)"%
			CASE 2
				PRINTFORML %"Ｃ＆Ａ絶頂"%
				PRINTFORML %"(それぞれ2倍の珠が得られます)"%
			CASE 3
				PRINTFORML %"Ｖ＆Ａ絶頂"%
				PRINTFORML %"(それぞれ2倍の珠が得られます)"%
			CASE 4
				PRINTFORML %"Ｃ＆Ｂ絶頂"%
				PRINTFORML %"(それぞれ2倍の珠が得られます)"%
			CASE 5
				PRINTFORML %"Ｖ＆Ｂ絶頂"%
				PRINTFORML %"(それぞれ2倍の珠が得られます)"%
			CASE 6
				PRINTFORML %"Ａ＆Ｂ絶頂"%
				PRINTFORML %"(それぞれ2倍の珠が得られます)"%
		ENDSELECT
	ELSEIF NOWEX:3
		IF NOWEX:3 == 2
			PRINTFORML %"強絶頂Ｂ"%
		ELSE
			PRINTFORML %"絶頂Ｂ"%
		ENDIF
	ELSEIF NOWEX:2
		IF NOWEX:2 == 2
			PRINTFORML %"強絶頂Ａ"%
		ELSE
			PRINTFORML %"絶頂Ａ"%
		ENDIF
	ELSEIF NOWEX:1
		IF NOWEX:1 == 2
			PRINTFORML %"強絶頂Ｖ"%
		ELSE
			PRINTFORML %"絶頂Ｖ"%
		ENDIF
	ELSEIF NOWEX:0
		IF NOWEX:0 == 2
			PRINTFORML %"強絶頂Ｃ"%
		ELSE
			PRINTFORML %"絶頂Ｃ"%
		ENDIF
	ENDIF
	;絶頂による欲望ＬＶアップ
	LOCAL = 0
	SIF NOWEX:0 >= 2 || NOWEX:Ｂ絶頂 >= 2 || NOWEX:Ｖ絶頂 || NOWEX:Ａ絶頂
		LOCAL = 1
	SIF NOWEX:1 >= 2 || NOWEX:Ａ絶頂 >= 2
		LOCAL = 2
	SIF (NOWEX:0 >= 2 || NOWEX:Ｂ絶頂 >= 2) && NOWEX:4
		LOCAL = 2
	SIF NOWEX:5
		LOCAL = 3
	SIF NOWEX:6
		LOCAL = 4
	;自制心があると１下がる
	SIF TALENT:自制心
		LOCAL -= 1
	IF ABL:欲望 < LOCAL
		ABL:欲望 ++
		PRINTFORML そして欲望がLV{LOCAL}になった
		;欲望の上昇による[抑圧]の消滅をチェック
		CALL YOKUBO_UP_CHECK
	ENDIF
	PRINTL 

;-------------------------------------------------
;調教対象の噴乳チェック
;-------------------------------------------------
@TARGET_MILK_CHECK(奴隷)
	#DIM 奴隷
	#DIM 母乳蓄積基準値
	#DIM 母乳噴出量
	#DIM 母乳蓄積量

	SIF !TALENT:奴隷:母乳体質
		RETURN 0

	;場合によってはMAXBASE未設定のまま来る事があるので0なら強制設定する
	IF MAXBASE:奴隷:母乳 == 0
		MAXBASE:奴隷:母乳 = 5000+5000*(!TALENT:奴隷:淫乳)
	ENDIF

	;快感の上昇量から母乳蓄積基準値を決定
	母乳蓄積基準値 = CUP:奴隷:快Ｃ/5 + CUP:奴隷:快Ｖ/5 + CUP:奴隷:快Ａ/5 + CUP:奴隷:快Ｂ*3

	;蓄積基準値が0、つまり刺激がなければ突然噴き出したりはしない
	IF (母乳蓄積基準値 == 0)
		RETURN 0
	ENDIF

	;最大でも150%しか増えないようにする（大量噴乳を減らす）
	母乳蓄積量 = MIN(母乳蓄積基準値,MAXBASE:奴隷:母乳*3/2)

	;自制心持ちなら母乳が貯まりづらい
	SIF TALENT:奴隷:自制心
		母乳蓄積量 /= 2
	;快感に素直なら母乳が貯まりやすい
	SIF TALENT:奴隷:快感応答 > 0
		TIMES 母乳蓄積量 , 1.20
	;快感の否定なら母乳が貯まりづらい（淫乱、娼婦で無効）
	SIF TALENT:奴隷:快感応答 < 0 && !TALENT:奴隷:淫乱
		TIMES 母乳蓄積量 , 0.80
	;B敏感なら母乳が貯まりやすい
	SIF TALENT:奴隷:Ｂ感度 > 0
		TIMES 母乳蓄積量 , 1.20
	;媚薬状態なら母乳が貯まりやすい
	SIF TEQUIP:奴隷:媚薬
		母乳蓄積量 *= 2
	;利尿剤状態なら母乳が貯まりづらい（水分が膀胱に回るから？）
	SIF TEQUIP:奴隷:利尿剤
		母乳蓄積量 /= 2
	;調教者が幼児退行なら母乳が貯まりやすい
	SIF TALENT:PLAYER:幼児／幼児退行
		TIMES 母乳蓄積量 , 1.20
	;調教者が幼稚なら母乳が貯まりやすい
	SIF TALENT:PLAYER:幼稚
		TIMES 母乳蓄積量 , 1.20
	;貧乳なら母乳が貯まりづらい
	SIF TALENT:奴隷:バストサイズ < 0
		TIMES 母乳蓄積量 , 0.50

	BASE:奴隷:母乳 += 母乳蓄積量
	
	;噴出チェック、100%以上貯まっていれば必ず、そうでなければ量と今回受けた刺激に応じて噴出率が変わる
	IF (BASE:奴隷:母乳 > MAXBASE:奴隷:母乳)
		;噴出後の母乳量が50%未満になるようにする(連続噴出防止)
		母乳噴出量 = MAX( RAND(BASE:奴隷:母乳), (BASE:奴隷:母乳-MAXBASE:奴隷:母乳/2) )
	ELSEIF 	( RAND(MAX(CUP:奴隷:快Ｂ,1)) > (MAXBASE:奴隷:母乳-BASE:奴隷:母乳) )
		;100%超えていない場合は量は知れてる
		母乳噴出量 = MAX(RAND(BASE:奴隷:母乳),0)
	ENDIF
	母乳噴出量 = MIN(母乳噴出量, BASE:奴隷:母乳)

	IF 母乳噴出量 < 1
		RETURN 0
	ENDIF

	DOWNBASE:奴隷:体力 += 20 * MAX(母乳噴出量 / MAXBASE:奴隷:母乳, 1)
	DOWNBASE:奴隷:気力 += 50 * MAX(母乳噴出量 / MAXBASE:奴隷:母乳, 1)

	IF EXP:奴隷:噴乳経験 < EXPLV:1
		SOURCE:奴隷:露出 += MAX(10000 * 母乳噴出量 / MAXBASE:奴隷:母乳, 1)
		SOURCE:奴隷:屈従 += MAX( 5000 * 母乳噴出量 / MAXBASE:奴隷:母乳, 1)
		SOURCE:奴隷:不潔 += MAX( 2000 * 母乳噴出量 / MAXBASE:奴隷:母乳, 1)
	ELSEIF EXP:奴隷:噴乳経験 < EXPLV:2
		SOURCE:奴隷:露出 += MAX( 5000 * 母乳噴出量 / MAXBASE:奴隷:母乳, 1)
		SOURCE:奴隷:屈従 += MAX( 3000 * 母乳噴出量 / MAXBASE:奴隷:母乳, 1)
		SOURCE:奴隷:不潔 += MAX( 1500 * 母乳噴出量 / MAXBASE:奴隷:母乳, 1)
	ELSEIF EXP:奴隷:噴乳経験 < EXPLV:3
		SOURCE:奴隷:露出 += MAX( 3000 * 母乳噴出量 / MAXBASE:奴隷:母乳, 1)
		SOURCE:奴隷:屈従 += MAX( 1500 * 母乳噴出量 / MAXBASE:奴隷:母乳, 1)
		SOURCE:奴隷:不潔 += MAX( 1000 * 母乳噴出量 / MAXBASE:奴隷:母乳, 1)
	ELSEIF EXP:奴隷:噴乳経験 < EXPLV:4
		SOURCE:奴隷:露出 += MAX( 1500 * 母乳噴出量 / MAXBASE:奴隷:母乳, 1)
		SOURCE:奴隷:屈従 += MAX(  800 * 母乳噴出量 / MAXBASE:奴隷:母乳, 1)
		SOURCE:奴隷:不潔 += MAX(  700 * 母乳噴出量 / MAXBASE:奴隷:母乳, 1)
	ELSEIF EXP:奴隷:噴乳経験 < EXPLV:5
		SOURCE:奴隷:露出 += MAX( 1000 * 母乳噴出量 / MAXBASE:奴隷:母乳, 1)
		SOURCE:奴隷:屈従 += MAX(  500 * 母乳噴出量 / MAXBASE:奴隷:母乳, 1)
		SOURCE:奴隷:不潔 += MAX(  400 * 母乳噴出量 / MAXBASE:奴隷:母乳, 1)
	ELSE
		SOURCE:奴隷:露出 += MAX(  500 * 母乳噴出量 / MAXBASE:奴隷:母乳, 1)
		SOURCE:奴隷:屈従 += MAX(  300 * 母乳噴出量 / MAXBASE:奴隷:母乳, 1)
	ENDIF

	SELECTCASE 母乳噴出量
		CASE IS > MAXBASE:奴隷:母乳
			PRINTFORML %CALLNAME:奴隷+"の乳首から大量の母乳が噴き出した"%
			EXP:奴隷:異常経験 += (EXP:奴隷:噴乳経験==0)
			EXP:奴隷:噴乳経験 += 3
			NOWEX:奴隷:噴乳 += 3
			EX:奴隷:噴乳 += 3
		CASE IS > (MAXBASE:奴隷:母乳/2)
			PRINTFORML %CALLNAME:奴隷+"の乳首から母乳が噴き出した"%
			EXP:奴隷:異常経験 += (EXP:奴隷:噴乳経験==0)
			EXP:奴隷:噴乳経験 += 2
			NOWEX:奴隷:噴乳 += 2
			EX:奴隷:噴乳 += 2
		CASE IS > (MAXBASE:奴隷:母乳/4)
			;125%以上貯まっていた
			PRINTFORML %CALLNAME:奴隷+"の乳首から母乳が溢れ出た"%
			EXP:奴隷:異常経験 += (EXP:奴隷:噴乳経験==0)
			EXP:奴隷:噴乳経験 += 1
			NOWEX:奴隷:噴乳 += 1
			EX:奴隷:噴乳 += 1
		CASEELSE
			PRINTFORML %CALLNAME:奴隷+"の乳首から母乳が滲み出た"%
	ENDSELECT

	;Ｂに母乳汚れを付ける
	STAIN:奴隷:Ｂ |= 汚れ_母乳

	;母乳を減らす
	BASE:奴隷:母乳 -= 母乳噴出量
	;万一の為に最大母乳-1になるように調整
	BASE:奴隷:母乳 = MIN(BASE:奴隷:母乳, MAXBASE:奴隷:母乳 - 1)

	;ビデオ撮影判定
	IF TEQUIP:奴隷:ビデオ撮影
		IF NOWEX:奴隷:噴乳 == 1
			LOCALS = MILK/
		ELSEIF NOWEX:奴隷:噴乳 == 2
			LOCALS = MILK/MILK/
		ENDIF
		TSTR:動画撮影記録 += LOCALS
	ENDIF

	WAIT
