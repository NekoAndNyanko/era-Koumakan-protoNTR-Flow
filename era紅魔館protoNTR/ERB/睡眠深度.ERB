
@行為騒音, ARG
;行為
SELECTCASE SELECTCOM
	CASE 20
		LOCAL = 10
	CASE 21
		LOCAL = 0
	CASE 40 TO 48
		LOCAL = 25
	CASE 60 TO 72, 92 TO 94, 97 TO 99 
		LOCAL = 15
	CASE 100, 101
		LOCAL = 100
	CASEELSE
		LOCAL = 10
ENDSELECT

;喘ぎ
LOCAL:1 = MAX(0, SOURCE:ARG:快Ｃ, SOURCE:ARG:快Ｖ, SOURCE:ARG:快Ａ, SOURCE:ARG:快Ｂ)
IF LOCAL:1
	;絶頂
	;4重
	IF (NOWEX:Ｂ絶頂 + NOWEX:Ａ絶頂 + NOWEX:Ｖ絶頂 + NOWEX:Ｃ絶頂) >= 8
		TIMES LOCAL:1, 3.50
	;3重
	ELSEIF (NOWEX:Ｂ絶頂 + NOWEX:Ａ絶頂 + NOWEX:Ｖ絶頂 + NOWEX:Ｃ絶頂) >= 6
		TIMES LOCAL:1, 2.75
	;2重
	ELSEIF (NOWEX:Ｂ絶頂 + NOWEX:Ａ絶頂 + NOWEX:Ｖ絶頂 + NOWEX:Ｃ絶頂) >= 4
		TIMES LOCAL:1, 2.15
	;1重強
	ELSEIF (NOWEX:Ｂ絶頂 + NOWEX:Ａ絶頂 + NOWEX:Ｖ絶頂 + NOWEX:Ｃ絶頂) >= 2
		TIMES LOCAL:1, 1.65
	;通常
	ELSEIF (NOWEX:Ｂ絶頂 + NOWEX:Ａ絶頂 + NOWEX:Ｖ絶頂 + NOWEX:Ｃ絶頂) >= 1
		TIMES LOCAL:1, 1.35
	ENDIF
	SIF !TALENT:ARG:淫乱
		TIMES LOCAL:1, 0.55
	LOCAL:1 /= 100
ENDIF
TFLAG:行為騒音 = LOCAL + LOCAL:1

;PRINTFORML {SOURCE:ARG:快Ｃ}, {SOURCE:ARG:快Ｖ}, {SOURCE:ARG:快Ａ}, {SOURCE:ARG:快Ｂ}
;PRINTFORML {NOWEX:6}, {NOWEX:5}, {NOWEX:4}, {NOWEX:3}, {NOWEX:2}, {NOWEX:1}, {NOWEX:0}
;PRINTFORML {LOCAL}, {LOCAL:1} {CFLAG:MASTER:うふふ}

@特殊起床
FOR LOCAL, 1, CHARANUM
	SIF CFLAG:MASTER:現在位置 != CFLAG:LOCAL:現在位置
		CONTINUE
	SIF !CFLAG:LOCAL:睡眠
		CONTINUE
	CFLAG:LOCAL:睡眠深度 += RAND(1, 10)
	SIF !TFLAG:行為騒音
		CONTINUE
	CFLAG:LOCAL:睡眠深度 = MAX(CFLAG:LOCAL:睡眠深度 - TFLAG:行為騒音, 0)
	
	IF CFLAG:MASTER:うふふ
		;起床判定
		IF CFLAG:LOCAL:睡眠深度 <= 200 && CFLAG:LOCAL:睡眠深度 > 100
			IF TFLAG:行為騒音 >= RAND(50, 100)
				SIF CFLAG:MASTER:うふふ == 1
					CALL うふふ中起床口上, LOCAL
			ENDIF
		ELSEIF CFLAG:LOCAL:睡眠深度 <= 100
			IF TFLAG:行為騒音 >= RAND:100
				SIF CFLAG:MASTER:うふふ == 1
					CALL うふふ中起床口上, LOCAL
			ENDIF
		ELSEIF CFLAG:LOCAL:睡眠深度 == 0
			SIF CFLAG:MASTER:うふふ == 1
				CALL うふふ中起床口上, LOCAL
		ENDIF
		
		IF CFLAG:LOCAL:睡眠
			PRINTFORM %CALLNAME:LOCAL%は
			IF CFLAG:LOCAL:睡眠深度 > 2500
				PRINTW 泥のように眠っている
			ELSEIF CFLAG:LOCAL:睡眠深度 > 800
				PRINTW 深い寝息をたてている
			ELSEIF CFLAG:LOCAL:睡眠深度 > 500
				PRINTW すやすやと眠っている
			ELSEIF CFLAG:LOCAL:睡眠深度 > 200
				PRINTW 寝返りをうっている
			ELSE
				PRINTL 小さく寝言を呟いた
				PRINTW これ以上うるさくしたら起きるかもしれない
			ENDIF
		ENDIF
	ENDIF
NEXT

@うふふ中起床口上, ARG
;ARG 起きたキャラ
PRINTFORML 物音に気付いて%CALLNAME:ARG%が起きてしまった！
PRINTW
;キャラ別取得変更
IF (TALENT:ARG:淫乱 || TALENT:ARG:親愛)
	;INFO.ERBの情事発覚処理との兼ね合いがうまくいかないので現状は追い出されない程度に
	;CALL うふふ中起床_関心, ARG
	RESULT = 3
ELSE
	SELECTCASE NO:ARG
		CASE 3
			IF TALENT:ARG:恋慕
				CALL うふふ中起床_怒り, ARG
			ELSE
				CALL うふふ中起床_無関心, ARG
			ENDIF
		CASE 5, 6
			CALL うふふ中起床_怒り, ARG
		CASE 148, 149
			IF CFLAG:ARG:成長度 <= 成長度_乳児
				PRINTFORML %CALLNAME:ARG%は泣き出した
				RESULT = 3
			ELSEIF TALENT:ARG:幼稚
				CALL うふふ中起床_無知, ARG
			ELSE
				CALL うふふ中起床_怒り, ARG
			ENDIF
		CASEELSE
			;ARGが身分上
			IF CFLAG:ARG:館内地位 > CFLAG:TARGET:館内地位
				CALL うふふ中起床_怒り, ARG
			ELSE
				CALL うふふ中起床_困惑, ARG
			ENDIF
	ENDSELECT
ENDIF
;RESULT 4= 起床キャラがうふふに加入
;		3以下 うふふ終了
;		3= パラメータ変化無し
;		2以下 追い出し
;		2= 傷心取得
;		1= 怒り取得
;		0= パラメータ変化無し
IF RESULT <= 3
	PRINTFORMW %CALLNAME:ARG%が起きてしまったので行為を終えます
ELSE
	WAIT
ENDIF

IF RESULT <= 2
	PRINTFORMW %CALLNAME:MASTER%達は追い出されてしまった
	;追い出し処理
	FOR LOCAL, 0, CHARANUM
		SIF !CFLAG:ARG:うふふ
			CONTINUE
		FOR LOCAL:1,0,MAXROOM()
			;庭は除外
			SIF LOCAL:1 == 場所_庭
				CONTINUE
			IF CAN_MOVE(CFLAG:LOCAL:現在位置, LOCAL:1)
				CFLAG:LOCAL:現在位置 = LOCAL:1
				BREAK
			ENDIF
		NEXT
	NEXT
ENDIF
;加入かリセット
IF RESULT == 4
	CFLAG:ARG:うふふ = CFLAG:MASTER:うふふ
ELSE
	;うふふリセット
	CVARSET CFLAG, 317
ENDIF

;好感度減少
IF (RESULT == 1 || RESULT == 2)
	PRINTFORMW 好感度(%CALLNAME:ARG%) -{CFLAG:ARG:好感度/10}
	CFLAG:ARG:好感度 -= (CFLAG:ARG:好感度/10)
ENDIF
IF RESULT == 1
	BASE:ARG:怒り = MAXBASE:ARG:怒り
ELSEIF RESULT == 2
	CALL 素質傷心取得, ARG
ENDIF
CFLAG:ARG:睡眠 = 0
CFLAG:ARG:睡眠深度 = 0

@うふふ中起床_MASTER状態, ARG
SELECTCASE SELECTCOM
	CASE 60 TO 72, 92 TO 94, 97 TO 99
		;繋がっている
		PRINTFORM 目の前で繋がった%CALLNAME:MASTER%達を見た
	CASE 0, 3, 5, 6, 7
		;行為：愛撫系
		PRINTFORM 目の前で%CALLNAME:TARGET%を愛撫する%CALLNAME:MASTER%の姿を見た
	CASE 1, 2
		;舐め
		PRINTFORM 目の前で%CALLNAME:TARGET%の股に顔を埋める%CALLNAME:MASTER%の姿を見た
	CASEELSE
		;その他
		PRINTFORM 目の前で起きている痴態に
ENDSELECT
PRINTFORML 



@うふふ中起床_怒り, ARG
CALL うふふ中起床_MASTER状態, ARG
PRINTFORML %CALLNAME:ARG%は%CALLNAME:MASTER%達の逸脱した行為を咎めた
SIF TALENT:ARG:恋慕 && !TALENT:ARG:親愛 && !TALENT:ARG:淫乱 && !TALENT:ARG:NTR && !TALENT:ARG:公衆便所
	RETURN 2
RETURN 1

@うふふ中起床_無知, ARG
CALL うふふ中起床_MASTER状態, ARG
PRINTFORML %CALLNAME:ARG%は頭に疑問符が浮かぶような顔をしている
PRINTFORML どうやらこの状況がいまいち理解できていないようだ
RETURN 3

@うふふ中起床_関心, ARG
PRINTFORML %CALLNAME:ARG%は負けじと%CALLNAME:MASTER%に体を摺り寄せて行為に混ざった
RETURN 4

@うふふ中起床_無関心, ARG
PRINTFORML %CALLNAME:ARG%は冷静な態度で%CALLNAME:MASTER%達に行為を中止し出て行くように伝えた
RETURN 0

@うふふ中起床_困惑, ARG
PRINTFORML %CALLNAME:ARG%は戸惑っている
SIF TALENT:ARG:恋慕 && !TALENT:ARG:親愛 && !TALENT:ARG:淫乱 && !TALENT:ARG:NTR && !TALENT:ARG:公衆便所
	RETURN 2
RETURN 1

;素質「傷心」取得処理
@素質傷心取得(奴隷, 取得方法, 浮気相手)
	#DIM 奴隷
	#DIM 取得方法	; 0:浮気現場目撃
	#DIM 浮気相手	; 浮気現場目撃時の相手

	IF 取得方法 == 0	; 浮気現場目撃
		PRINTFORML %CALLNAME:奴隷%は愛する%CALLNAME:MASTER%の浮気現場を見てしまい心を痛めてしまった…
		PRINTFORMW 好感度(%CALLNAME:奴隷%) -{CFLAG:奴隷:好感度/10}
		CFLAG:奴隷:好感度 -= (CFLAG:奴隷:好感度/10)
		TALENT:奴隷:傷心 += 1000
		IF CFLAG:奴隷:傷心相手1 == 0
			CFLAG:奴隷:傷心相手1 = 浮気相手
			CFLAG:奴隷:傷心相手2 = 0
		ELSE
			CFLAG:奴隷:傷心相手2 = 浮気相手
		ENDIF
	ENDIF

