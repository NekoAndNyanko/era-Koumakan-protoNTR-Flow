;-------------------------------------------------------------------------------
;妖精を探す - 実行可否判定
; RETURN 0 コマンドの実行不可
; RETURN 1 コマンドの実行可能
@COM_ABLE429
	;一括管理分
	IF !TFLAG:100
		RETURN 0
	ELSEIF GLOBAL_COMABLE(429)
		RETURN RESULT
	ELSEIF CFLAG:MASTER:うふふ == 2
		RETURN 0
	ELSEIF !FairyMaids_hasVacancy()
		RETURN 0
	ELSEIF CFLAG:MASTER:現在位置 > 場所番号最大値
		RETURN 1
	ENDIF
RETURN 0

;-------------------------------------------------------------------------------
;妖精を探す
; RETURN 0 コマンドの実行不可
; RETURN 1 コマンドの実行可能
@COM429
	#DIM 妖精番号
	CALL COM_ABLE429()
	IF !RESULT:0
		RETURN 0
	ENDIF
	TIME:0 += 10
	REUSELASTLINE %CALLNAME:MASTER+"は辺りを探し回った……"%
	WAIT
	PRINTFORML %CALLNAME:MASTER+"は辺りを探し回った……妖精を見つけた！"%
	
	妖精番号 = FairyMaids_HiredCount()
	CALL FairyMaids_Create(妖精番号)
	CALL FairyMaids_PrintLook(妖精番号)
	
	PRINTFORML %"この妖精を勧誘しますか？"%
	PRINTFORML %"[ 1]-勧誘する"%
	PRINTFORML %"[ 0]-そっとしておく"%
	$COM429_INPUT_LOOP
	INPUT
	SELECTCASE RESULT:0
		CASE 0
			PRINTFORML %CALLNAME:MASTER+"は妖精に気付かれないように、そっとその場を後にした……"%
		CASE 1
			;緊急リリースにつき超手抜き
			IF FairyMaids_hasVacancy()
				PRINTFORML %CALLNAME:MASTER+"が声をかけると、妖精はあっさりとメイドになることを承諾した……"%
				CALLF FairyMaids_Employ(妖精番号)
			ELSE
				PRINTFORML %"妖精を連れて帰る前に、許可を貰っておかないと後が怖そうだ……"%
			ENDIF
		CASEELSE
			CLEARLINE 1
			GOTO COM429_INPUT_LOOP
	ENDSELECT
RETURN 0
