;-------------------------------------------------------------------------------
;妖精メイドと話す(拡張版) - 実行可否判定
; RETURN 0 コマンドの実行不可
; RETURN 1 コマンドの実行可能
@COM_ABLE420
	;移動実行判定
	SIF !TFLAG:100
		RETURN 0
	;一括管理
	SIF GLOBAL_COMABLE(420)
		RETURN RESULT
	;詰所
	SIF CFLAG:MASTER:現在位置 != 場所_妖精メイド詰め所
		RETURN 0
	SIF CFLAG:うふふ == 2
		RETURN 0
RETURN 1

;-------------------------------------------------
;妖精メイドと話す(拡張版)
; RETURN 0 コマンドの実行に失敗
; RETURN 1 コマンドの実行に成功
@COM420
	#DIMS コマンド,100
	#DIM コマンド数 = 0
	コマンド数 = 0
	FOR LOCAL:0,0,100
		TRYCCALLFORM %"COM420_"+TOSTR(LOCAL:0,"00")+"_ENABLE"%
			IF RESULT:0
				コマンド:コマンド数 = %RESULTS:0%
				コマンド数++
			ENDIF
		CATCH
		ENDCATCH
	NEXT
	;直接番号叩かれたら呼べるけど、まあ妥協。
	IF (コマンド数 > 0)
		PRINTDATAL
			DATAFORM %"妖精メイド「何かごようですか？」"%
			DATAFORM %"妖精メイド「今日もがんばりましょう！」"%
			DATAFORM %"妖精メイド「サボってるとメイド長に怒られちゃいますよ？」"%
		ENDDATA
		FOR LOCAL,0,コマンド数
			PRINTFORML %コマンド:LOCAL%
		NEXT
		PRINTFORML %"[ 0]-やっぱりやめる"%
		$COM420_INPUT_LOOP
		INPUT
		TRYCCALLFORM COM420_%TOSTR(RESULT:0,"00")%
			RETURN RESULT:0
		CATCH
			CLEARLINE 1
			GOTO COM420_INPUT_LOOP
		ENDCATCH
	ELSE
		CALL COM420_01
		RETURN RESULT:0
	ENDIF
RETURN 0
