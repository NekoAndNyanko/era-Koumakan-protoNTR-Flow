
;−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−
;紅魔館敷地内
@MAP_PRINT_0

PRINTL 〜紅魔館〜
PRINTL
;・マップ作成
;CALL MAP_SETING, "■■■■■▲▲▲■■■■■■■■ ", -1, 場所_正門, 場所_広間
;　　　　　　　　　　　↑表示されるマップ　　　　　　　↑　　　↑マップ横のボタン、最高５つまで、省略可
;　　　　　　　　　　　　　　　　　　　　　　　　　　　｜
;　　　　　　　　　　　　　　　　　　　　　　　　　色コード値(下で説明)

;・マップ内ボタン作成
;CALL MAP_SETING, "１/■■■■■/16/▲▲▲■■■/５/■■■■■ ", -1, 場所_正門, 場所_広間
;マップ内のボタンとなる数値の左右を"/"で囲う、末端の場合は端ではない方のみ"/"を置く
;"/"で囲われた数字がボタンになる

;・部分カラー指定
;CALL MAP_SETING, "１/■■■■■/16/#FF8000=▲▲▲/■■■/５/■■■■■ ", -1, 場所_正門, 場所_広間
;色を変えたい文字(記号)の右端に"#(16進数色コード)="で色を指定できる、色コードの英字は大文字小文字どちらでも可
;指定すると次の"/"までの文字が指定した色に変わる、上の場合▲▲▲の部分が違う色で表示される
;"#resetC="でEmueraに設定している基本の文字色に設定できる
;ボタンの色は変えられない

;・色コード値
;CALL MAP_SETING, "１/■■■■■/16/#FF8000=▲▲▲■■/■/５/■■■■■ ", 0Xdc143c, 場所_正門, 場所_広間
;0X(16進数色コード)で色コード値を変えるとボタンを除く全ての文字(記号)がその色で表示される
;色の指定は部分カラー指定が優先される、上の場合▲▲▲■■の部分のみ違う色で表示される
;"-1"でEmueraに設定している基本の文字色になる


CALL MAP_SETING, "■■■■■■■■■■■■■■■■　"							, 0Xdc143c, 場所_正門, 場所_広間
CALL MAP_SETING, "■/22/　　　　　　　　　　/10/　/６/■　"						, 0Xdc143c, 場所_食堂, 場所_図書室
CALL MAP_SETING, "■　■■■■■　■■■　　　　■　"							, 0Xdc143c, 場所_厨房, 場所_納屋
CALL MAP_SETING, "■　■　/５/　　/３/　■■■■■　■　"						, 0Xdc143c, 場所_パチュリー私室
CALL MAP_SETING, "■　■■■■■　■■　　/８/■　■　"							, 0Xdc143c, 場所_小悪魔私室
CALL MAP_SETING, "■　■/17/■　/#resetC=＜　＞/■/４/　■■　■　"				, 0Xdc143c, 場所_守衛小屋
CALL MAP_SETING, "■　■/23/　　　/２/　　　　/７/■　■　"						, 0Xdc143c, 場所_庭;, 場所_地下室階段
CALL MAP_SETING, "■　■■■■■　■■■■■■　■　"							, 0Xdc143c, 場所_屋外トイレ, 場所_1Fトイレ
CALL MAP_SETING, "■/９/　　　　　/１/　　　　　　　■　"						, 0Xdc143c
CALL MAP_SETING, "■■■■■■■　■■■■■■■■　"							, 0Xdc143c
CALL MAP_SETING, "　　　　　　　　　　　　　　　　　"							, 0Xdc143c
CALL MAP_SETING, "２階　　　　　　　　　　地下　　　"							, -1, 場所_二階踊り場, 場所_二階廊下
CALL MAP_SETING, "■■■■■■■■　　　　■■■■　"							, 0Xdc143c, 場所_咲夜私室
CALL MAP_SETING, "■/13/■/15/■/14/■■■　　　■/17/#resetC=＞/■　"			, 0Xdc143c, 場所_妖精メイド詰め所
CALL MAP_SETING, "■　　　/12/　　/24/■　　　■■■■■"						, 0Xdc143c, 場所_あなた私室
CALL MAP_SETING, "■■■■■　■■■■■　■　/#resetC=＞/　■"					, 0Xdc143c, 場所_レミリア私室, 場所_3Fテラス
CALL MAP_SETING, "■■■/２/#resetC=＜/11/#resetC=＞/16/　/26/■　■　/18/　■"	, 0Xdc143c, 場所_2Fトイレ
CALL MAP_SETING, "■■■■■■■■■■■　■■■■■"							, 0Xdc143c, 場所_地下室, 場所_大浴場


;−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−
;霧の湖他
@MAP_PRINT_1

PRINTL 〜霧の湖周辺など〜
PRINTL
CALL MAP_SETING, "　　　　■/#resetC=　紅魔館　/■　　　　　　　　　　　　"									, 0Xdc143c
CALL MAP_SETING, "　　　　■　　　　　■　　　　　　　/#resetC=魔法の森　"									, 0Xdc143c
CALL MAP_SETING, "　　　　■■■/１/■■■　　　　　　　　　/#228b22=●●●"								, 0Xdc143c
CALL MAP_SETING, "　　　　　　　　　　　　　　　　　　/#228b22=●●●●●"									, -1
CALL MAP_SETING, "　　　　　　　　　　　　　　　　　/#228b22=●●/■■■/#228b22=●"						, -1
CALL MAP_SETING, "　　　　　　　/19/　　　　　　　　/#228b22=●●●/■/30/■/#228b22=●"					, -1
CALL MAP_SETING, "　　　　　　　　　　　　　　　　　/28/　　　　　"											, -1
CALL MAP_SETING, "#1E90FF=　　　　　／／／／／/　　　　　　●　●●　　　"									, 0X228b22
CALL MAP_SETING, "#1E90FF=　　　　／／／／／／／/　　　　　/#228b22=●/29/#228b22=●/■/25/■/#228b22=●"	, -1
CALL MAP_SETING, "#1E90FF=　　　／／／/霧の湖/#1E90FF=／／／/　　　　/#228b22=●●●/■■■/#228b22=●"		, -1
CALL MAP_SETING, "#1E90FF=　　　　／／／／／／／/　　　　　　●●●●●●"									, 0X228b22
CALL MAP_SETING, "#1E90FF=　　　　　／／／／／/　　　　　　　　　　●●●"									, 0X228b22
CALL MAP_SETING, "　　　　　　　　　　　　　　　　　　　　　　●"											, 0X228b22
CALL MAP_SETING, "　　　　　　　/27/　　　　　　　　　　　　　　　"											, -1, 場所_正門
CALL MAP_SETING, "　　　　　　　　　　　　　　　　　　　　　　　"											, -1, 場所_魔法の森内部, 場所_ルーミアの住処
CALL MAP_SETING, "　　　　　　　　　　　　　　　　　　　　　　　"											, -1, 場所_魔理沙の家, 場所_アリスの家
CALL MAP_SETING, "■/20/■　　　■/21/■　　　　　　　　　　　　　　"										, -1, 場所_湖, 場所_湖南部
CALL MAP_SETING, "■■■　　　■■■　　　　　　　　　　　　　　"											, -1, 場所_チルノの家, 場所_大妖精の家


;−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−−
;マップセット
@MAP_SETING, ARGS, C_CODE, ARG:0, ARG:1, ARG:2, ARG:3, ARG:4

#DIM C_CODE
#DIMS MAPING, 50

;ARG:n = マップの横に表示するボタン

VARSET MAPING

SPLIT ARGS, "/", MAPING
LOCAL:1 = RESULT
;マップ表示
PRINT ┃
FOR LOCAL, 0, LOCAL:1
	;数値か確認
	TOHALF MAPING:LOCAL
	TOINT RESULTS
	;数値ならボタン化
	IF RESULT != 0
		CALL GETMAP(RESULT)
	ELSE
		;先頭８文字を取得、先頭が#、後尾が=ならカラー取得
		IF STRLENSU(MAPING:LOCAL) >= 8 && SUBSTRINGU(MAPING:LOCAL, 0, 1) == "#" && SUBSTRINGU(MAPING:LOCAL, 7, 1) == "="
			
			;resetCで基本設定の文字色へ戻す
			IF SUBSTRINGU(MAPING:LOCAL, 1, 6) == "resetC"
				RESETCOLOR
			ELSE
				;間の６文字を２文字で区切って色を取得（無効文字は0とする）
				SETCOLOR (MAP_SETC(SUBSTRINGU(MAPING:LOCAL, 1, 1), SUBSTRINGU(MAPING:LOCAL, 2, 1), 4) + MAP_SETC(SUBSTRINGU(MAPING:LOCAL, 3, 1), SUBSTRINGU(MAPING:LOCAL, 4, 1), 2) + MAP_SETC(SUBSTRINGU(MAPING:LOCAL, 5, 1), SUBSTRINGU(MAPING:LOCAL, 6, 1), 0))
			ENDIF
			
			;先頭８文字を除いて表示
			PRINTFORM %SUBSTRINGU(MAPING:LOCAL, 8, STRLENSU(MAPING:LOCAL)-8)%
		ELSE
			SIF C_CODE >= 0 && C_CODE <= 0XFFFFFF
				SETCOLOR C_CODE
			PRINTFORM %MAPING:LOCAL%
		ENDIF
		RESETCOLOR
	ENDIF
NEXT
PRINT ┃

;マップ側面のボタン表示
FOR LOCAL, 0, 5
	SIF ARG:LOCAL == 0
		CONTINUE
	
	;数値1~9なら大文字化
	IF ARG:LOCAL < 10
		TOSTR ARG:LOCAL
		TOFULL RESULTS
		LOCALS = %RESULTS%
	ELSE
		LOCALS = %@"{ARG:LOCAL}"%
	ENDIF
		
	PRINT  
	PRINTBUTTON @"%LOCALS%:%GETPLACENAME(ARG:LOCAL, 0)% ", ARG:LOCAL
NEXT
PRINTL


@MAP_SETC(ARGS, ARGS:1, ARG)
#FUNCTION

RETURNF MAP_SETC2(TOLOWER(ARGS)) * POWER(16, ARG+1) + MAP_SETC2(TOLOWER(ARGS:1)) * POWER(16, ARG)


@MAP_SETC2(ARGS)
#FUNCTION

SELECTCASE ARGS
	CASE "1"
		RETURNF 1
	CASE "2"
		RETURNF 2
	CASE "3"
		RETURNF 3
	CASE "4"
		RETURNF 4
	CASE "5"
		RETURNF 5
	CASE "6"
		RETURNF 6
	CASE "7"
		RETURNF 7
	CASE "8"
		RETURNF 8
	CASE "9"
		RETURNF 9
	CASE "a"
		RETURNF 10
	CASE "b"
		RETURNF 11
	CASE "c"
		RETURNF 12
	CASE "d"
		RETURNF 13
	CASE "e"
		RETURNF 14
	CASE "f"
		RETURNF 15
	CASEELSE
		RETURNF 0
ENDSELECT

