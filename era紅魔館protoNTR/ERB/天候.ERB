
;======================================
;日時
;======================================
;追加パッチverup 93----------------------------------------------------------------
@日付初期設定
暦法月 = 1
暦法日 = 1
$Ty245124
DRAWLINE
PRINTL
PRINTL 日付初期設定
PRINTL
PRINTFORM   %日付_月(暦法月)% 
PRINTBUTTON "[0:変更]", 0
PRINTL
PRINTFORM   {暦法日}日 
PRINTBUTTON "[1:変更]", 1
PRINTL
PRINTL
PRINTL
SIF 暦法月 >= 1 && 暦法月 <= 6 && 暦法日 >= 1 && 暦法日 <= 30
	PRINTBUTTON "[2:決定]", 2
PRINTL 
INPUT
IF RESULT == 0
	PRINTL 月変更
	PRINTL [1] 春季     4~5月
	PRINTL [2] 夏季上旬 6~7月
	PRINTL [3] 夏季下旬 8~9月
	PRINTL [4] 秋季     10~11月
	PRINTL [5] 冬季上旬 12~1月
	PRINTL [6] 冬季下旬 2~3月
	INPUT
	SIF RESULT >= 1 && RESULT <= 6
		暦法月 = RESULT
ELSEIF RESULT == 1
	PRINTL 日時変更(1~30のいずれかの数値を入力してください)
	INPUT
	
	SIF RESULT >= 1 && RESULT <= 30
		暦法日 = RESULT
	
ELSEIF RESULT == 2 && 暦法月 >= 1 && 暦法月 <= 6 && 暦法日 >= 1 && 暦法日 <= 30
	CALL 日間気温設定
	CALL 現在気温設定
	RETURN 
ENDIF
GOTO Ty245124

;BEFORETRAIN.ERB 32,INFO.ERB 612----------------------------------------------------------------
@日付変更
暦法日 ++
IF 暦法日 > 30
	暦法日 = 1
	IF 暦法月 == 6
		暦法月 = 1
	ELSE
		暦法月 ++
	ENDIF
ENDIF

;SHOP.ERB 8,INFO.ERB 615----------------------------------------------------------------
@日付_月(ARG)
#FUNCTIONS
SELECTCASE ARG
	CASE 1
		RETURNF "春季"
	CASE 2
		RETURNF "夏季上旬"
	CASE 3
		RETURNF "夏季下旬"
	CASE 4
		RETURNF "秋季"
	CASE 5
		RETURNF "冬季上旬"
	CASE 6
		RETURNF "冬季下旬"
ENDSELECT


;======================================
;天候
;======================================

;----------------------------------------------------------------
@年間基礎雷発生確率(ARG, ARG:1)
#FUNCTION
;ARG   指定の月
;ARG:1 指定の日
SELECTCASE ARG
	CASE 1
		SELECTCASE ARG:1
			CASE IS < 16
				RETURNF 15
			CASEELSE
				RETURNF 20
		ENDSELECT
	CASE 2
		SELECTCASE ARG:1
			CASE IS < 16
				RETURNF 25
			CASEELSE
				RETURNF 20
		ENDSELECT
	CASE 3
		SELECTCASE ARG:1
			CASE IS < 16
				RETURNF 30
			CASEELSE
				RETURNF 15
		ENDSELECT
	CASE 4
		SELECTCASE ARG:1
			CASE IS < 16
				RETURNF 10
			CASEELSE
				RETURNF 10
		ENDSELECT
	CASE 5
		SELECTCASE ARG:1
			CASE IS < 16
				RETURNF 5
			CASEELSE
				RETURNF 5
		ENDSELECT
	CASE 6
		SELECTCASE ARG:1
			CASE IS < 16
				RETURNF 5
			CASEELSE
				RETURNF 15
		ENDSELECT
ENDSELECT
;----------------------------------------------------------------
@年間基礎降水確率(ARG, ARG:1)
#FUNCTION
;ARG   指定の月
;ARG:1 指定の日
SELECTCASE ARG
	CASE 1
		SELECTCASE ARG:1
			CASE IS < 16
				RETURNF 10
			CASEELSE
				RETURNF 20
		ENDSELECT
	CASE 2
		SELECTCASE ARG:1
			CASE IS < 16
				RETURNF 70
			CASEELSE
				RETURNF 50
		ENDSELECT
	CASE 3
		SELECTCASE ARG:1
			CASE IS < 16
				RETURNF 30
			CASEELSE
				RETURNF 40
		ENDSELECT
	CASE 4
		SELECTCASE ARG:1
			CASE IS < 16
				RETURNF 30
			CASEELSE
				RETURNF 10
		ENDSELECT
	CASE 5
		SELECTCASE ARG:1
			CASE IS < 16
				RETURNF 10
			CASEELSE
				RETURNF 10
		ENDSELECT
	CASE 6
		SELECTCASE ARG:1
			CASE IS < 16
				RETURNF 10
			CASEELSE
				RETURNF 20
		ENDSELECT
ENDSELECT
;----------------------------------------------------------------
@年間基礎最高気温(ARG, ARG:1)
#FUNCTION
;ARG   指定の月
;ARG:1 指定の日
SELECTCASE ARG
	CASE 1
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 17
			CASE 4, 5
				RETURNF 18
			CASE 6 TO 8
				RETURNF 19
			CASE 9 TO 11
				RETURNF 20
			CASE 12 TO 14
				RETURNF 21
			CASE 15 TO 18
				RETURNF 22
			CASE 19 TO 22
				RETURNF 23
			CASE 23 TO 27
				RETURNF 24
			CASE 28 TO 30
				RETURNF 25
		ENDSELECT
	CASE 2
		SELECTCASE ARG:1
			CASE 1, 2
				RETURNF 25
			CASE 3 TO 7
				RETURNF 26
			CASE 8 TO 13
				RETURNF 27
			CASE 14 TO 17
				RETURNF 28
			CASE 18 TO 22
				RETURNF 29
			CASE 23 TO 28
				RETURNF 30
			CASE 29, 30
				RETURNF 31
		ENDSELECT
	CASE 3
		SELECTCASE ARG:1
			CASE 1, 2
				RETURNF 31
			CASE 3, 4
				RETURNF 30
			CASE 5 TO 7
				RETURNF 29
			CASE 8 TO 10
				RETURNF 28
			CASE 11, 12
				RETURNF 27
			CASE 13, 14
				RETURNF 26
			CASE 15, 16
				RETURNF 25
			CASE 17 TO 19
				RETURNF 24
			CASE 20 TO 22
				RETURNF 23
			CASE 23, 24
				RETURNF 22
			CASE 25 TO 27
				RETURNF 21
			CASE 28, 29
				RETURNF 20
			CASE 30
				RETURNF 19
		ENDSELECT
	CASE 4
		SELECTCASE ARG:1
			CASE 1 TO 4
				RETURNF 19
			CASE 5 TO 7
				RETURNF 18
			CASE 8 TO 10
				RETURNF 17
			CASE 11, 12
				RETURNF 16
			CASE 13, 14
				RETURNF 15
			CASE 15, 16
				RETURNF 14
			CASE 17, 18
				RETURNF 13
			CASE 19, 20
				RETURNF 12
			CASE 21, 22
				RETURNF 11
			CASE 23 TO 25
				RETURNF 10
			CASE 26, 27
				RETURNF 9
			CASE 28, 29
				RETURNF 8
			CASE 30
				RETURNF 7
		ENDSELECT
	CASE 5
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 7
			CASE 4 TO 9
				RETURNF 6
			CASE 10 TO 14
				RETURNF 5
			CASE 15 TO 21
				RETURNF 4
			CASE 22 TO 28
				RETURNF 5
			CASE 29, 30
				RETURNF 6
		ENDSELECT
	CASE 6
		SELECTCASE ARG:1
			CASE 1 TO 6
				RETURNF 6
			CASE 7 TO 12
				RETURNF 7
			CASE 13, 14
				RETURNF 8
			CASE 15, 16
				RETURNF 9
			CASE 17, 18
				RETURNF 10
			CASE 19, 20
				RETURNF 11
			CASE 21, 22
				RETURNF 12
			CASE 23, 24
				RETURNF 13
			CASE 25, 26
				RETURNF 14
			CASE 27, 28
				RETURNF 15
			CASE 29
				RETURNF 16
			CASE 30
				RETURNF 17
		ENDSELECT
ENDSELECT

;----------------------------------------------------------------
@年間基礎最低気温(ARG, ARG:1)
#FUNCTION
;ARG   指定の月
;ARG:1 指定の日
SELECTCASE ARG
	CASE 1
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 11
			CASE 4 TO 6
				RETURNF 12
			CASE 7, 8
				RETURNF 13
			CASE 9 TO 11
				RETURNF 14
			CASE 12 TO 14
				RETURNF 15
			CASE 15 TO 17
				RETURNF 16
			CASE 18 TO 20
				RETURNF 17
			CASE 21 TO 23
				RETURNF 18
			CASE 24 TO 27
				RETURNF 19
			CASE 28 TO 30
				RETURNF 20
		ENDSELECT
	CASE 2
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 20
			CASE 4 TO 7
				RETURNF 21
			CASE 8 TO 10
				RETURNF 22
			CASE 11 TO 14
				RETURNF 23
			CASE 15 TO 22
				RETURNF 24
			CASE 23 TO 30
				RETURNF 25
		ENDSELECT
	CASE 3
		SELECTCASE ARG:1
			CASE 1 TO 5
				RETURNF 25
			CASE 6 TO 9
				RETURNF 24
			CASE 10 TO 12
				RETURNF 23
			CASE 13, 14
				RETURNF 22
			CASE 15 TO 17
				RETURNF 21
			CASE 18 TO 20
				RETURNF 20
			CASE 21 TO 23
				RETURNF 19
			CASE 24, 25
				RETURNF 18
			CASE 26
				RETURNF 17
			CASE 27, 28
				RETURNF 16
			CASE 29
				RETURNF 15
			CASE 30
				RETURNF 14
		ENDSELECT
	CASE 4
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 14
			CASE 4, 5
				RETURNF 13
			CASE 6, 7
				RETURNF 12
			CASE 8, 9
				RETURNF 11
			CASE 10, 11
				RETURNF 10
			CASE 12 TO 14
				RETURNF 9
			CASE 15 TO 17
				RETURNF 8
			CASE 18 TO 20
				RETURNF 7
			CASE 21 TO 23
				RETURNF 6
			CASE 24 TO 26
				RETURNF 5
			CASE 27 TO 29
				RETURNF 4
			CASE 30
				RETURNF 3
		ENDSELECT
	CASE 5
		SELECTCASE ARG:1
			CASE 1, 2
				RETURNF 3
			CASE 3 TO 6
				RETURNF 2
			CASE 7 TO 10
				RETURNF 1
			CASE 11 TO 13
				RETURNF 0
			CASE 14 TO 21
				RETURNF -1
			CASE 22 TO 30
				RETURNF 0
		ENDSELECT
	CASE 6
		SELECTCASE ARG:1
			CASE 1 TO 3
				RETURNF 0
			CASE 4 TO 6
				RETURNF 1
			CASE 7, 8
				RETURNF 2
			CASE 9, 10
				RETURNF 3
			CASE 11 TO 14
				RETURNF 4
			CASE 15, 16
				RETURNF 5
			CASE 17, 18
				RETURNF 6
			CASE 19, 20
				RETURNF 7
			CASE 21 TO 23
				RETURNF 8
			CASE 24, 25
				RETURNF 9
			CASE 26 TO 28
				RETURNF 10
			CASE 29, 30
				RETURNF 11
		ENDSELECT
ENDSELECT

;INFO.ERB 625----------------------------------------------------------------
@天候(ARG)
#FUNCTIONS
;ARG 天候値
SELECTCASE ARG
	CASE 0
		RETURNF "快晴"
	CASE 1
		RETURNF "晴れ"
	CASE 2
		RETURNF "曇り"
	CASE 3
		RETURNF "雨"
	CASE 4
		RETURNF "雪"
	CASE 5
		RETURNF "雷雨"
	CASE 6
		RETURNF "大雨"
	CASE 7
		RETURNF "大雪"
	CASE 8
		RETURNF "嵐"
	CASE 9
		RETURNF "吹雪"
	CASE 10
		RETURNF "霧"
	CASE 11
		RETURNF "暗闇"
	CASE 12
		RETURNF "桃霧"
ENDSELECT
;BEFORETRAIN.ERB 33,INFO.ERB 613----------------------------------------------------------------
@日間気温設定
;日時変更時に設定

;異常気象終了
IF (天候(天候値) == "嵐" || 天候(天候値) == "吹雪" || 天候(天候値) == "霧" || 天候(天候値) == "暗闇" || 天候(天候値) == "桃霧")
	;温度によって変化
	IF FLAG:現在気温 <= 3
		天候値 = 4
	ELSE
		天候値 = 3
	ENDIF
ENDIF

FLAG:最高気温 = 年間基礎最高気温(暦法月, 暦法日) + RAND(-3, 3)
;気温差は最低でも3度
FLAG:最低気温 = MIN(FLAG:最高気温-3, 年間基礎最低気温(暦法月, 暦法日) + RAND(-3, 3))
FLAG:降水確率 = RAND:(年間基礎降水確率(暦法月, 暦法日)+1)

CALL 異常気象


;----------------------------------------------------------------
@異常気象

;最高気温変化
IF FLAG:最高気温 >= (年間基礎最高気温(暦法月, 暦法日) + 2) && RAND:100 == 0
	IF RAND:2
		FLAG:最高気温 += RAND(3, 5)
	ELSE
		LOCAL = RAND(3, 5)
		FLAG:最高気温 -= LOCAL
		FLAG:最低気温 = MIN(FLAG:最高気温-3, FLAG:最低気温)
	ENDIF
ENDIF

;最低気温変化
IF FLAG:最低気温 <= (年間基礎最低気温(暦法月, 暦法日) - 2) && RAND:100 == 0
	FLAG:最高気温 -= RAND(3, 5)
ENDIF

;降水確率上昇
IF RAND:100 == 0
	FLAG:降水確率 += 30
ENDIF


SELECTCASE RAND:100
	;嵐、吹雪
	CASE 0
	;発生までに掛かる時間
	FLAG:異常気象発生ディレイ = -(RAND:720)
	;霧
	CASE 1
		天候値 = 10
	;暗闇
	CASE 2
		天候値 = 11
	;桃霧
	CASE 3
		天候値 = 12
ENDSELECT




;EVENTCOMEND.ERB 7----------------------------------------------------------------
@現在気温設定
;高低差を５分割
SELECTCASE TIME
	;PM11~AM4
	CASE 0 TO 299, IS >= 1380
		LOCAL = FLAG:最低気温
	;AM5~7
	CASE 300 TO 479
		LOCAL = FLAG:最低気温 + ((FLAG:最高気温 - FLAG:最低気温) / 5)
	;AM8~9
	CASE 480 TO 599
		LOCAL = FLAG:最低気温 + ((FLAG:最高気温 - FLAG:最低気温) / 5 * 2)
	;AM10~11
	CASE 600 TO 719
		LOCAL = FLAG:最低気温 + ((FLAG:最高気温 - FLAG:最低気温) / 5 * 3)
	;PM12~2
	CASE 720 TO 899
		LOCAL = FLAG:最低気温 + ((FLAG:最高気温 - FLAG:最低気温) / 5 * 4)
	;PM3~5
	CASE 900 TO 1079
		LOCAL = FLAG:最高気温
	;PM6~7
	CASE 1080 TO 1199
		LOCAL = FLAG:最低気温 + ((FLAG:最高気温 - FLAG:最低気温) / 5 * 4)
	;PM8~9
	CASE 1200 TO 1259
		LOCAL = FLAG:最低気温 + ((FLAG:最高気温 - FLAG:最低気温) / 5 * 2)
	;PM9~10
	CASE 1260 TO 1379
		LOCAL = FLAG:最低気温 + ((FLAG:最高気温 - FLAG:最低気温) / 5)
ENDSELECT
;日が出ていないと温度低下
SELECTCASE TIME
	;AM5~PM7
	CASE 300 TO 1199
		IF 天候(天候値) != "快晴" && 天候(天候値) != "晴れ"
			LOCAL -= ((FLAG:最高気温 - FLAG:最低気温) / 5)
		ENDIF
ENDSELECT
FLAG:現在気温 = LOCAL

;EVENTCOMEND.ERB 6,BEFOR 36----------------------------------------------------------------
@天候状態
#DIM DYNAMIC 天候TIME
#DIM DYNAMIC 天候変更

;嵐発生
IF FLAG:異常気象発生ディレイ < 0
	IF (FLAG:異常気象発生ディレイ + TIME_PROGRESS(TFLAG:300)) >= 0
		;温度によって変化
		IF FLAG:現在気温 <= 3
			天候値 = 9
		ELSE
			天候値 = 8
		ENDIF
		;発生時間は4〜12時間
		FLAG:異常気象発生ディレイ = RAND(240, 720)
			;PRINTFORML 嵐発生
	ELSE
		FLAG:異常気象発生ディレイ += TIME_PROGRESS(TFLAG:300)
			;PRINTFORML 嵐まであと{FLAG:異常気象発生ディレイ}
	ENDIF
;嵐終了
ELSEIF FLAG:異常気象発生ディレイ > 0
	IF (FLAG:異常気象発生ディレイ - TIME_PROGRESS(TFLAG:300)) <= 0
		天候値 = 3
		FLAG:異常気象発生ディレイ = 0
			;PRINTFORML 嵐終了
	ELSE
		FLAG:異常気象発生ディレイ -= TIME_PROGRESS(TFLAG:300)
			;PRINTFORML 嵐やむまで{FLAG:異常気象発生ディレイ}
	ENDIF
ENDIF

;10~30分に一度
IF (天候TIME > TIME || TIME - 天候TIME >= 天候変更)
	;異常気象なら天候変化なし
	IF (天候(天候値) == "嵐" || 天候(天候値) == "吹雪" || 天候(天候値) == "霧" || 天候(天候値) == "暗闇" || 天候(天候値) == "桃霧")
		;温度によって変化
		IF FLAG:現在気温 <= 3 && (天候(天候値) == "嵐" || 天候(天候値) == "吹雪")
			天候値 = 9
		ELSE
			天候値 = 8
		ENDIF
		GOTO SKIP_天候
	ENDIF
	;雨天なら晴れに戻る確率発生
	IF 天候値 >= 3
		;雨が上がる確率は100 - 降水確率　最大70% 最低10%
		IF RAND:100 < LIMIT(100 - FLAG:降水確率, 10, 70)
			天候値 = 1
		ENDIF
	ENDIF
	;曇りの状態なら降雨確率発生
	IF 天候(天候値) == "曇り" && FLAG:降水確率 != 0
		;降雨確率
		IF RAND:100 < FLAG:降水確率
			;温度によって雪になる
			IF FLAG:現在気温 <= 3
				天候値 = 4
			ELSE
				;雷雨
				IF RAND:100 < 年間基礎雷発生確率(暦法月, 暦法日)
					天候値 = 5
				ELSE
					天候値 = 3
				ENDIF
			ENDIF
			
		ENDIF
	ENDIF
	
	;快晴〜曇りの状態変化
	IF 天候値 < 3
		;50%の確率で天候維持
		IF RAND:100 < 50
			IF 天候(天候値) == "快晴"
				IF RAND:100 <= 70
					天候値 = 1
				ELSE
					天候値 = 2
				ENDIF
			ELSEIF 天候(天候値) == "晴れ"
				IF RAND:100 <= 50
					天候値 = 0
				ELSE
					天候値 = 2
				ENDIF
			ELSE
				IF RAND:100 <= 70
					天候値 = 1
				ELSE
					天候値 = 0
				ENDIF
			ENDIF
		ENDIF
	ENDIF
	
	;雨、雪の大雨、大雪化
	IF (天候(天候値) == "雨" || 天候(天候値) == "雪")
		;確率は降水量/10 最低1%
		IF RAND:100 < MAX(1, FLAG:降水確率/10)
			IF 天候(天候値) == "雨"
				天候値 = 6
			ELSE
				天候値 = 7
			ENDIF
		ENDIF
	ENDIF
	$SKIP_天候
	;次の変化時間と今の時間を保存
	天候変更 = RAND(10, 30)
	天候TIME = (TIME - (TIME % 30))
ENDIF


;EVENTCOMEND.ERB 9----------------------------------------------------------------
@天候によるステータス増減処理
;LOCAL   キャラ番号
;LOCAL:1 かける値
;LOCAL:2 減少量
;LOCAL:3 地の文変化

;かける値 = (経過時間 - (経過時間%5)) / 5
LOCAL:1 = ((TIME_PROGRESS(TFLAG:300) - (TIME_PROGRESS(TFLAG:300)%5)) / 5)
			;PRINTFORML 経過時間{TIME_PROGRESS(TFLAG:300)} 気温{FLAG:現在気温} かけ値{LOCAL:1}
SIF LOCAL:1 <= 0
	RETURN
;キャラの体力減少処理
FOR LOCAL, 0, CHARANUM
	;野外であること
	SIF OPENPLACE(CFLAG:LOCAL:現在位置) != 2
		CONTINUE
	LOCAL:2 = 0
	;気温に耐性がない場合、体力が減少する
	;※処理上、コマンド実行前の気温をベースに処理が進みます
	IF FLAG:現在気温 > CFLAG:LOCAL:暑さ耐性
		LOCAL:2 = (FLAG:現在気温 - CFLAG:LOCAL:暑さ耐性) * LOCAL:1
		LOCAL:3 = 0
	ELSEIF FLAG:現在気温 < CFLAG:LOCAL:寒さ耐性
		LOCAL:2 = (CFLAG:LOCAL:寒さ耐性 - FLAG:現在気温) * LOCAL:1
		LOCAL:3 = 1
	ELSE
		CONTINUE
	ENDIF
	IF LOCAL:2
	;現在の気力による分岐
		IF BASE:LOCAL:気力
			BASE:LOCAL:体力 = MAX(0, BASE:LOCAL:体力 - LOCAL:2 / 2)
			BASE:LOCAL:気力 = MAX(0, BASE:LOCAL:気力 - LOCAL:2)
				;PRINTFORML %CALLNAME:LOCAL% 耐暑{CFLAG:LOCAL:暑さ耐性} 耐寒{CFLAG:LOCAL:寒さ耐性} 体力-{LOCAL:2 / 2} 気力-{LOCAL:2}
		ELSE
			BASE:LOCAL:体力 = MAX(0, BASE:LOCAL:体力 - LOCAL:2)
				;PRINTFORML %CALLNAME:LOCAL% 耐暑{CFLAG:LOCAL:暑さ耐性} 耐寒{CFLAG:LOCAL:寒さ耐性} 体力-{LOCAL:2} 気力０
		ENDIF
		IF LOCAL:3
			PRINTFORML %CALLNAME:LOCAL%は寒さで体力を消耗した
		ELSE
			PRINTFORML %CALLNAME:LOCAL%は暑さで体力を消耗した
		ENDIF
	ENDIF
NEXT



;VERUP----------------------------------------------------------------
@気温耐性取得
	;CFLAG:暑さ耐性 暑さ耐性
	;CFLAG:寒さ耐性 寒さ耐性
	;メイン初期設定
	CFLAG:0:暑さ耐性 = 30
	CFLAG:1:暑さ耐性 = 30
	CFLAG:2:暑さ耐性 = 35
	CFLAG:3:暑さ耐性 = 25
	CFLAG:4:暑さ耐性 = 30
	CFLAG:5:暑さ耐性 = 100
	CFLAG:6:暑さ耐性 = 100
	CFLAG:7:暑さ耐性 = 30
	CFLAG:8:暑さ耐性 = 20
	CFLAG:9:暑さ耐性 = 25
	
	CFLAG:0:寒さ耐性 = 10
	CFLAG:1:寒さ耐性 = 5
	CFLAG:2:寒さ耐性 = 0
	CFLAG:3:寒さ耐性 = 10
	CFLAG:4:寒さ耐性 = 10
	CFLAG:5:寒さ耐性 = 5
	CFLAG:6:寒さ耐性 = 5
	CFLAG:7:寒さ耐性 = 0
	CFLAG:8:寒さ耐性 = -50
	CFLAG:9:寒さ耐性 = 10
	;子供の初期設定
	FOR LOCAL, 10, CHARANUM
		IF CFLAG:LOCAL:父親 <= CHARANUM-1 && CFLAG:LOCAL:母親 <= CHARANUM-1
			IF RAND:2
				CFLAG:LOCAL:暑さ耐性 = CFLAG:(CFLAG:LOCAL:父親):暑さ耐性
				CFLAG:LOCAL:寒さ耐性 = CFLAG:(CFLAG:LOCAL:父親):寒さ耐性
			ELSE
				CFLAG:LOCAL:暑さ耐性 = CFLAG:(CFLAG:LOCAL:母親):暑さ耐性
				CFLAG:LOCAL:寒さ耐性 = CFLAG:(CFLAG:LOCAL:母親):寒さ耐性
			ENDIF
		ELSEIF CFLAG:LOCAL:父親 <= CHARANUM-1
			CFLAG:LOCAL:暑さ耐性 = CFLAG:(CFLAG:LOCAL:父親):暑さ耐性
			CFLAG:LOCAL:寒さ耐性 = CFLAG:(CFLAG:LOCAL:父親):寒さ耐性
		ELSE
			CFLAG:LOCAL:暑さ耐性 = CFLAG:(CFLAG:LOCAL:母親):暑さ耐性
			CFLAG:LOCAL:寒さ耐性 = CFLAG:(CFLAG:LOCAL:母親):寒さ耐性
		ENDIF
	NEXT
;PREGNACY_S 326----------------------------------------------------------------
@子供気温耐性取得, ARG
IF CFLAG:ARG:父親 <= CHARANUM-1 && CFLAG:ARG:母親 <= CHARANUM-1
	IF RAND:2
		CFLAG:ARG:暑さ耐性 = CFLAG:(CFLAG:ARG:父親):暑さ耐性
		CFLAG:ARG:寒さ耐性 = CFLAG:(CFLAG:ARG:父親):寒さ耐性
	ELSE
		CFLAG:ARG:暑さ耐性 = CFLAG:(CFLAG:ARG:母親):暑さ耐性
		CFLAG:ARG:寒さ耐性 = CFLAG:(CFLAG:ARG:母親):寒さ耐性
	ENDIF
ELSEIF CFLAG:ARG:父親 <= CHARANUM-1
	CFLAG:ARG:暑さ耐性 = CFLAG:(CFLAG:ARG:父親):暑さ耐性
	CFLAG:ARG:寒さ耐性 = CFLAG:(CFLAG:ARG:父親):寒さ耐性
ELSE
	CFLAG:ARG:暑さ耐性 = CFLAG:(CFLAG:ARG:母親):暑さ耐性
	CFLAG:ARG:寒さ耐性 = CFLAG:(CFLAG:ARG:母親):寒さ耐性
ENDIF

