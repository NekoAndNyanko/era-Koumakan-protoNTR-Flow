;-------------------------------------------------
;フェラする
;愛撫系コマンド、レベル1
;-------------------------------------------------
@COM2

TRYCALL COM417_is_Protected(PLAYER,TARGET,娼婦紋_恥丘)
SIF RESULT
	RETURN 0

;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;TCVAR:2 射精箇所フラグ（-1=コンドーム 1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手）

LOCALS = {PLAYER}/
CSTR:TARGET:10 += LOCALS
TCVAR:TARGET:2 = 4
;強度
TCVAR:TARGET:4 = 1500

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:体力 += 5
DOWNBASE:気力 += 50

SOURCE:快Ｃ = 110
SOURCE:露出 = 20
SOURCE:逸脱 = 20
SOURCE:反感 = 20

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:自己愛 < 0 || TALENT:抵抗
	SOURCE:鬱屈 += 20

;調教者のTALENT:舌使いをみる
SIF TALENT:PLAYER:舌使い
	TIMES SOURCE:快Ｃ , 1.50

;調教者のTALENT:猫舌をみる
SIF TALENT:PLAYER:猫舌
	TIMES SOURCE:快Ｃ , 0.75
;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のＰ⇔調教者の口の汚れが移動
STAIN:Ｐ |= STAIN:PLAYER:口
STAIN:PLAYER:口 |= STAIN:Ｐ

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 3
	EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 3
ENDIF
;調教者が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
SIF TALENT:PLAYER:猫舌
	EXP:PLAYER:嗜虐快楽経験 += 1

RETURN 1
