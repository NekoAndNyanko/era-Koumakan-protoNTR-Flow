;-------------------------------------------------------------------------------
;FIXME:関数の説明を書いてください
;@return 1:xxx,0:xxx
@SAMEN_DIRECTION(ARG,ARG:1)
	#FUNCTION
	;ARG 射精する人
	;ARG:1 射精される人
	LOCALS = /{ARG:1}/
	SIF STRCOUNT(CSTR:ARG:10,LOCALS)
		RETURNF 1

;-------------------------------------------------------------------------------
;FIXME:関数の説明を書いてください
;@return 1:xxx,0:xxx
@SAMEN_DIRECTION2(ARG,ARG:1)
	#FUNCTION
	;ARG 射精する人
	;ARG:1 射精される人
	LOCALS = /{ARG:1}/
	SIF STRCOUNT(CSTR:ARG:11,LOCALS)
		RETURNF 1

;-------------------------------------------------------------------------------
;FIXME:関数の説明を書いてください
;@return 1:xxx,0:xxx
@EVENT_SHOOT
	VARSET LOCAL
	;TCVAR:2 射精箇所フラグ（1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手）
	;TCVAR:4 射精快感強度
	;射精前判定
	FOR LOCAL,0,CHARANUM
		SIF !(HAS_PENIS(LOCAL))
			CONTINUE
		;コンドーム
		IF TEQUIP:LOCAL:コンドーム
			TCVAR:LOCAL:3 = 1
		ENDIF
		SIF LOCAL > 0
			CALL TARGET_EJAC_CHECK(LOCAL)
		;ダブルフェラなどのふたりめ補正に使う
		LOCAL:2 = 0
		FOR LOCAL:1,0,CHARANUM
			IF LOCAL:1 == 0
				LOCAL:3 = 0
			ELSE
				SIF TARGET:(LOCAL:1) <= 0
					CONTINUE
				LOCAL:3 = TARGET:(LOCAL:1)
			ENDIF
			SIF SAMEN_DIRECTION(LOCAL,LOCAL:3)
				CALL EJAC_CHECK(LOCAL,LOCAL:3,LOCAL:2)
			LOCAL:2 ++
		NEXT
	NEXT
	;射精処理
	FOR LOCAL,0,CHARANUM
		SIF LOCAL > 0 && TARGET:LOCAL <= 0
			CONTINUE
		IF LOCAL == 0
			LOCAL:1 = MASTER
		ELSE
			LOCAL:1 = TARGET:LOCAL
		ENDIF
		;射精処理
		SIF TCVAR:(LOCAL:1):4
			CALL EJAC_CHECK_AFTER(LOCAL:1)
		;部位別処理
		SIF NOWEX:(LOCAL:1):射精
			CALL SAMEN_SHOOT(LOCAL:1)
	NEXT
	;必須ではないが口上表示をわかりやすくするために射精状況をTFLAG:1に割り振る
	;PLAYERからTARGETへの膣内〜アナル奉仕射精まではTCVAR:12(=1〜9)に準拠
	IF SAMEN_DIRECTION2(PLAYER,TARGET)
		SIF TCVAR:PLAYER:12 != 0
			SETBIT TFLAG:1,TCVAR:PLAYER:12
	ENDIF
	FOR LOCAL,1,CHARANUM
		;コンドーム
		SIF TCVAR:PLAYER:13
			SETBIT TCVAR:LOCAL:50,射精箇所_コンドーム
		SIF SAMEN_DIRECTION2(PLAYER,LOCAL)
			SETBIT TCVAR:LOCAL:50,TCVAR:PLAYER:12
	NEXT
	;手淫フェラ
	IF TFLAG:50 == 5 && NOWEX:PLAYER:射精
		SETBIT TFLAG:1,射精箇所_手淫フェラ
		SETBIT TCVAR:50,射精箇所_手淫フェラ
	ENDIF
	;パイズリフェラ
	IF TFLAG:50 == 12 && NOWEX:PLAYER:射精
		SETBIT TFLAG:1,射精箇所_パイズリフェラ
		SETBIT TCVAR:50,射精箇所_パイズリフェラ
	ENDIF
	;授乳手コキ
	IF TFLAG:50 == 16 && NOWEX:PLAYER:射精
		SETBIT TFLAG:1,射精箇所_授乳手コキ
		SETBIT TCVAR:50,射精箇所_授乳手コキ
	ENDIF
	;愛撫されてふたなり射精
	IF SELECTCOM == 95 && NOWEX:PLAYER:射精
		SETBIT TFLAG:1,射精箇所_愛撫
		SETBIT TCVAR:50,射精箇所_愛撫
	ENDIF
	;泡踊りでTARGET射精
	IF TCVAR:PLAYER:12 == 8
		SETBIT TFLAG:1,射精箇所_体表
		SETBIT TCVAR:50,射精箇所_体表
	ENDIF
	;シックスナイン
	IF TFLAG:50 == 1 && (NOWEX:PLAYER:射精 || NOWEX:射精)
		SETBIT TFLAG:1,射精箇所_シックスナイン
		SETBIT TCVAR:50,射精箇所_シックスナイン
	ENDIF
	;イラマチオ
	IF SELECTCOM == 140 && NOWEX:PLAYER:射精
		SETBIT TFLAG:1,射精箇所_イラマチオ
		SETBIT TCVAR:50,射精箇所_イラマチオ
	ENDIF
	;子宮口責め
	IF TFLAG:50 == 7 && NOWEX:PLAYER:射精
		SETBIT TFLAG:1,射精箇所_子宮口責め
		SETBIT TCVAR:50,射精箇所_子宮口責め
	ENDIF

;-------------------------------------------------------------------------------
;FIXME:関数の説明を書いてください
;@return 1:xxx,0:xxx
@EJAC_CHECK(ARG,ARG:1,ARG:2)
	;ARG 射精する人
	;ARG:1 射精させる人
	;ARG:2 Wフェラ等におけるふたりめ補正
	;-------------------------------------------------
	;射精ゲージチェック
	;-------------------------------------------------
	;LOCAL:1〜に補正値をぶっこんでいきます
	;CFLAG:101 射精箇所フラグ（1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6= 
	;技巧（共通）
	LOCAL = ABL:(ARG:1):技巧
	LOCAL:1 = 10 + GET_REVISION(LOCAL, 5 , 5)
	;従順（共通）
	LOCAL = 0
	IF ARG:1 == MASTER
		LOCAL = 3
	ELSE
		LOCAL = ABL:(ARG:1):従順
	ENDIF
	LOCAL:2 = 10 + GET_REVISION(LOCAL, 3 , 5)
	;奉仕精神(奉仕系)
	LOCAL = 0
	IF TFLAG:30 && ARG == PLAYER
		IF ARG:1 == MASTER
			LOCAL= 3
		ELSE
			LOCAL = ABL:(ARG:1):奉仕精神
		ENDIF
		LOCAL:3 = 10 + GET_REVISION(LOCAL, 10 , 5)
	ELSE
		LOCAL:3 = 10
	ENDIF
	;欲望(性交系)
	LOCAL:4 = 10
	IF TCVAR:ARG:2 <= 2
		LOCAL = 0
		IF ARG:1 == MASTER
			LOCAL = 3
		ELSE
			LOCAL = ABL:(ARG:1):欲望
		ENDIF
		LOCAL:4 = 10 + GET_REVISION(LOCAL, 5 , 3)
	ENDIF
	;潤滑
	LOCAL:5 = 10
	IF TCVAR:ARG:2 <= 2 || TCVAR:ARG:2 == 6
		IF PALAM:潤滑 < PALAMLV:1
			LOCAL:5 = 6
		ELSEIF PALAM:潤滑 < PALAMLV:2
			LOCAL:5 = 8
		ELSEIF PALAM:潤滑 < PALAMLV:3
			LOCAL:5 = 10
		ELSEIF PALAM:潤滑 < PALAMLV:4
			LOCAL:5 = 12
		ELSEIF PALAM:潤滑 >= PALAMLV:4
			LOCAL:5 = 14
		ENDIF
	ENDIF
	;プレイヤーのＣ感覚をみる
	LOCAL:6 = 10 + GET_REVISION(ABL:ARG:3, 50 , 5)
	;部位別処理
	LOCAL:7 = 10
	;恋慕
	SIF TALENT:(ARG:1):恋慕
		LOCAL:7 += 2
	SELECTCASE TCVAR:ARG:2
			;膣
		CASE 1
			;処女
			SIF TALENT:(ARG:1):処女
				LOCAL:7 += 5
			SIF TALENT:(ARG:1):淫壺
				LOCAL:7 += 2
			
			;数の子天井
			SIF GETＶオプション(ARG:1, 1)
				LOCAL:7 += 2
			;淫乱と恋慕未取得、又はＶオプション"自在に収縮できる"が無ければ拡張具合に応じて減少
			IF (!GETＶオプション(ARG:1, 2) || (!TALENT:(ARG:1):淫乱 && !TALENT:(ARG:1):恋慕))
				SIF 締り具合名称(BASE:(ARG:1):Ｖ緩さ) == "きゅっきゅっ"
					LOCAL:7 -= 1
				SIF 締り具合名称(BASE:(ARG:1):Ｖ緩さ) == "きゅうきゅう"
					LOCAL:7 -= 3
				SIF 締り具合名称(BASE:(ARG:1):Ｖ緩さ) == "がばがば"
					LOCAL:7 -= 5
				SIF 締り具合名称(BASE:(ARG:1):Ｖ緩さ) == "ぽっかり"
					LOCAL:7 -= 7
			ENDIF
			;アナル
		CASE 2
			SIF !EXP:(ARG:1):Ａ性交経験
				LOCAL:7 += 3
			SIF TALENT:(ARG:1):尻穴狂い
				LOCAL:7 += 2
			SIF 締り具合名称(BASE:(ARG:1):Ａ緩さ) == "きゅっきゅっ"
				LOCAL:7 -= 1
			SIF 締り具合名称(BASE:(ARG:1):Ａ緩さ) == "きゅうきゅう"
				LOCAL:7 -= 3
			SIF 締り具合名称(BASE:(ARG:1):Ａ緩さ) == "がばがば"
				LOCAL:7 -= 5
			SIF 締り具合名称(BASE:(ARG:1):Ａ緩さ) == "ぽっかり"
				LOCAL:7 -= 7
			;手淫
		CASE 3
			SIF TALENT:(ARG:1):器用な指
				LOCAL:7 += 2
			;口淫
		CASE 4
			SIF TALENT:(ARG:1):舌使い
				LOCAL:7 += 2
			SIF TALENT:(ARG:1):猫舌
				LOCAL:7 -= 2
			;パイズリフェラ
			SIF TFLAG:50 == 12
				LOCAL:7 += TALENT:(ARG:1):バストサイズ
			;ついでにお掃除フラグ
			IF ABL:(ARG:1):奉仕精神 >= 2 && ABL:(ARG:1):技巧 >= 2 && TEQUIP:(ARG:1):ボールギャグ == 0 && EXP:(ARG:1):精飲経験 > 20
				STAIN:ARG:Ｐ = 汚れ_ペニス
				IF TFLAG:50 == 9 && ARG:1 == MASTER_POSE(4,1,1)
					TFLAG:8 |= 2
				ELSEIF ARG:1 == TARGET
					TFLAG:8 |= 1
				ENDIF
			ENDIF
			;パイズリ
		CASE 5
			LOCAL:7 += TALENT:(ARG:1):バストサイズ
			;パイズリフェラ
			IF TFLAG:50 == 12
				SIF TALENT:(ARG:1):舌使い
					LOCAL:7 += 2
				SIF TALENT:(ARG:1):猫舌
					LOCAL:7 -= 2
			ENDIF
			;お掃除フラグ
			IF ABL:(ARG:1):奉仕精神 >= 2 && ABL:(ARG:1):技巧 >= 2 && TEQUIP:(ARG:1):ボールギャグ == 0 && EXP:(ARG:1):精飲経験 > 20
				STAIN:ARG:Ｐ = 汚れ_ペニス
				IF ARG:1 == ASSI
					TFLAG:8 |= 2
				ELSEIF ARG:1 == TARGET
					TFLAG:8 |= 1
				ENDIF
			ENDIF
			;素股
		CASE 6
			;足コキ
		CASE 7
			SIF TALENT:(ARG:1):サド
				LOCAL:7 += 2
			;体表(泡踊り)
		CASE 8
			;アナル奉仕
		CASE 9
			SIF TALENT:(ARG:1):舌使い
				LOCAL:7 += 2
			SIF TALENT:(ARG:1):猫舌
				LOCAL:7 -= 2
			;道具
		CASE 10
			;触手
		CASE 11
	ENDSELECT
	LOCAL:10 = TCVAR:ARG:4 * LOCAL:1 * LOCAL:2 * LOCAL:3 * LOCAL:4 * LOCAL:5 * LOCAL:6 * LOCAL:7 / 10000000 * (ARG:2 + 1)
	LOCAL:10 = LOCAL:10 * (BASE:ARG:精力 + 200) / (MAXBASE:ARG:精力 + 200)
	LOCAL:11 = LOCAL:10
	SELECTCASE BASE:ARG:勃起
		CASE IS < 300
			LOCAL:10 /= 3
		CASE IS < 500
			LOCAL:10 /= 2
		CASE IS > 1000
			LOCAL:10 = LOCAL:10 * 2 / 3
		CASEELSE
	ENDSELECT
	BASE:ARG:勃起 = BASE:ARG:勃起 + LOCAL:10 / 5
	SELECTCASE BASE:ARG:勃起
		CASE IS < 500
			LOCAL:11 /= 5
		CASE IS > 1000
			LOCAL:11 /= 2
	ENDSELECT
	BASE:ARG:射精 += LOCAL:11

;-------------------------------------------------------------------------------
;射精確定後の処理
@EJAC_CHECK_AFTER(ARG)
	VARSET LOCAL
	;ARG 射精する人
	IF !TCVAR:ARG:イきそう
		IF BASE:ARG:射精 > MAXBASE:ARG:射精
			IF BASE:ARG:勃起 < 1000
				BASE:ARG:射精 = MAXBASE:ARG:射精 - 1
			ELSE
				TCVAR:ARG:イきそう = 1
			ENDIF
		ELSE
			RETURN 0
		ENDIF
		RETURN 0
	ENDIF
	;精力が減少する前の値で取得
	TRYCALL 精力取得, ARG
	LOCAL = MIN(BASE:ARG:射精 / MAXBASE:ARG:射精, (100 + BASE:ARG:精力) / 100)
	IF TALENT:ARG:157 == 1
	BASE:ARG:精力 = 1000
	ELSE
	BASE:ARG:精力 = MAX(0,BASE:ARG:精力 - BASE:ARG:射精 * (BASE:ARG:精力 + 500 ) / ((MAXBASE:ARG:精力 + 500 ) * 100))
	ENDIF
	TCVAR:ARG:イきそう = 0
	STAIN:ARG:Ｐ |= 汚れ_精液
	;BASEの処理
	BASE:ARG:2 = 0
	CSTR:ARG:11 = %CSTR:ARG:10%
	TCVAR:ARG:12 = TCVAR:ARG:2
	TCVAR:ARG:13 = TCVAR:ARG:3
	;異常経験、射精経験、精液経験
	SIF !EXP:ARG:射精経験 && HAS_VAGINA(ARG)
		EXP:ARG:異常経験 += 1
	EXP:ARG:絶頂経験 ++ 
	EXP:ARG:射精経験 += LOCAL
	EXP:ARG:精液経験 += LOCAL - 1
	NOWEX:ARG:射精 += LOCAL
	EX:ARG:射精 += LOCAL
	;ビデオ
	IF TEQUIP:ビデオ撮影
		IF ARG == MASTER
			IF LOCAL == 2
				LOCALS = SM/SM/
			ELSE
				LOCALS = SM/
			ENDIF
			TSTR:1 += LOCALS
		ELSEIF ARG == TARGET
			IF LOCAL == 2
				LOCALS = ST/ST/
			ELSE
				LOCALS = ST/
			ENDIF
			TSTR:1 += LOCALS
		ELSEIF ARG == ASSI
			IF LOCAL == 2
				LOCALS = SA/SA/
			ELSE
				LOCALS = SA/
			ENDIF
			TSTR:1 += LOCALS
		ENDIF
	ENDIF
	IF TALENT:ARG:157 == 1
		;
	ELSE
	DOWNBASE:ARG:体力 += 20 * LOCAL - 1
	DOWNBASE:ARG:気力 += 40 + 60 * (LOCAL - 1)
	ENDIF
	;TARGETにはBASEやSOURCE等の処理を入れる
	SIF ARG == MASTER
		RETURN 0
	IF EXP:ARG:射精経験 < EXPLV:1
		SOURCE:ARG:露出 += 5000 * (LOCAL + 1) / 2
		SOURCE:ARG:屈従 += 2500 * (LOCAL + 1) / 2
		SOURCE:ARG:不潔 += 900 * (LOCAL + 1) / 2
	ELSEIF EXP:ARG:射精経験 < EXPLV:2
		SOURCE:ARG:露出 += 2500 * (LOCAL + 1) / 2
		SOURCE:ARG:屈従 += 2000 * (LOCAL + 1) / 2
		SOURCE:ARG:不潔 += 700 * (LOCAL + 1) / 2
	ELSEIF EXP:ARG:射精経験 < EXPLV:3
		SOURCE:ARG:露出 += 1000 * (LOCAL + 1) / 2
		SOURCE:ARG:屈従 += 800 * (LOCAL + 1) / 2
		SOURCE:ARG:不潔 += 500 * (LOCAL + 1) / 2
	ELSEIF EXP:ARG:射精経験 < EXPLV:4
		SOURCE:ARG:露出 += 800 * (LOCAL + 1) / 2
		SOURCE:ARG:屈従 += 600 * (LOCAL + 1) / 2
		SOURCE:ARG:不潔 += 350 * (LOCAL + 1) / 2
	ELSEIF EXP:ARG:射精経験 < EXPLV:5
		SOURCE:ARG:露出 += 400 * (LOCAL + 1) / 2
		SOURCE:ARG:屈従 += 400 * (LOCAL + 1) / 2
		SOURCE:ARG:不潔 += 200 * (LOCAL + 1) / 2
	ELSE
		SOURCE:ARG:露出 += 200 * (LOCAL + 1) / 2
		SOURCE:ARG:屈従 += 250 * (LOCAL + 1) / 2
	ENDIF

;-------------------------------------------------
;調教対象の射精チェック
;-------------------------------------------------
@TARGET_EJAC_CHECK(ARG)
	SIF !HAS_PENIS(TARGET)
		RETURN 0
	LOCAL = (CUP:ARG:快Ｃ + CUP:ARG:快Ｖ + CUP:ARG:快Ａ + CUP:ARG:快Ｂ) / 4
	;自制心
	SIF TALENT:ARG:自制心
		LOCAL /= 2
	;快感に素直
	SIF TALENT:ARG:快感応答 > 0
		TIMES LOCAL , 1.20
	;快感の否定（淫乱、娼婦で無効）
	SIF TALENT:ARG:快感応答 < 0 && !TALENT:ARG:淫乱
		TIMES LOCAL , 0.80
	;媚薬
	SIF TEQUIP:ARG:媚薬
		LOCAL *= 2
	;利尿剤
	SIF TEQUIP:ARG:利尿剤
		LOCAL /= 2
	BASE:ARG:2 += LOCAL
	SIF BASE:ARG:2 > MAXBASE:ARG:2
		BASE:ARG:2 = MAXBASE:ARG:2 - 1

;-------------------------------------------------------------------------------
;射精処理のキャラ別ターミナル
@SAMEN_SHOOT(ARG)
	;ARG 射精したキャラ番号
	FOR LOCAL,0,CHARANUM
		SIF SAMEN_DIRECTION2(ARG,LOCAL)
			CALL SAMEN_SHOOT2(ARG,LOCAL)
	NEXT

;-------------------------------------------------------------------------------
;部位別処理
@SAMEN_SHOOT2(ARG,ARG:1)
	;ARG 射精したキャラ番号
	;ARG:1 射精されたキャラ番号
	;TCVAR:12 射精箇所フラグ（1=膣内 2=アナル 3=手淫 4=口淫 5=パイズリ 6=素股 7=足コキ 8=体表 9=アナル奉仕 10=道具 11=触手）
	SIF TCVAR:ARG:13
		RETURN 0
	SELECTCASE TCVAR:ARG:12
			;膣内
		CASE 1
			CALL EJACULATION_V((ARG:1), ARG)
			;ビデオ
			IF TEQUIP:28
				LOCALS = SV/
				TSTR:1 += LOCALS
			ENDIF
			;アナル
		CASE 2
			CALL EJACULATION_A((ARG:1), ARG)
			;手淫
		CASE 3
			EXP:(ARG:1):精液経験 += 1 + NOWEX:ARG:射精
			CALL STAIN_ADD_SEMEN_HAND((ARG:1), ARG)
			IF TFLAG:50 != 16
			SIF ABL:(ARG:1):精液中毒 > 2 && ABL:(ARG:1):技巧 > 2
				EXP:(ARG:1):精飲経験 ++
			ENDIF
			;接触のリセット
			TEQUIP:ARG:指 = 0
			;口淫
		CASE 4
			CALL EJACULATION_M((ARG:1), ARG)
			;イラマチオ
			IF SELECTCOM == 140
				EXP:(ARG:1):精飲経験 += NOWEX:ARG:射精 * 2
			ELSEIF  RAND:(MIN(60,EXP:(ARG:1):精飲経験 + 1)) > 10 + RAND:30
				EXP:(ARG:1):精飲経験 += NOWEX:ARG:射精 * 2
			ELSE
				EXP:(ARG:1):精飲経験 += NOWEX:ARG:射精
			ENDIF
			;接触のリセット
			TEQUIP:ARG:口 = 0
			;パイズリ
		CASE 5
			EXP:(ARG:1):精液経験 += NOWEX:ARG:射精 * 3
			CALL STAIN_ADD_SEMEN_B((ARG:1), ARG)
			SIF TFLAG:50 == 12
				STAIN:(ARG:1):口 |= 汚れ_精液
			;接触のリセット
			TEQUIP:ARG:胸 = 0
			;素股
		CASE 6
			EXP:(ARG:1):精液経験 += NOWEX:ARG:射精
			CALL STAIN_ADD_SEMEN_Sumata((ARG:1), ARG)
			;接触のリセット
			TEQUIP:ARG:クリトリス = 0
			;足コキ
		CASE 7
			EXP:(ARG:1):精液経験 += NOWEX:ARG:射精
			;体表
		CASE 8
			EXP:(ARG:1):精液経験 += NOWEX:ARG:射精 * 2
			;コンドーム
		CASE -1
	ENDSELECT
