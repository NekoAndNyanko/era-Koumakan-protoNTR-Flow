;-------------------------------------------------------------------------------
;押し倒す
@COM350
	#DIM 奴隷
	#DIM LOOP_I
	;押し倒す
	FOR LOOP_I,1,CHARANUM
		SIF TARGET:LOOP_I <= 0
			CONTINUE
		奴隷 = TARGET:LOOP_I
		;NGの場合もあるので後でフラグを立てる。
		;CFLAG:奴隷:うふふ = 1
		CALL CHK_350_WC_KEY(奴隷)
		IF 0 == RESULT
			RETURN 0
		ENDIF
		; 浮気非公認の恋慕住人の場合、あなた以外の相手の精液を受けている場合拒絶される
		CALL CHK_350_STAIN_SEMEN_MES(奴隷)
		IF 0 == RESULT
			RETURN 0
		ENDIF
	NEXT
	;全員成功時、うふふフラグを立てる。
	FOR LOOP_I,1,CHARANUM
		SIF TARGET:LOOP_I <= 0
			CONTINUE
		奴隷 = TARGET:LOOP_I
		CFLAG:奴隷:うふふ = 1
	NEXT
	CFLAG:MASTER:うふふ = 1
	TIME += 5
RETURN 1

;公衆便所押し倒し可否チェック
;  戻り値： 1: 押し倒し成功
;           0: 押し倒し失敗
@CHK_350_WC_KEY(奴隷)
	#DIM 奴隷
	IF TALENT:奴隷:公衆便所 && EQUIP:奴隷:下半身下着１ == 100
		IF FLAG:貞操帯鍵購入フラグ == 奴隷
			;公衆便所相手に鍵を買っていた場合
			IF CFLAG:奴隷:NTR貞操帯反応
				;公衆便所自身が貞操帯の鍵を認識していない場合
				IF TCVAR:奴隷:馴れ合い強度 < 2
					;公衆便所がその気になってないのに押し倒した場合
					PRINTFORML %CALLNAME:奴隷%は思わず%CALLNAME:MASTER%を押し返そうとしたが、
					PRINTFORML 貞操帯の鍵をちらつかせてやると、身体の力を抜いて%CALLNAME:MASTER%を受け入れた。
					CFLAG:奴隷:ぱんつ確認 = 1
				ELSE
					;公衆便所がその気になってる時に押し倒した場合
					PRINTFORML %CALLNAME:奴隷%は貞操帯に触れ、%CALLNAME:MASTER%はいつ私を買ってくれるのか、と訊ねた。
					PRINTFORML 買ったばかりの鍵を見せてやると、%CALLNAME:奴隷%はその脚を開いて鍵穴を指さした。
					CFLAG:奴隷:ぱんつ確認 = 1
				ENDIF
				CALL NTR_LOCK_REACTION(奴隷)
			ELSE
				;公衆便所自身が貞操帯の鍵を認識している場合
				IF TCVAR:奴隷:馴れ合い強度 < 2
					;公衆便所がその気になってないのに押し倒した場合
					PRINTFORML %CALLNAME:奴隷%はわずかに抵抗する様子を見せたが、すぐに身体の力を抜いて%CALLNAME:MASTER%を受け入れた。
				ELSE
					;公衆便所がその気になってる時に押し倒した場合
					PRINTFORML %CALLNAME:奴隷%は、%CALLNAME:MASTER%の前に貞操帯を晒し、早く鍵を開けてくれとねだった。
					CFLAG:奴隷:ぱんつ確認 = 1
				ENDIF
			ENDIF
		ELSE
			PRINTFORML %CALLNAME:MASTER+"が迫ると"+CALLNAME:奴隷+"は鍵のかけられた貞操帯を見せ、『"+CALLNAME:奴隷+"』は"+NTR_NAME(0)+"の商品なのだから、使いたければ"+NTR_NAME(0)+"から鍵を買わなければダメだと断った。"%
			CFLAG:奴隷:ぱんつ確認 = 1
			CFLAG:奴隷:うふふ = 0
			CFLAG:MASTER:うふふ = 0
			TIME += 5
			RETURN 0
		ENDIF
	ENDIF
RETURN 1

;精液汚れによる押し倒し可否チェック(メッセージあり)
;　恋慕持ち、かつ浮気非公認の住人の場合、あなた以外の精液汚れを隠そうとする。
;  戻り値： 1: 押し倒し成功
;           0: 押し倒し失敗
@CHK_350_STAIN_SEMEN_MES(奴隷)
	#DIM 奴隷
	;コンフィグによる抑制時は常に成功
	IF !IS_NTR_ESCAPE()
		RETURN 1
	ENDIF
	IF TALENT:奴隷:恋慕 && 0 == TALENT:奴隷:浮気公認
		; 汚れ部位とあなたの射精履歴が一致しない
		IF 0 == CHK_350_STAIN_SEMEN(奴隷)
			;射精者が訪問者であれば滞在（会話→浮気公認）
			IF 人物_訪問者 == CFLAG:奴隷:射精者M || 人物_訪問者 == CFLAG:奴隷:射精者V || 人物_訪問者 == CFLAG:奴隷:射精者A
				PRINTFORML %CALLNAME:MASTER+"が迫ると"+CALLNAME:奴隷+"は気まずそうに今は汗をかいているからと断った。"%
				PRINTFORML %"不審に思った"+CALLNAME:MASTER+"が無理に押し倒すと、"+CALLNAME:奴隷+"の身体に情事の痕跡を発見した。"%
				CFLAG:奴隷:汚れ発覚 = 汚れ発覚_あり
			;射精者が訪問者でなければ逃走
			ELSE
				PRINTFORML %CALLNAME:MASTER+"が迫ると"+CALLNAME:奴隷+"は慌てたような感じで今は汗をかいているから先にお風呂に入りたいと断った。"%
				CFLAG:奴隷:汚れ発覚 = 汚れ発覚_逃走
			ENDIF
			CFLAG:奴隷:うふふ = 0
			CFLAG:MASTER:うふふ = 0
			TIME += 5
			RETURN 0
		ENDIF
	ENDIF
RETURN 1

;精液汚れによる押し倒し可否チェック(メッセージなし)
;　恋慕持ち、かつ浮気非公認の住人の場合、あなた以外の精液汚れを隠そうとする。
;  戻り値： 1: 押し倒し成功
;           0: 押し倒し失敗
@CHK_350_STAIN_SEMEN(奴隷)
	#FUNCTION
	#DIM 奴隷
	IF TALENT:奴隷:恋慕 && 0 == TALENT:奴隷:浮気公認
		; 口,V,B,A のいずれかに精液汚れあり
		IF GETBIT(STAIN:奴隷:部位_口, 汚れB_精液) || GETBIT(STAIN:奴隷:部位_Ｖ, 汚れB_精液) || GETBIT(STAIN:奴隷:部位_Ａ, 汚れB_精液) || GETBIT(STAIN:奴隷:部位_Ｂ, 汚れB_精液)
			; 本日あなたによる射精履歴なし
			IF 0 == CFLAG:奴隷:あなた射精フラグ
				RETURNF 0
			ENDIF
		ENDIF
	ENDIF
RETURNF 1


;精液汚れによる押し倒し可否チェックの部位別チェック(未使用)
;　あなたが射精した箇所が汚れている場合スルー
@CHK_350_STAIN(奴隷, 部位)
	#FUNCTION
	#DIM 奴隷
	#DIM 部位

	SELECTCASE 部位
		CASE 被射精箇所_口
			IF !GETBIT(CFLAG:奴隷:あなた射精フラグ, 部位) && GETBIT(STAIN:奴隷:部位_口, 汚れB_精液)
				RETURNF 0
			ENDIF
		CASE 被射精箇所_Ｖ
			IF !GETBIT(CFLAG:奴隷:あなた射精フラグ, 部位) && GETBIT(STAIN:奴隷:部位_Ｖ, 汚れB_精液)
				RETURNF 0
			ENDIF
		CASE 被射精箇所_Ａ
			IF !GETBIT(CFLAG:奴隷:あなた射精フラグ, 部位) && GETBIT(STAIN:奴隷:部位_Ａ, 汚れB_精液)
				RETURNF 0
			ENDIF
		CASE 被射精箇所_Ｂ
			IF !GETBIT(CFLAG:奴隷:あなた射精フラグ, 部位) && GETBIT(STAIN:奴隷:部位_Ｂ, 汚れB_精液)
				RETURNF 0
			ENDIF
	ENDSELECT
RETURNF 1
