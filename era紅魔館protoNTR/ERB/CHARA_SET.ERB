;option 12
;キャラクター設定系
@CHARA_RESET
#DIM select
PRINTL 
PRINTL [1]キャラクターの名前を変更する
PRINTFORML [2]キャラクターを個別に初期化する
PRINTFORML [3]キャラクター全てを初期化する (%CALLNAME:MASTER%以外)
PRINTL [999]-戻る

$INPUT_LOOP
INPUT
IF RESULT == 1
	$RE_LOOP1
	PRINTL 
	PRINTL どちらを変更しますか？
	PRINTL [1] NAME - 名前
	PRINTL [2] CALLNAME - 通称
	PRINTL [999]-戻る
	
	$INPUT_LOOP1
	INPUT
	IF RESULT == 1
		PRINTL 
		PRINTL NAMEを変更するキャラクターを選択してください
		FOR LOCAL, 0, CHARANUM
			PRINTFORM [{LOCAL, 3}]%NAME:LOCAL, 16, LEFT%
			SETCOLOR 色設定_テキスト_備考
			PRINTFORML (%CALLNAME:LOCAL%)
			RESETCOLOR
		NEXT
		PRINTL [999]-戻る
		$INPUT_LOOP1_1
		INPUT
		IF RESULT >= 0 && RESULT < CHARANUM
			LOCAL = RESULT
			PRINTL 
			PRINTFORM %NAME:LOCAL%の名前を入力してください
			SETCOLOR 色設定_テキスト_備考
			PRINTL 　　(無入力でデフォルトに戻す)
			RESETCOLOR
			INPUTS
			IF STRLENS(RESULTS) > 30
				PRINTW 名前が長すぎます
				GOTO INPUT_LOOP1_1
			ELSEIF STRLENS(RESULTS) == 0
				NAME:LOCAL = %CSVNAME(NO:LOCAL)%
				PRINTFORMW %NAME:LOCAL%と命名しました
				PRINTL 
			ELSE
				NAME:LOCAL = %RESULTS%
				PRINTFORMW %NAME:LOCAL%と命名しました
				PRINTL 
			ENDIF
		ELSEIF RESULT != 999
			GOTO INPUT_LOOP1_1
		ENDIF
		GOTO RE_LOOP1
	ELSEIF RESULT == 2
		PRINTL 
		PRINTL CALLNAMEを変更するキャラクターを選択してください
		FOR LOCAL, 0, CHARANUM
			PRINTFORML [{LOCAL, 3}]%CALLNAME:LOCAL, 16, LEFT%
		NEXT
		PRINTL [999]-戻る
		$INPUT_LOOP2_1
		INPUT
		IF RESULT >= 0 && RESULT < CHARANUM
			LOCAL = RESULT
			PRINTL 
			PRINTFORM %CALLNAME:LOCAL%の名前を入力してください
			SETCOLOR 色設定_テキスト_備考
			PRINTL 　　(無入力でデフォルトに戻す)
			RESETCOLOR
			INPUTS
			IF STRLENS(RESULTS) > 20
				PRINTW 名前が長すぎます
				GOTO INPUT_LOOP2_1
			ELSEIF STRLENS(RESULTS) == 0
				CALLNAME:LOCAL = %CSVCALLNAME(NO:LOCAL)%
				PRINTFORMW %CALLNAME:LOCAL%と命名しました
				PRINTL 
			ELSE
				CALLNAME:LOCAL = %RESULTS%
				PRINTFORMW %CALLNAME:LOCAL%と命名しました
				PRINTL 
			ENDIF
		ELSEIF RESULT != 999
			GOTO INPUT_LOOP2_1
		ENDIF
		GOTO RE_LOOP1
	ELSEIF RESULT != 999
		GOTO INPUT_LOOP1
	ENDIF
ELSEIF RESULT == 2
	$RE_LOOP2
	PRINTL 
	PRINTL 初期化する設定とキャラクターを選択してください
	PRINTL 
	PRINTL - 設定
	PRINT [0] 処女設定：
	IF select & 1
		SETCOLOR 色設定_設定_肯定的
		PRINTFORML あり
	ELSE
		SETCOLOR 色設定_設定_無効化
		PRINTFORML なし
	ENDIF
	RESETCOLOR
	PRINTL 
	FOR LOCAL, 1, CHARANUM
		PRINTFORM [{LOCAL, 3}]%NAME:LOCAL, 16, LEFT%
		SETCOLOR 色設定_テキスト_備考
		PRINTFORML (%CALLNAME:LOCAL%)
		RESETCOLOR
	NEXT
	PRINTL [999]-戻る
	$INPUT_LOOP_S
	INPUT
	IF RESULT == 0
		select += (select & 1) ? -1 # 1
		GOTO RE_LOOP2
	ELSEIF RESULT > 0 && RESULT < CHARANUM
		LOCAL = RESULT
		LOCALS = %CALLNAME:LOCAL%
		ADDCHARA LOCAL
		SWAPCHARA LOCAL, CHARANUM-1
		DELCHARA CHARANUM-1
		IF select & 1
			TALENT:LOCAL:処女 = 1
			EXP:LOCAL:Ｖ経験 = 0
			EXP:LOCAL:Ｖ性交経験 = 0
			EXP:LOCAL:Ａ性交経験 = 0
			ABL:LOCAL:Ｖ感覚 = 0
			ABL:LOCAL:Ａ感覚 = 0
		ENDIF
		IF 人物_美鈴 <= LOCAL && LOCAL <= 人物_魔理沙
			TRYCALLFORM 体詳細初期設定{LOCAL}(LOCAL)
		ENDIF
		PRINTFORMW %LOCALS%を初期化しました
		PRINTL 
	ELSEIF RESULT != 999
		GOTO INPUT_LOOP_S
	ENDIF
ELSEIF RESULT == 3
	$RE_LOOP3
	PRINTL 
	PRINTL あなた以外のキャラクターを初期化しますか？
	PRINT [0] 処女設定：
	IF select & 1
		SETCOLOR 色設定_設定_肯定的
		PRINTFORML あり
	ELSE
		SETCOLOR 色設定_設定_無効化
		PRINTFORML なし
	ENDIF
	RESETCOLOR
	PRINTL 
	PRINTL [1] はい
	PRINTL [2] やめる
	$INPUT_LOOP_S2
	INPUT
	IF RESULT == 0
		select += (select & 1) ? -1 # 1
		GOTO RE_LOOP3
	ELSEIF RESULT == 1
		FOR LOCAL,1,CHARANUM
			DELCHARA 1
		NEXT
		FOR LOCAL,1,開始時人数
			ADDCHARA LOCAL
			IF select & 1
				TALENT:LOCAL:処女 = 1
				EXP:LOCAL:Ｖ経験 = 0
				EXP:LOCAL:Ｖ性交経験 = 0
				EXP:LOCAL:Ａ性交経験 = 0
				ABL:LOCAL:Ｖ感覚 = 0
				ABL:LOCAL:Ａ感覚 = 0
			ENDIF
			IF 人物_美鈴 <= LOCAL && LOCAL <= 人物_ルーミア
				TRYCALLFORM 体詳細初期設定{LOCAL}(LOCAL)
			ENDIF
		NEXT
		PRINTFORMW 初期化しました
		PRINTL 
	ELSEIF RESULT != 2
		GOTO INPUT_LOOP_S2
	ENDIF
ENDIF
