;-------------------------------------------------
;胸愛撫
;愛撫系コマンド、レベル2
;-------------------------------------------------
@COM6

TRYCALL COM417_is_Protected(PLAYER,TARGET,娼婦紋_胸元)
SIF RESULT
	RETURN 0

;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;行為者
TCVAR:116 = PLAYER
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:体力 += 5
DOWNBASE:気力 += 50

SOURCE:快Ｂ = 100
SOURCE:情愛 = 50
SOURCE:性行動 = 60
SOURCE:露出 = 10
SOURCE:不潔 = 20
SOURCE:反感 = 20
;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:自己愛 < 0 || TALENT:抵抗
	SOURCE:鬱屈 += 20
;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷の胸の汚れがなければ口をつかう（愛液と母乳よごれはＯＫ、汚れ無視か助手調教時は問題なし）
IF STAIN:Ｂ < 汚れ_ペニス || STAIN:Ｂ == 汚れ_母乳 || STAIN:Ｂ == 汚れ_母乳_愛液 || ASSIPLAY == 1 || TALENT:MASTER:汚臭耐性 == 2
	;調教者のTALENT:舌使いをみる
	TIMES SOURCE:不潔 , 1.40
	SIF TALENT:PLAYER:舌使い
		TIMES SOURCE:快Ｂ , 1.40
	SOURCE:鬱屈 += SOURCE:不潔 / 20
	;奴隷のＢ⇔調教者の口の汚れが移動
	STAIN:Ｂ |= STAIN:PLAYER:口
	STAIN:PLAYER:口 |= STAIN:Ｂ
ENDIF
;奴隷のＢ⇔調教者の指の汚れが移動
STAIN:Ｂ |= STAIN:PLAYER:手
STAIN:PLAYER:手 |= STAIN:Ｂ

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:(50 + HETEROSEX(TARGET,PLAYER)) += 5
	EXP:PLAYER:(50 + HETEROSEX(TARGET,PLAYER)) += 5
ENDIF

RETURN 1
